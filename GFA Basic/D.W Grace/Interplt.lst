' GENERAL RESCALING OF ARRAY DATA
' TESTED USING SIMPLE VIDEO (BITMAP) FEEDBACK
'
FOR n|=0 TO 7
  VSETCOLOR n|,n|,0,0
NEXT n|
FOR n|=0 TO 7
  VSETCOLOR n|+8,7,n|,0
NEXT n|
' VSETCOLOR 0,1911
'
x_res&=50
y_res&=50
x_max&=x_res&-1
y_max&=y_res&-1
'
DIM cur&(x_max&,y_max&),new&(x_max&,y_max&)
DIM play_back$(50)
'
' DRAW BORDER
COLOR 8
PLOT 25,25
LINE x_res&,0,x_res&,y_res&
LINE 0,y_res&,x_res&,y_res&
'
' TEST PICTURE:
REPEAT
  IF MOUSEK=1
    PLOT MOUSEX,MOUSEY
  ENDIF
UNTIL MOUSEK=2
'
' TEST SCALING
scale=0.96
v1=(1-scale)*x_res&/2
v2=(1-scale)*y_res&/2
'
' m11=scale
' m12=0
' m21=0
' m22=scale
'
m11=scale*COSQ(1)
m12=-scale*SINQ(1)
m21=scale*SINQ(1)
m22=scale*COSQ(1)
'
' TEST LOOP - CRAP VIDEO FEEDBACK *********************************************
'
FOR frame|=0 TO 39
  '
  ' GET CURRENT SCREEN INTO cur&()
  FOR x&=0 TO x_max&
    FOR y&=0 TO y_max&
      cur&(x&,y&)=POINT(x&,y&)
    NEXT y&
  NEXT x&
  '
  ' RESCALE cur&() INTO new&()
  enlarge(cur&(),new&())
  '
  ' DRAW new&()
  DEFFILL 0
  PBOX 0,0,x_max&,y_max&
  FOR x&=0 TO x_max&
    FOR y&=0 TO y_max&
      COLOR new&(x&,y&)
      PLOT x&,y&
    NEXT y&
  NEXT x&
  GET 0,0,x_max&,y_max&,play_back$(frame|)
  PRINT AT(1,15);"FRAME = ";frame|
  '
NEXT frame|
'
PRINT "PRESS A KEY"
WHILE INKEY$=""
WEND
'
REPEAT
  CLS
  FOR frame|=0 TO 39
    VSYNC
    PUT 100,120,play_back$(frame|)
    PAUSE 3
  NEXT frame|
  WHILE INKEY$=""
  WEND
UNTIL INKEY$="Q"
'
> PROCEDURE enlarge(VAR a&(),b&())
  '
  ' mat() AND vec() DESCRIBE THE INVERSE MAP: b&() -> a&().
  '
  LOCAL x&,y&,x0&,y0&,x1&,y1&,dx,dy,dx1,dy1,temp
  '
  FOR x&=0 TO x_max&
    '
    ' SPEED OPTIMAISATION VARIABLE
    x_p1&=x&+1
    '
    FOR y&=0 TO y_max&
      '
      ' SPEED OPTIMAISATION VARIABLE
      y_p1&=y&+1
      '
      ' GET SOURCE CO-ORDINATES x,y (IN a&()) FROM x&,y& (IN b&())
      x=m11*x&+m12*y&+v1
      y=m21*x&+m22*y&+v2
      '
      x0&=x
      y0&=y
      x1&=x0&+1
      y1&=y0&+1
      dx=FRAC(x)
      dy=FRAC(y)
      dx1=1-dx
      dy1=1-dy
      '
      ' INTERPOLATE ARRAY DATA FROM FOUR CO-ORDS (IN a&())
      '
      temp=dx1*dy1*a&(x0&,y0&)+dx*dy1*a&(x1&,y0&)
      ADD temp,dx1*dy*a&(x0&,y1&)+dx*dy*a&(x1&,y1&)
      '
      '      b&(x&,y&)=temp+0.5                ! ordinary
      IF temp<1
        b&(x&,y&)=0
      ELSE
        b&(x&,y&)=temp+RND*2-0.2                   ! with noise
      ENDIF
      '
    NEXT y&
  NEXT x&
  '
RETURN
