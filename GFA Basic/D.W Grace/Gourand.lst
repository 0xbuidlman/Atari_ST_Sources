FOR n&=1 TO 7
  VSETCOLOR n&,n&,n&,0
NEXT n&
'
REPEAT
  gourand(RND*319,RND*199,RND*319,RND*199,RND*319,RND*199,1,0,0)
  CLS
UNTIL MOUSEK=2
'
PROCEDURE gourand(x1&,y1&,x2&,y2&,x3&,y3&,i1,i2,i3)
  '
  LOCAL x&,y&,x_dif0&,y_dif0&,x_dif1&,y_dif1&,line_step&,point_step&,n&,m&
  LOCAL x_l0,y_l0,x_l1,y_l1,x_dif,y_dif
  LOCAL i_l0,xl0_step,yl0_step,il0_step
  LOCAL i_l1,xl1_step,yl1_step,il1_step
  LOCAL i,i_step,x_step,y_step
  '
  COLOR 8
  LINE x1&,y1&,x2&,y2&
  LINE x1&,y1&,x3&,y3&
  LINE x3&,y3&,x2&,y2&
  ' INITIALISE LINE START VARIABLES.
  i_l0=i1
  x_l0=x1&
  y_l0=y1&
  '
  ' INITIALISE LINE END VARIABLES.
  i_l1=i2
  x_l1=x2&
  y_l1=y2&
  '
  ' DETERMINE LINE STEP
  x_dif0&=x3&-x1&
  y_dif0&=y3&-y1&
  x_dif1&=x3&-x2&
  y_dif1&=y3&-y2&
  line_step&=MAX(ABS(x_dif0&),ABS(y_dif0&),ABS(x_dif1&),ABS(y_dif1&))+1
  '
  ' INITIALISE LINE START STEP VARIABLES
  '
  IF x_dif0&=0 OR line_step&=0
    x_l0_step=0
  ELSE
    x_l0_step=x_dif0&/line_step&
  ENDIF
  '
  IF y_dif0&=0 OR line_step&=0
    y_l0_step=0
  ELSE
    y_l0_step=y_dif0&/line_step&
  ENDIF
  '
  IF line_step&=0
    i_l0_step=0
  ELSE
    i_l0_step=(i3-i1)/line_step&
  ENDIF
  '
  ' INITIALISE LINE END STEP VARIABLES
  '
  IF x_dif1&=0 OR line_step&=0
    x_l1_step=0
  ELSE
    x_l1_step=x_dif1&/line_step&
  ENDIF
  '
  IF y_dif1&=0 OR line_step&=0
    y_l1_step=0
  ELSE
    y_l1_step=y_dif1&/line_step&
  ENDIF
  '
  IF line_step&=0
    i_l1_step=0
  ELSE
    i_l1_step=(i3-i2)/line_step&
  ENDIF
  '
  FOR n&=0 TO line_step&
    '
    ' INITIALISE POINT VARIABLES.
    x=x_l0
    y=y_l0
    i=i_l0
    '
    ' INITIALISE POINT STEP VARIABLES (dif0& VARIABLES ARE REUSED)
    x_dif=x_l1-x_l0
    y_dif=y_l1-y_l0
    point_step=MAX(ABS(x_dif),ABS(y_dif))+1
    '
    IF x_dif=0 OR point_step=0
      x_step=0
    ELSE
      x_step=x_dif/point_step
    ENDIF
    '
    IF y_dif=0 OR point_step=0
      y_step=0
    ELSE
      y_step=y_dif/point_step
    ENDIF
    '
    IF point_step=0
      i_step=0
    ELSE
      i_step=(i_l1-i_l0)/point_step
    ENDIF
    '
    point_step&=point_step
    FOR m&=0 TO point_step&
      '
      COLOR @intens(i,x,y,1,7)
      PLOT x,y
      '
      ' UPDATE POINT VARIABLES
      x=x+x_step
      y=y+y_step
      i=i+i_step
      '
      EXIT IF MOUSEK=1
      '
    NEXT m&
    '
    ' UPDATE LINE START VARIABLES
    x_l0=x_l0+x_l0_step
    y_l0=y_l0+y_l0_step
    i_l0=i_l0+i_l0_step
    '
    ' UPDATE LINE END VARIABLES
    x_l1=x_l1+x_l1_step
    y_l1=y_l1+y_l1_step
    i_l1=i_l1+i_l1_step
    '
  NEXT n&
  '
RETURN
'
FUNCTION intens(i,x&,y&,start_col|,num_cols|)
  '
  LOCAL col&
  '
  IF i=1
    RETURN num_cols|+start_col|-1
  ELSE
    '
    col&=2*i*(num_cols|)
    '
    IF col& MOD 2=0
      '
      IF (x&+y&) MOD 2=0
        RETURN col& DIV 2+start_col|
      ELSE
        RETURN col& DIV 2+start_col|-1
      ENDIF
      '
    ELSE
      '
      RETURN col& DIV 2+start_col|
      '
    ENDIF
    '
  ENDIF
  '
ENDFUNC
