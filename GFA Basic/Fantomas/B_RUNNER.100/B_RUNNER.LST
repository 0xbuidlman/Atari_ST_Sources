'
' ****************************************************************************
'
'              ð Slide Show d'images du film BLADE RUNNER + musique ð
'
'                              ½ Fantomas 1995
'
'                    DerniŠres modifs: 31 D‚cembre 1995
'
' ****************************************************************************
'
'
$C+
ON BREAK GOSUB fin
ON ERROR GOSUB fin
'
super%=GEMDOS(&H20,L:0)          ! Superviseur...
'
' ----------------------------------------------------------------------------
' Gestion de la m‚moire.
' ----------------------------------------------------------------------------
'
INLINE module%,22935
INLINE play_mod%,11850
INLINE depack%,366
INLINE buffer%,1024
INLINE scroll%,1090
'
ecran%=XBIOS(3)
RESERVE 10000                   ! Reservation m‚moire
mod%=MALLOC(40120)              ! Module soundtrack
bu1%=MALLOC(76800)              ! Image PI9
bu2%=MALLOC(76800)              ! Image PI9
ec1%=MALLOC(76800)              ! Ecran 1.
'
' ----------------------------------------------------------------------------
' Changement de r‚solution 320x200 256c.
' ----------------------------------------------------------------------------
'
res&=XBIOS(88,W:-1)                           ! R‚solution actuelle
moniteur%=XBIOS(89)                           ! Type de moniteur
IF moniteur%=2                                ! Moniteur VGA
  ~XBIOS(5,L:ec1%,L:ec1%,W:3,W:&X100110011)   ! VGA: 320x240 256c.
ELSE
  ~XBIOS(5,L:ec1%,L:ec1%,W:3,W:&X11)          ! TV:  320x200 256c.
ENDIF
'
' ----------------------------------------------------------------------------
' Initialisation des variables.
' ----------------------------------------------------------------------------
'
image%=bu2%+1024                   ! Donn‚es pour l'image aprŠs la palette
' ---> Player soundtrack.
dsp_relo%=play_mod%                ! Adr: relocation du player.
dsp_play%=play_mod%+10             ! Adr: joue module.
dsp_stop%=play_mod%+34             ! Adr: Stoppe module.
'
' ----------------------------------------------------------------------------
'
' ----------------------------------------------------------------------------
'
BYTE{&H484}=0                                    ! Repetition clavier
OUT 4,18                                         ! Souris OFF
'
' --------------
' Efface l'‚cran
' --------------
'
CLS
'
' ---------------------------
' Installe & Lance la musique
' ---------------------------
'
~C:depack%(L:module%,L:mod%)                   ! D‚compacte musique
CALL dsp_relo%                                 ! Relocation du player
~C:dsp_play%(L:mod%)                           ! On lance la musique
'
' -----------------------------
' Couleurs du texte et du cadre
' -----------------------------
'
' LPOKE &HFFFF9804,63     ! Texte
' LPOKE &HFFFF9800,63     ! Cadre
'
' ---------------------
' Image de pr‚sentation
' ---------------------
'
BLOAD "\B_RUNNER\DATAS\BLADE_00.FAN",bu1%
~C:depack%(L:bu1%,L:bu2%)                      ! Decompacte image
BMOVE bu2%,&HFFFF9800,1024
BMOVE image%,ec1%,76800-1024
REPEAT
UNTIL BYTE{&HFFFFFC02}=57
CLS
'
' -------------------------
' Affiche la premiere image
' -------------------------
'
n|=1
BLOAD "\B_RUNNER\DATAS\BLADE_01.FAN",bu1%
~C:depack%(L:bu1%,L:bu2%)                      ! Decompacte image
@rvb_noir
BMOVE image%,ec1%,61440
'
' --------------------------
' Scroll de texte 8x8 1 plan
' --------------------------
'
CALL scroll%
'
' ---------------------
' Slide Show des images
' ---------------------
'
REPEAT
  @noir_rvb
  @wait
  @rvb_noir
  INC n|
  IF n|<21
    n$=STR$(n|)
    IF LEN(n$)<2
      n$="0"+n$
    ENDIF
    nom$="BLADE_"+n$+".FAN"
    BLOAD "\B_RUNNER\DATAS\"+nom$,bu1%
    ~C:depack%(L:bu1%,L:bu2%)
    VSYNC
    BMOVE image%,ec1%,61440
    @wait
  ELSE
    n|=1
    BLOAD "\B_RUNNER\DATAS\BLADE_01.FAN",bu1%
    ~C:depack%(L:bu1%,L:bu2%)
    BMOVE image%,ec1%,61440
  ENDIF
UNTIL BYTE{&HFFFFFC02}=57                        ! Attente de [SPACE].
@fin                                             ! On Quitte le programme.
'
' *--------------------------------------------------------------------------*
'
'                              ð Procedures ð
'
' *--------------------------------------------------------------------------*
'
> PROCEDURE fin
  ' ----------------------------------------------------------------------------
  ' Fin du programme.
  ' ----------------------------------------------------------------------------
  CALL dsp_stop%                                 ! Stoppe la musique.
  LPOKE &H4D6,0
  BYTE{&H484}=key|                               ! Restaure clavier.
  OUT 4,8                                        ! On remet la souris.
  SHOWM                                          ! La revoil… !
  blow_res&=BSET(res&,15)                        ! Carte Blow Up
  ~XBIOS(5,L:ecran%,L:ecran%,W:3,W:blow_res&)    ! Ancienne  r‚solution.
  ~MFREE(mod%)                                   ! Rends la m‚moire au TOS
  ~MFREE(bu1%)                                   !
  ~MFREE(bu2%)                                   !
  ~MFREE(ec1%)                                   !
  ~GEMDOS(&H20,L:super%)                         !
  END                                            ! Fini, on retourne au bureau.
RETURN
> PROCEDURE wait
  ' ------------------------
  ' Pause entre chaque image
  ' ------------------------
  cmpt%=0
  REPEAT
    INC cmpt%
  UNTIL cmpt%=128000 OR BYTE{&HFFFFFC02}=57
RETURN
> PROCEDURE rvb_noir
  ' ---------------
  ' Couleur -> Noir
  ' ---------------
  BMOVE bu2%,buffer%,1024
  FOR f&=0 TO 63
    FOR n&=0 TO 254
      IF n&<>1
        r|=BYTE{buffer%+n&*4}
        v|=BYTE{buffer%+n&*4+1}
        b|=BYTE{buffer%+n&*4+3}
        IF r|>0
          SUB r|,4
        ENDIF
        IF v|>0
          SUB v|,4
        ENDIF
        IF b|>0
          SUB b|,4
        ENDIF
        BYTE{buffer%+n&*4}=r|
        BYTE{buffer%+n&*4+1}=v|
        BYTE{buffer%+n&*4+3}=b|
      ENDIF
    NEXT n&
    VSYNC
    BMOVE buffer%,&HFFFF9800,1024
  NEXT f&
RETURN
> PROCEDURE noir_rvb
  '
  '
  '
  FOR f&=0 TO 63
    FOR n&=0 TO 254
      IF n&<>1
        r|=BYTE{buffer%+n&*4}
        v|=BYTE{buffer%+n&*4+1}
        b|=BYTE{buffer%+n&*4+3}
        IF r|<BYTE{bu2%+n&*4}
          ADD r|,4
        ENDIF
        IF v|<BYTE{bu2%+n&*4+1}
          ADD v|,4
        ENDIF
        IF b|<BYTE{bu2%+n&*4+3}
          ADD b|,4
        ENDIF
        BYTE{buffer%+n&*4}=r|
        BYTE{buffer%+n&*4+1}=v|
        BYTE{buffer%+n&*4+3}=b|
      ENDIF
    NEXT n&
    VSYNC
    BMOVE buffer%,&HFFFF9800,1024
  NEXT f&
RETURN
