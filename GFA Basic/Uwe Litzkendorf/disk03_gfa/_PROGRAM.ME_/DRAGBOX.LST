'
' Allgemein verwendbare "Dragbox".
' ================================
'
' Bedeutungen der Parameter:
'
'    Zeile%       = Cursorzeile, in welcher der Wert angezeigt werden soll
'    Spalte%      = Cursorspalte, "   "      "    "      "        "    "
'    Offset%      = Verschiebung des Minimalwertes (+/-)
'    Teiler%      = Teiler zur Begrenzung des Maximalwertes
'    Dragxinit%   = X-Koordinate des Punktes, an welchem die Dragbox
'                   mit ihrer linken oberen Ecke erscheinen soll.
'    Dragyinit%   = Y-Koordinate  "      "     "    "      "    "
'    Dragbreit%   = Breite der Dragbox
'    Draghoch%    = H”he der Dragbox
'    Xbound%      = X-Koordinate der linken oberen Ecke der Begrenzungsbox
'    Ybound%      = Y-Koordinate  "    "      "      "   "       "
'    Boundbreit%  = Breite der Begrenzungsbox
'    Boundhoch%   = H”he der Begrenzungsbox
'
'    Nach Abschluž der Prozedur steht der gew„hlte Wert in der globalen
'    Variablen "Wahlwert%" und kann dort ausgelesen werden.
'
'    Aužerdem kann die Position der Dragbox, an welcher sie zum Abschluž der
'    Prozedur stand, aus den, ebenfalls globalen Variablen
'
'    Dragxl% = X-Koordinate der linken oberen Ecke
'    Dragyl% = Y-Koordinate  "    "      "      "
'    Dragxr% = X-Koordinate der rechten unteren Ecke
'    Dragyr% = Y-Koordinate  "     "      "      "
'
'    ausgelesen werden.
'
'
' Prozedur-Aufruf z.B. mit:
'
' @Dragbox(20,12,10,1,100,100,10,20,100,100,300,20)
'
'
'
Procedure Dragbox(Zeile%,Spalte%,Offset%,Teiler%,Dragxinit%,Dragyinit%,Dragbreit%,Draghoch%,Xbound%,Ybound%,Boundbreit%,Boundhoch%)
  Dragstart:
  Graphmode 3
  Color 1
  Defline 0,1,0,0
  Repeat
    Box Dragxinit%,Dragyinit%,Dragxinit%+Dragbreit%,Dragyinit%+Draghoch%
    Box Dragxinit%,Dragyinit%,Dragxinit%+Dragbreit%,Dragyinit%+Draghoch%
  Until Mousek
  Repeat
    Mouse Dragx%,Dragy%,Dragkey%
    If Dragx%>Xbound%+Boundbreit%
      Dragx%=Xbound%+Boundbreit%
    Endif
    If Dragy%>Ybound%+Boundhoch%
      Dragy%=Ybound%+Boundhoch%
    Endif
    If Dragx%<Xbound%+Dragbreit%
      Dragx%=Xbound%+Dragbreit%
    Endif
    If Dragy%<Ybound%+Draghoch%
      Dragy%=Ybound%+Draghoch%
    Endif
    Box Dragx%-Dragbreit%,Dragy%-Draghoch%,Dragx%,Dragy%
    Box Dragx%-Dragbreit%,Dragy%-Draghoch%,Dragx%,Dragy%
    Wahlwert%=Int((Dragx%-Dragxinit%+Offset%)/(Teiler%+0.01))
    Print At(Spalte%,Zeile%);Wahlwert%
  Until Dragkey%=0
  Dragxl%=Dragx%-Dragbreit%
  Dragyl%=Dragy%-Draghoch%
  Dragxr%=Dragx%
  Dragyr%=Dragy%
Return
