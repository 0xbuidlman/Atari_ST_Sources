'
' **************************************
' *  G E M S Y S   -   R O U T I N E N *
' **************************************
'
'
Procedure Gethandle
  Gemsys 77
  Handle=Dpeek(Gintout)
Return
'
'
Procedure Windfind(Xwindow,Ywindow)
  Dpoke Gintin,Xwindow
  Dpoke Gintin+2,Ywindow
  Gemsys 106
  Windowhandle=Dpeek(Gintout)
Return
'
'
Procedure Movebox(Mov.breit,Mov.hoch,Mov.xstart,Mov.ystart,Mov.xziel,Mov.yziel)
  Dpoke Gintin,Mov.breit
  Dpoke Gintin+2,Mov.hoch
  Dpoke Gintin+4,Mov.xstart
  Dpoke Gintin+6,Mov.ystart
  Dpoke Gintin+8,Mov.xziel
  Dpoke Gintin+10,Mov.yziel
  Gemsys 72
Return
'
'
Procedure D.ragbox(D.br,D.ho,D.x,D.y,B.x,B.y,B.br,B.ho)
  Dpoke Gintin,D.br
  Dpoke Gintin+2,D.ho
  Dpoke Gintin+4,D.x
  Dpoke Gintin+6,D.y
  Dpoke Gintin+8,B.x
  Dpoke Gintin+10,B.y
  Dpoke Gintin+12,B.br
  Dpoke Gintin+14,B.ho
  Gemsys 71
  X.fin=Dpeek(Gintout+2)
  Y.fin=Dpeek(Gintout+4)
Return
'
'
Procedure Mausstate
  Gemsys 79
  Mausx=Dpeek(Gintout+2)
  Mausy=Dpeek(Gintout+4)
  Mausk=Dpeek(Gintout+6)
  Flipkey=Dpeek(Gintout+8)
Return
'
'
Procedure Mouseevent(Outin,Ev.boxxl,Ev.boxyl,Ev.boxbreit,Ev.boxhoch)
  Dpoke Gintin,Outin
  Dpoke Gintin+2,Ev.boxxl
  Dpoke Gintin+4,Ev.boxyl
  Dpoke Gintin+6,Ev.boxbreit
  Dpoke Gintin+8,Ev.boxhoch
  Gemsys 22
  Mausx=Dpeek(Gintout+2)
  Mausy=Dpeek(Gintout+4)
  Mausk=Dpeek(Gintout+6)
  Switchk=Dpeek(Gintout+8)
Return
'
'
Procedure Keyevent
  Gemsys 20
  Keycode=Dpeek(Gintout)
Return
'
'
Procedure Buttonevent(Clicks,Ev.button,Ev.bstat)
  Dpoke Gintin,Clicks
  Dpoke Gintin+2,Ev.button
  Dpoke Gintin+4,Ev.bstat
  Gemsys 21
  Clicks=Dpeek(Gintout)
  Mausx=Dpeek(Gintout+2)
  Mausy=Dpeek(Gintout+4)
  Mausk=Dpeek(Gintout+6)
  Switchk=Dpeek(Gintout+8)
Return
'
'
' *************************************
' *  V D I S Y S  -  R O U T I N E N  *
' *************************************
'
'
Procedure Textrotor(Handle,Angle)
  Dpoke Contrl+2,0
  Dpoke Contrl+6,1
  Dpoke Contrl+12,Handle
  Dpoke Intin,Angle
  Vdisys 13
Return
'
'
Procedure Textgraph(Handle,Grafikmode)
  Dpoke Contrl+2,0
  Dpoke Contrl+6,1
  Dpoke Contrl+12,Handle
  Dpoke Intin,Grafikmode
  Vdisys 32
Return
'
'
Procedure Textart(Handle,Textart)
  Dpoke Contrl+2,0
  Dpoke Contrl+6,1
  Dpoke Contrl+12,Handle
  Dpoke Intin,Textart
  Vdisys 106
Return
'
'
Procedure Testpix(Handle,Xpixel,Ypixel)
  Dpoke Contrl+2,2
  Dpoke Contrl+6,0
  Dpoke Contrl+12,Handle
  Dpoke Ptsin,Xpixel
  Dpoke Ptsin+2,Ypixel
  Vdisys 105
  Pxset=Dpeek(Intout)
  Pxcol=Dpeek(Intout+2)
Return
'
'
Procedure F.interior(Handle,F.stil,F.muster)
  Dpoke Contrl+2,0
  Dpoke Contrl+6,1
  Dpoke Contrl+12,Handle
  Dpoke Intin,F.muster
  Vdisys 24
  Dpoke Contrl+2,0
  Dpoke Contrl+6,0
  Dpoke Intin,F.stil
  Vdisys 23
Return
'
'
Procedure Selfline(Handle,L.typ,L.def)
  Dpoke Contrl+2,0
  Dpoke Contrl+6,1
  Dpoke Contrl+12,Handle
  Dpoke Intin,L.typ
  Vdisys 15
  Dpoke Contrl+12,Handle
  Dpoke Intin,L.def
  Vdisys 113
Return
'
'
Procedure Diabar(Handle,Barxl,Baryl,Barxr,Baryr)
  Dpoke Contrl,11
  Dpoke Contrl+2,4
  Dpoke Contrl+6,0
  Dpoke Contrl+10,1
  Dpoke Contrl+12,Handle
  Dpoke Ptsin,Barxl
  Dpoke Ptsin+2,Baryl
  Dpoke Ptsin+4,Barxr
  Dpoke Ptsin+6,Baryr
  Vdisys 11
Return
'
'
Procedure Charboxh(Handle,Boxhoehe)
  Dpoke Contrl+2,0
  Dpoke Contrl+6,1
  Dpoke Contrl+12,Handle
  Dpoke Intin,Boxhoehe
  Vdisys 107
Return
'
'
Procedure E.col(Reg,Rot,Gruen,Blau)
  Dpoke Contrl+2,0
  Dpoke Contrl+6,4
  Dpoke Contrl+12,2
  Dpoke Intin,Reg
  Dpoke Intin+2,Rot
  Dpoke Intin+4,Gruen
  Dpoke Intin+6,Blau
  Vdisys 14
Return
'
'
' *************************************
' *  G E M D O S  -  R O U T I N E N  *
' *************************************
'
'
Procedure Setdrive(Desdrv)
  A=Gemdos(&HE,Desdrv)
Return
'
'
Procedure Z.printer(S.tring$)
  A=Gemdos(&H9,L:Varptr(S.tring$))
Return
'
'
Procedure Diskbuffer
  A=Gemdos(&H2F)
  Print A
  For I=0 To 512
    Print Chr$(Peek(A+I));
  Next I
Return
'
'
Procedure Conout(Zeichen$)
  Aa=Asc(Zeichen$)
  A=Gemdos(&H2,Aa)
Return
'
'
Procedure Conin
  A=Gemdos(&H1)
  Zeichen$=Chr$(Dpeek(Varptr(A)+4))
  Print Zeichen$''A
Return
'
'
' ************************************
' *  B  I  O  S  -  R O U T I N E N  *
' ************************************
'
'
Procedure Sondertasten(Keystat)
  A=Bios(11,Keystat)
Return
'
'
' !!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
'
' Achtung:
'
' !!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
'
' Bei den Erl„uterungen zu dieser Routine
' hat sich ein Fehler eingeschlichen.
' Der Parameter "Rwflag%" hat folgende Bedeutung:
'       0 = Sektor lesen
'       1 = Sektor schreiben
'       2 = Sektor lesen / Diskwechsel ignorieren
'       3 = Sektor schreiben / Diskwechsel ignorieren
'
Procedure Sektormod(Rwflag%,Buffer$,Anzahl%,Recno%,Dev%)
  A%=Bios(4,Rwflag%,L:Varptr(Buffer$),Anzahl%,Recno%,Dev%)
  Print Buffer$
Return
'
' ------------------------------------------------------------
'
Procedure Diskparameter(Flopnr)
  Parablock=Bios(7,Flopnr)
  For I=0 To 14 Step 2
    Diskpara$=Diskpara$+Str$(Dpeek(Parablock+I))+"  "
  Next I
  Print Diskpara$
Return
'
'
Procedure Changedisk(Flopnr)
  Changestat=Bios(9,Flopnr)
Return
'
'
' ***********************************
' *  X B I O S  -  R O U T I N E N  *
' ***********************************
'
'
Procedure Keytakter(Verz,Takt)
  A=Xbios(35,Verz,Takt)
Return
'
'
Procedure Setpalette(Colbuff$)
  A=Xbios(6,L:Varptr(Colbuff$))
Return
'
'
Procedure Getreso
  A=Xbios(4)
Return
'
'
Procedure Midiout(Anzahl,S.tring$)
  A=Xbios(12,Anzahl,L:Varptr(S.tring$))
Return
