'
' *******************************
' *   D E F F N   -   D E M O   *
' *******************************
'
Do
  Cls
  Clear
  Deffill ,2,2
  I=0
  Graphmode 3
  Defline 1,3
  Repeat
    Mouse Xnull,Ynull,K
    Circle Xnull,Ynull,7
    Circle Xnull,Ynull,7
  Until Mousek
  Graphmode 2
  Defline 1,1
  Circle Xnull,Ynull,7
  Text Xnull+8,Ynull-8,"Nullpkt.(0/0)"
  Pause 20
  Do
    Graphmode 3
    Inc I
    Repeat
      Mouse X,Y,K
      Text X,Y,Str$(X-Xnull)+"/"+Str$(Ynull-Y)+" ("+Str$(I)+")"
      Text X,Y,Str$(X-Xnull)+"/"+Str$(Ynull-Y)+" ("+Str$(I)+")"
    Until Mousek
    Repeat
      Mouse Xx,Yy,Kk
      Box X,Y,Xx,Yy
      Box X,Y,Xx,Yy
      Text Xx,Yy,Str$(Abs(Xx-X))+"/"+Str$(Abs(Yy-Y))
      Text Xx,Yy,Str$(Abs(Xx-X))+"/"+Str$(Abs(Yy-Y))
    Until Kk<>1
    Exit If Kk=2
    Breite=Abs(Xx-X)
    Hoehe=Abs(Yy-Y)
    X.diff=Min(X,Xx)-Xnull
    Y.diff=Ynull-Max(Y,Yy)
    Graphmode 2
    Xlo=@X.lo
    Ylo=@Y.lo
    Xru=@X.ru
    Yru=@Y.ru
    Pbox Xlo,Ylo,Xru,Yru
    Defline 1,1
    Line Xlo,Ylo,Xru,Yru
    Line Xlo,Yru,Xru,Ylo
    F.laeche=@Flaeche
    Add Areas,F.laeche
    X.halbe=@Halbe(Breite)+X.diff
    Y.halbe=@Halbe(Hoehe)+Y.diff
    Add An.xn,@Multi(X.halbe)
    Add An.yn,@Multi(Y.halbe)
  Loop
  Graphmode 1
  Sx=An.xn/Areas
  Sy=An.yn/Areas
  Defline 4,1
  Line 0,Ynull-Sy,640,Ynull-Sy
  Line Xnull+Sx,0,Xnull+Sx,399
  Defline ,3
  Circle Xnull+Sx,Ynull-Sy,7
  Print At(1,1);"SX : ";Sx
  Print At(1,2);"SY : ";Sy
  Repeat
    If Mousek
      Alert 2,"N„chste Eingabe ?      ",1,"JA|NEIN|ENDE",Antwort
    Endif
    If Antwort=3
      Edit
    Endif
  Until Antwort=1
Loop
Deffn Flaeche=Hoehe*Breite
Deffn Halbe(Seite)=Seite/2
Deffn Multi(H.seite)=H.seite*F.laeche
Deffn X.lo=Xnull+X.diff
Deffn Y.lo=Ynull-Hoehe-Y.diff
Deffn X.ru=Xnull+X.diff+Breite
Deffn Y.ru=Ynull-Y.diff
'
