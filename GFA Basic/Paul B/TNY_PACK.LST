' To use just call @save_tiny
' screen to save is set to physical screen (xbio(3)) but can be any address
' (c) PaulB (2003)
' feel free to use this code however you see fit.
> PROCEDURE save_tiny
  ' Save A Packed SPRITE KNIGHT Picture
  ' -----------------------------------
  FILESELECT #"SAVE TINY PICTURE","\*.TNY","",filename$
  IF filename$=>""
    IF RIGHT$(filename$,3)="TNY"
      HIDEM
      DIM cntrl|(3200),data&(16400),words&(16000)
      '
      ' Get words from screen 1 column at a time. (20 columns in a screen line)
      ' 1st bitplane comes before 2nd and so on
      screen=XBIOS(3)
      scr=screen
      CLR col2,offset2,scan2,no
      w=V:words&(0)
      REPEAT
        WORD{w}=WORD{scr}
        ADD w,2
        INC no
        IF no<16000
          @tiny_next_scr_word
        ENDIF
      UNTIL no=16000
      '
      ' COMPRESS DATA IN TINY FORMAT HERE
      '
      c&=0                  ! Counts How Many Single Or Repetitive Words
      str=V:words&(0)       ! Address Of Word Array
      word_counter&=0       ! When This Reaches 16000, Compression Stops
      LET cntrl=V:cntrl|(0) ! Address Of Control Bytes
      LET cntrl_bytes&=0    ! Number of Control bytes in file
      LET data=V:data&(0)   ! Address Of Data Words
      LET data_words&=0     ! Number of Data Words in file
      REPEAT
        '
        l1&=WORD{str}       ! Test 2 Words To See If Same
        l2&=WORD{ADD(str,2)}
        '
        IF l1&=l2& AND word_counter&<length ! Words Are Same?
          ADD str,4           ! Point 'str' To 3rd Word On
          ADD word_counter&,2 ! 2 More Words Compressed (Add 3 To Counter)
          c&=2                ! 2 Words The Same Found So Far
          WHILE WORD{str}=l1& AND word_counter&<length
            ADD str,2     ! Adjust Address
            INC c&        ! Another Word Compressed
            INC word_counter&
          WEND
          WORD{data}=l1&    ! Data Word To Repeat
          ADD data,2        ! Adjust Address
          INC data_words&
          SELECT c&
          CASE 2 TO 127     ! Repeat 'data' 2 To 127 Times
            BYTE{cntrl}=c&
            INC cntrl
            INC cntrl_bytes&
          DEFAULT           ! Repeat 'data' 128 To 32767 Times
            BYTE{cntrl}=0
            INC cntrl
            BYTE{cntrl}=BYTE{V:c&}
            INC cntrl
            BYTE{cntrl}=BYTE{SUCC(V:c&)}
            INC cntrl       ! Place for next cntrl code
            ADD cntrl_bytes&,3
          ENDSELECT
        ELSE                ! Words Are Not Same (Copy Words Literally)
          IF word_counter&<length
            WORD{data}=l1&    ! Place Individual Word
            ADD data,2        ! Adjust Address
            ADD str,2         ! Adjust Address
            c&=1              ! 1 Single Word So Far
            INC word_counter&
            INC data_words&
            WHILE WORD{str}<>WORD{ADD(str,2)} AND word_counter&<length
              WORD{data}=WORD{str} ! Place Individual Word
              ADD data,2
              ADD str,2
              INC c&
              INC word_counter&
              INC data_words&
            WEND
            SELECT c&
            CASE  TO 127
              BYTE{cntrl}=SUB(256,c&)
              INC cntrl
              INC cntrl_bytes&
            DEFAULT
              BYTE{cntrl}=1
              INC cntrl
              BYTE{cntrl}=BYTE{V:c&}
              INC cntrl
              BYTE{cntrl}=BYTE{SUCC(V:c&)}
              INC cntrl
              ADD cntrl_bytes&,3
            ENDSELECT
          ENDIF
        ENDIF
      UNTIL word_counter&>=length
      '
      size=37+cntrl_bytes&+(data_words&*2)
      '
      IF DFREE(0)>=size
        OPEN "O",#1,filename$
        OUT #1,0                          ! Low Res Pic. No Colour Rotation
        FOR t=0 TO 15
          OUT& #1,XBIOS(7,t,-1) AND &HFFF
        NEXT t
        OUT& #1,cntrl_bytes&,data_words&  ! Place No Of cntrl Bytes In File
        BPUT #1,V:cntrl|(0),cntrl_bytes&  ! Place Actual Control Bytes
        BPUT #1,V:data&(0),data_words&*2  ! Place Actual Data Words
        CLOSE #1
      ELSE
        ~FORM_ALERT(1,"[1][DISK FULL | |Save Abandoned |][ OK ]")
      ENDIF
      ERASE words&(),cntrl|(),data&()
      SHOWM
    ELSE
      ~FORM_ALERT(1,"[1][WRONG FILE EXTENDER | |Save Abandoned |][ OK ]")
    ENDIF
  ENDIF
RETURN
> PROCEDURE tiny_next_scr_word
  INC scan2
  SELECT scan2
  CASE 200
    CLR scan2
    INC col2
    IF col2=20
      col2=0
      ADD offset2,2
    ENDIF
    scr=ADD(ADD(screen,SHL(col2,3)),offset2)
  DEFAULT
    ADD scr,160
  ENDSELECT
RETURN
