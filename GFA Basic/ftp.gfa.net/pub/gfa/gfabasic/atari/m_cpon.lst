PROCEDURE gfa_m_cpon__(paladr__%,speed__%)
  ' ****************************************************************************
  ' ***                                                                      ***
  ' **   ********    ** * **   Ein Produkt von                                **
  ' **   *********  *********  Hans-Peter Burk und Helmut Micko               **
  ' **         **   *********  Software Entwicklungen                         **
  ' **   *********  *** * ***                                                 **
  ' **         **   *** * ***  Library         : GFA_GLIB.3_0                 **
  ' **   *********  *** * ***  Name            : M_CPON                       **
  ' **   ********   *** * ***  Version         : 1.1  12.12.1989  18:21       **
  ' **                         Autor           : Helmut Micko                 **
  ' ***                                                                      ***
  ' ****************************************************************************
  ' *** Funktion  : Einblenden einer Farbpalette in der mittleren Aufl”sung. ***
  ' **                                                                        **
  ' **  Parameter : 1.Adresse der Farbpalette (16 Words)                      **
  ' **              2.Geschwindigkeit (0=schnell)                             **
  ' **                                                                        **
  ' **                                                                        **
  ' **                                                                        **
  ' **                                                                        **
  ' **  Lib_rv__% : Unbenutzt                                                 **
  ' *** Lib_rv__$ : Unbenutzt                                                ***
  ' ****************************************************************************
  LOCAL bufadr__%,i1__%,i2__%,col1__%,col2__%,redv__%,greenv__%,bluev__%
  INLINE bufadr__%,32
  FOR i1__%=0 TO 31 STEP 2
    WORD{bufadr__%+i1__%}=0
  NEXT i1__%
  FOR i1__%=7 DOWNTO 1
    FOR i2__%=0 TO 7 STEP 2
      col1__%=WORD{bufadr__%+i2__%}
      col2__%=DPEEK(paladr__%+i2__%)
      redv__%=(col1__% AND &H700)
      greenv__%=(col1__% AND &H70)
      bluev__%=(col1__% AND &H7)
      IF i1__%*256<=(col2__% AND &H700)
        ADD redv__%,256
      ENDIF
      IF i1__%*16<=(col2__% AND &H70)
        ADD greenv__%,16
      ENDIF
      IF i1__%<=(col2__% AND &H7)
        INC bluev__%
      ENDIF
      WORD{bufadr__%+i2__%}=redv__%+greenv__%+bluev__%
    NEXT i2__%
    ~XBIOS(6,L:bufadr__%)
    PAUSE speed__%
  NEXT i1__%
RETURN
