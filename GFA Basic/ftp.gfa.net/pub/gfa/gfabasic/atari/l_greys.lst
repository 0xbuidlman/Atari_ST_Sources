PROCEDURE gfa_l_greys__(paladr__%,value__%)
  ' ****************************************************************************
  ' ***                                                                      ***
  ' **   ********    ** * **   Ein Produkt von                                **
  ' **   *********  *********  Hans-Peter Burk und Helmut Micko               **
  ' **         **   *********  Software Entwicklungen                         **
  ' **   *********  *** * ***                                                 **
  ' **         **   *** * ***  Library         : GFA_GLIB.3_0                 **
  ' **   *********  *** * ***  Name            : L_GREYS                      **
  ' **   ********   *** * ***  Version         : 1.4  12.12.1989  18:25       **
  ' **                         Autor           : Helmut Micko                 **
  ' ***                                                                      ***
  ' ****************************************************************************
  ' *** Funktion  : Umwandeln der Farben einer Farbpalette der niedrigsten   ***
  ' **              Aufl”sung in Graut”ne.                                    **
  ' **  Parameter : 1.Adresse der Farbpalette (16 Words)                      **
  ' **              2.Aufhellungsfaktor (0-7)                                 **
  ' **                                                                        **
  ' **                                                                        **
  ' **                                                                        **
  ' **                                                                        **
  ' **  Lib_rv__% : Unbenutzt                                                 **
  ' *** Lib_rv__$ : Unbenutzt                                                ***
  ' ****************************************************************************
  LOCAL bufadr__%,i__%,col__%,redv__%,greenv__%,bluev__%
  INLINE bufadr__%,32
  value__%=value__% MOD 8
  FOR i__%=0 TO 31 STEP 2
    col__%=DPEEK(paladr__%+i__%)
    redv__%=(col__% AND &H700)/256
    greenv__%=(col__% AND &H70)/16
    bluev__%=(col__% AND &H7)
    col__%=redv__%+greenv__%+bluev__%+1
    DIV col__%,3
    ADD col__%,value__%
    IF col__%>7
      col__%=7
    ENDIF
    WORD{bufadr__%+i__%}=col__%*256+col__%*16+col__%
  NEXT i__%
  ~XBIOS(6,L:bufadr__%)
RETURN
