On Error Gosub Errorhandler
On Break Gosub Errorhandler
For Ans=1 To 92 Step 4
  Sound 1,10,#Ans*20+1001
  Sound 2,10,#Ans*20+1002
  Sound 3,10,#Ans*20+1000
  Wave 7,7,14,Ans,1
Next Ans
Print At(10,5);"-----========== B O O T A T O R ==========-----"
Print At(10,7);" By GeKa-Soft (Luxembourg) 89"
Same!=False
Alert 1,"CHOOSE AN OPTION:|Bootsector --> File|File       --> Bootsector|Bootsector --> Bootsector",2," B -> F |F -> B | B -> B ",Action%
Same:
Ans%=Action%
If Ans%=1
  Gosub Getboot
  If Error!
    Goto Handleerror
  Endif
  Cls
  Print "Write this bootsector to a file:"
  Gosub Showhex(Destination$)
  Gosub Writefile(Destination$)
Else
  If Not Same!
    If Ans%=2
      Gosub Getfile
      If Error!
        Goto Handleerror
      Endif
    Else
      Gosub Getboot
      If Error!
        Goto Handleerror
      Endif
      Source$=Destination$
    Endif
  Endif
  Alert 1,"Insert destination",1," OK",Ans%
  Gosub Getboot
  If Error!
    Goto Handleerror
  Endif
  Cls
  Print "Old code"
  Gosub Showhex(Destination$)
  Gosub Testexec(Destination$)
  If Executable!
    Alert 1,"The destination bootsector|is already executable !",1,"Continue|Abort",Ans%
  Endif
  If Ans%=1
    Sget Old$
    Cls
    If Mousek=2
      Alert 2,"Welcome to the dangerous part!| | Are you shure to loos|the bios parameter block",1," NO!! |YES|Perhaps",Ans%
      If Ans%=3
        Repeat
          Print At(1,1);"Yeaa!! you found the (not very userfriendly) editor!"
          Gosub Showhex(Source$)
          Print At(1,15);
          Part1$=Left$(Source$,255)
          Part2$=Right$(Source$,257)
          Form Input 255 As Part1$
          Print At(16,18);
          Form Input 255 As Part2$
          Source$=Part1$+Part2$+"  "
          Alert 2," AGAIN ????????????",1,"OH NO!!| MORE! ",Ans%
        Until Ans%=1
        Cls
      Endif
    Endif
    If Ans%=1
      Mid$(Source$,12,17)=Mid$(Destination$,12,17)
    Endif
    Gosub Makesourceexec
    Print "New code              |<---- Bios parameter block ---->|"
    Gosub Showhex(Source$)
    Print At(10,23);"=====> Press any key <====="
    Sget New$
    Do
      Exit If Mousek Or Inkey$<>""
      Sput New$
      Pause 20
      Exit If Mousek Or Inkey$<>""
      Sput Old$
      Pause 20
    Loop
    Sput New$
    Alert 2," Last chance to |  avoid cataclysm :",1," WRITE |  NO !! ",Ans%
    If Ans%=1
      Gosub Writetoboot(Source$)
    Endif
  Endif
Endif
Handleerror:
Alert 1," And now ???| Do the same action| or choose another one ? ",1,"Same|Other| END ",Ans%
If Ans%=1
  Same!=True
  Goto Same
Else
  If Ans%=2
    Run
  Else
    Cls
    Print At(10,8);"If you like this Programm send some money to:"
    Print At(10,10);"GeKa-Soft"
    Print At(10,11);"3 rue Kennedy"
    Print At(10,12);"L-8332 Olm"
    Ans%=Timer
    Do
      Exit If Timer-Ans%>500 Or Inkey$<>"" Or Mousek
    Loop
    End
  Endif
Endif
'
Procedure Errorhandler
  On Error Gosub Errorhandler
  On Break Gosub Errorhandler
  Alert 1,"That was an action generating|an internal error. ("+Str$(Err)+")|Please try again",1," OK ",Ans%
  Resume Handleerror
Return
'
Procedure Getboot
  Local Ans%
  Destination$=String$(512,0)
  Let Error!=Xbios(8,L:Varptr(Destination$),L:0,0,1,0,0,1)<0
  If Error!
    Alert 1,"Read error",1,"OK",Ans%
  Endif
Return
'
Procedure Writetoboot(A$)
  Local Ans%
  Let Error!=Xbios(9,L:Varptr(A$),L:0,0,1,0,0,1)<0
  If Error!
    Alert 1,"Write error",1," OK ",Ans%
  Endif
Return
'
Procedure Testexec(A$)
  Sum%=0
  For I%=1 To Len(A$)-1 Step 2
    Add Sum%,Cvi(Mid$(A$,I%,2))
  Next I%
  Let Executable!=Sum% Mod &H10000=&H1234
Return
'
Procedure Getfile
  Let Error!=False
  Fileselect "\*.*","",File$
  If File$=""
    Let Error!=True
  Endif
  Open "I",#1,File$
  If Lof(#1)<513
    Source$=Input$(Lof(#1),#1)
  Else
    Alert 1,"File too long",1,"OK",Ans%
    Let Error!=True
  Endif
  Close
  Source$=Source$+String$(512-Len(Source$),0)
Return
'
Procedure Writefile(A$)
  Let Error!=False
  Local Ans%,File$
  Fileselect "\*.*","",File$
  If File$=""
    Let Error!=True
  Endif
  Ans%=1
  If Exist(File$)
    Alert 1,"File already exists",1,"Continue|Abort",Ans%
  Endif
  If Ans%=1
    Open "O",#1,File$
    Print #1,A$;
    Close
  Else
    Let Error!=True
  Endif
Return
'
Procedure Makesourceexec
  '                       *********   exec  ***********
  Void Xbios(18,L:Varptr(Source$),L:-1,-1,1)
Return
'
Procedure Showhex(A$)
  Local I%,A%
  I%=1
  Repeat
    A%=Asc(Mid$(A$,I%,1))
    If A%<16
      Print "0";Hex$(A%);
    Else
      Print Hex$(A%);
    Endif
    If Odd(I%)
      Print "p";
    Else
      Print "q";
    Endif
    Inc I%
  Until I%>Len(A$)
  Print "q"
  For I%=1 To Len(A$)
    A%=Asc(Mid$(A$,I%,1))
    If A%>31
      Print Chr$(A%);
    Else
      Print "p q";
    Endif
  Next I%
Return
