'
@init
@line1_main
@re_init
'
> PROCEDURE line1_main
  DIM x(107),y(107),z(107),x2d&(107),y2d&(107)
  DIM face&(107,5),c(720),s(720),qz(500)
  DIM x&(3),y&(3)
  '
  @line1_pal
  @line1_init(80)
  @line1_play
  '
  ERASE y&(),x&()
  ERASE qz(),s(),c(),face&()
  ERASE x(),y(),z(),x2d&(),y2d&()
RETURN
> PROCEDURE line1_play
  CLR k&
  COLOR 2
  @line1_bli_init
  CLR m&,n&
  REPEAT
    CLS
    @line1_calcul_3d(n&,m&,160,100)
    @line1_affiche
    @line1_bli_p2(x2%,ADD(x3%,2))
    @line1_bli_p2(ADD(x2%,2),ADD(x3%,4))
    SWAP x3%,x2%
    ~XBIOS(5,L:x3%,L:x2%,-1)
    m&=ADD(m&,7) MOD 720
    n&=ADD(n&,4) MOD 720
    VSYNC
  UNTIL PEEK(&HFFFC02)=57
RETURN
> PROCEDURE line1_calcul_3d(theta,phi,ofx&,ofy&)
  c0=c(theta)
  s0=s(theta)
  c1=c(phi)
  s1=s(phi)
  FOR i&=0 TO PRED(points&)
    x0=x(i&)
    y0=y(i&)
    z0=z(i&)
    y1=y0*s0+x0*c0
    qz3=ADD(300,z0*s1-y1*c1)
    qz3=qz(qz3)
    x2d&(i&)=(x0*s0-y0*c0)*qz3+ofx&
    y2d&(i&)=(y1*s1+z0*c1)*qz3+ofy&
  NEXT i&
RETURN
> PROCEDURE line1_affiche
  LOCAL m&,n&,o&,p&
  RESTORE face
  '
  FOR i&=0 TO 9
    READ n&
    m&=SUCC(n&)
    o&=SUCC(m&)
    p&=SUCC(o&)
    ALINE x2d&(n&),y2d&(n&),x2d&(m&),y2d&(m&),1,&HFFFFFFFF,0
    ALINE x2d&(m&),y2d&(m&),x2d&(p&),y2d&(p&),1,&HFFFFFFFF,0
    ALINE x2d&(p&),y2d&(p&),x2d&(o&),y2d&(o&),1,&HFFFFFFFF,0
    ALINE x2d&(o&),y2d&(o&),x2d&(n&),y2d&(n&),1,&HFFFFFFFF,0
  NEXT i&
  FOR i&=0 TO 3
    READ n&
    m&=ADD(n&,4)
    ALINE x2d&(n&),y2d&(n&),x2d&(m&),y2d&(m&),1,&HFFFFFFFF,0
    INC n&
    ALINE x2d&(n&),y2d&(n&),x2d&(m&),y2d&(m&),1,&HFFFFFFFF,0
    INC n&
    ALINE x2d&(n&),y2d&(n&),x2d&(m&),y2d&(m&),1,&HFFFFFFFF,0
    INC n&
    ALINE x2d&(n&),y2d&(n&),x2d&(m&),y2d&(m&),1,&HFFFFFFFF,0
  NEXT i&
  '
face:
  DATA 0,4,8,12,16,20
  DATA 24,29,34,39
  DATA 24,29,34,39
  '
RETURN
> PROCEDURE line1_bli_init
  CARD{&H8A00}=&X1111111111111111
  CARD{&H8A02}=&X1111111111111111
  CARD{&H8A04}=&X1111111111111111
  CARD{&H8A06}=&X1111111111111111
  CARD{&H8A08}=&X1111111111111111
  CARD{&H8A0A}=&X1111111111111111
  CARD{&H8A0C}=&X1111111111111111
  CARD{&H8A0E}=&X1111111111111111
  CARD{&H8A10}=&X1111111111111111
  CARD{&H8A12}=&X1111111111111111
  CARD{&H8A14}=&X1111111111111111
  CARD{&H8A16}=&X1111111111111111
  CARD{&H8A18}=&X1111111111111111
  CARD{&H8A1A}=&X1111111111111111
  CARD{&H8A1C}=&X1111111111111111
  CARD{&H8A1E}=&X1111111111111111
  '
  CARD{&HFFFF8A28}=&X1111111111111111
  CARD{&HFFFF8A2A}=&X1111111111111111
  CARD{&HFFFF8A2C}=&X1111111111111111
RETURN
> PROCEDURE line1_bli_p2(img%,x%)
  xb%=x%
  CARD{&HFFFF8A20}=8
  CARD{&HFFFF8A22}=0
  LONG{&HFFFF8A24}=img%
  CARD{&HFFFF8A2E}=8
  CARD{&HFFFF8A30}=0
  LONG{&HFFFF8A32}=xb%
  CARD{&HFFFF8A36}=21
  CARD{&HFFFF8A38}=200
  CARD{&HFFFF8A3A}=&H203
  CARD{&HFFFF8A3C}=&HC000
RETURN
> PROCEDURE line1_init(ray&)
  CLR points&
  @line1_creer_cube(0,0,0,ray&,points&)
  @line1_creer_cube(0,0,0,ray&*0.7,points&)
  @line1_creer_cube(0,0,0,ray&*0.4,points&)
  '
  @line1_creer_pyrm(0,0,0.75*ray&,-ray&*0.6,points&,1,1,0)
  @line1_creer_pyrm(0,0,-0.75*ray&,ray&*0.6,points&,1,1,0)
  @line1_creer_pyrm(0,0.75*ray&,0,-ray&*0.6,points&,1,0,1)
  @line1_creer_pyrm(0,-0.75*ray&,0,ray&*0.6,points&,1,0,1)
  '
  FOR h&=0 TO 720
    c(h&)=COSQ(h&)
    s(h&)=SINQ(h&)
  NEXT h&
  '
  FOR h&=1 TO 500
    qz(h&)=300/h&
  NEXT h&
RETURN
> PROCEDURE line1_creer_cube(dx,dy,dz,ray&,n&)
  r&=n&
  FOR x&=0 TO 1
    FOR y&=0 TO 1
      FOR z&=0 TO 1
        x(r&)=ray&*(0.5-x&)+dx
        y(r&)=ray&*(0.5-y&)+dy
        z(r&)=ray&*(0.5-z&)+dz
        INC r&
      NEXT z&
    NEXT y&
  NEXT x&
  points&=r&
RETURN
> PROCEDURE line1_creer_pyrm(dx,dy,dz,ray&,n&,a0&,a1&,a2&)
  r&=n&
  z&=0
  FOR x&=0 TO a0&
    FOR y&=0 TO a1&
      FOR z&=0 TO a2&
        x(r&)=ray&*(0.5-x&)+dx
        y(r&)=ray&*(0.5-y&)+dy
        z(r&)=ray&*(0.5-z&)+dz
        INC r&
      NEXT z&
    NEXT y&
  NEXT x&
  '
  x(r&)=ray&*(0.5-x&)*(1-a0&)+dx
  y(r&)=ray&*(0.5-y&)*(1-a1&)+dy
  z(r&)=ray&*(0.5-z&)*(1-a2&)+dz
  INC r&
  '
  points&=r&
RETURN
> PROCEDURE line1_pal
  SETCOLOR 0,0
  SETCOLOR 1,1,1,2
  SETCOLOR 2,0,1,2
  SETCOLOR 4,1,2,3
  SETCOLOR 3,3,4,6
  SETCOLOR 5,3,4,5
  SETCOLOR 6,3,5,6
  SETCOLOR 7,6,7,7
RETURN
'
> PROCEDURE init
  CLS
  old%=XBIOS(3)
  res%=XBIOS(4)
  '
  savg%=GEMDOS(32,L:0)
  '
  DIM e|(32512)
  e%=256*SUCC(DIV(V:e|(0),256))
  ~XBIOS(5,L:e%,L:-1,-1)
  x3%=XBIOS(3)
  x2%=XBIOS(2)
RETURN
> PROCEDURE re_init
  CLS
  ~XBIOS(5,L:old%,L:old%,res%)
  ERASE e|()
  '
  ~GEMDOS(32,L:savg%)
  '
  CLS
RETURN
