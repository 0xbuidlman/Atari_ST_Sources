' HR_64000.LST
' pour attribuer une valeur de couleur
' … chaque groupe de 4 points d'un graphique 640*400
' --------------------------------------------------
DIM n|(63999)
s$=SPACE$(32034)
BLOAD "KARIN.PI3",V:s$
s$=RIGHT$(s$,32000)
SPUT s$
s%=V:s$
d%=V:n|(0)
'
ALERT 2,"Conversion du graphique|haute r‚solution|… l'aide du GFA Basic ou|du GFA Assembleur ?",1,"Basic|Ass.",question%
t=TIMER
'
' en GFA Basic
IF question%=1
  z%=0
  FOR m%=s% TO s%+31999 STEP 160
    FOR n%=m% TO m%+79
      o%=BYTE{n%}
      u%=BYTE{n%+80}
      n|(z%)=-BTST(o%,7)-BTST(o%,6)-BTST(u%,7)-BTST(u%,6)
      n|(z%+1)=-BTST(o%,5)-BTST(o%,4)-BTST(u%,5)-BTST(u%,4)
      n|(z%+2)=-BTST(o%,3)-BTST(o%,2)-BTST(u%,3)-BTST(u%,2)
      n|(z%+3)=-BTST(o%,1)-BTST(o%,0)-BTST(u%,1)-BTST(u%,0)
      ADD z%,4
    NEXT n%
  NEXT m%
ENDIF
'
' en GFA Assembleur
IF question%=2
  hr_ass$=SPACE$(108)
  hr_ass%=VARPTR(hr_ass$)
  BLOAD "hr_64000.prg",hr_ass%
  ' appel du programme en Assembleur, dans lequel
  ' s% et d% sont les adresses de l'information graphique
  VOID C:hr_ass%(L:s%,L:d%)
ENDIF
'
tt=(TIMER-t)/200
t$=STR$(tt)
IF question%=1
  ALERT 1,"En GFA Basic,|la conversion demande|"+t$+" secondes.",1,"OK",d%
ELSE
  ALERT 1,"En GFA Assembleur,|la conversion demande|"+t$+" secondes.",1,"OK",d%
ENDIF
'
ALERT 2,"|Sauvegarder les |valeurs de couleur|dans le fichier 'Karin.nbr' ?",1,"oui|non",d%
IF d%=1
  BSAVE "KARIN.NBR",V:n|(0),64000
ENDIF
'
EDIT
