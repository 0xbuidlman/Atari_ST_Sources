' CHERCHE.LST
' --------
quoted$=SPACE$(86)
quoted%=VARPTR(quoted$)
BLOAD "e:\part3luc\quote.prg",quoted%
'
' CHAINE:
'
a$=" Voici une ® chaine de car¯acteres o— un®®e position sera¯ recherch‚e ¯."
g_ouvrants%=174      ! ASCII guillemet ouvrant
g_fermants%=175     ! ASCII guillemet fermant
locat%=55       ! ASCII Position dans chaine
'
' Recherche des guillemets
'
ALERT 2,"Recherche Guillemets |GFA Basic|GFA Assembleur?",1,"Basic|Ass.",question%
t=TIMER
'
' GFA-Basic
IF question%=1
  propr%=@rechercher(VARPTR(a$),g_ouvrants%,g_fermants%,locat%)
ELSE
  ' GFA Assembleur:
  '
  ' appel du programme en Assembleur dans lequel :
  '   L:V:a$  = Adresse information string est
  '   g_ouvrants%  = guillemets ouvrants … gauche
  '   g_fermants% = guillemets fermants … droite
  '   locat%  = Position du caractŠre nous concernant
  '   propr%    = propri‚t‚ du caractŠre 55
  '         0: en dehors des guillemets
  '       <>0: entre les guillemets
  propr%=C:quoted%(L:V:a$,g_ouvrants%,g_fermants%,locat%)
ENDIF
'
tt=(TIMER-t)/200
t$=STR$(tt)
'
PRINT "  Teststring: |12345678901234567890123456789012345678901234567890|"
test$=a$
WHILE test$<>""
  PRINT TAB(14);"|";LEFT$(test$,50);"|"
  test$=MID$(test$,51)
  IF locat%<50 AND locat%>=0
    PRINT TAB(15+locat%);"^"
  ENDIF
  SUB locat%,50
WEND
PRINT "Propri‚t‚ du caractŠre … la position indiqu‚e: ";propr%
IF propr%<=0
  PRINT "Le caractŠre se trouve en dehors des guillemets."
ELSE
  PRINT "Le caractŠre se trouve entre les guillemets."
ENDIF
'
IF question%=1
  ALERT 1,"La recherche demande |en GFA Basic|"+t$+" secondes.",1," OK ",d%
ELSE
  ALERT 1,"La recherche demande |en GFA Assembleur|"+t$+" secondes.",1," OK ",d%
ENDIF
'
EDIT
'
FUNCTION rechercher(p%,ouvrant%,fermant%,pos%)
  LOCAL propr%
  propr%=0
  FOR m%=p% TO p%+pos%-1
    IF BYTE{m%}=ouvrant%
      INC propr%
    ENDIF
    IF BYTE{m%}=fermant%
      DEC propr%
    ENDIF
  NEXT m%
  RETURN propr%
ENDFUNC
