' SCROLL.LST
' d‚filement avec des paramŠtres GET modifi‚s
'
' cr‚ation du texte
a$="Voici une d‚monstration portant sur une "
b$="ligne trŠs longue … faire d‚filer. La mo"
c$="dification des paramŠtres GET rend cela "
d$="trŠs facile. Votre curiosit‚ est piqu‚e?"
DEFTEXT 1,0,0,32
TEXT 0,24,a$
TEXT 0,56,b$
TEXT 0,88,c$
TEXT 0,120,d$
'
PRINT AT(30,13);"Appuyez sur une touche s.v.p."
VOID INP(2)
'
' modifier le graphique
GET 0,0,639,127,graphique$
d%=VARPTR(graphique$)
FOR m%=XBIOS(2) TO XBIOS(2)+80*32-1 STEP 80
  FOR n%=m% TO m%+80*32*4-1 STEP 80*32
    BMOVE n%,d%+6,80
    ADD d%,80
  NEXT n%
NEXT m%
'
d%=VARPTR(graphique$)
' mettre les nouveaux paramŠtres GET
DPOKE d%,640*4-1
DPOKE d%+2,128/4-1
'
' d‚filement
CLS
FOR y%=64 TO 350 STEP 64
  FOR x%=320 TO -640*3 STEP -1
    PUT x%,y%,graphique$
  NEXT x%
NEXT y%
