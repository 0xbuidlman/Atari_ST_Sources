'       T R O N
'         av
' Christer Engman 9/2-93
'
@init
@main
@sum
@exit
> PROCEDURE init
  SETCOLOR 0,0,0,0
  VSETCOLOR 1,7,7,7
  VSETCOLOR 2,7,0,0
  VSETCOLOR 3,0,7,0
  VSETCOLOR 4,7,7,0
  VSETCOLOR 5,7,0,7
  HIDEM
  '
  ABSOLUTE key|,SUCC(V:kget%)
  SPOKE &H484,&X1000            ! Turn of key click and key repeat
  '
  REPEAT
    CLS
    INPUT "Number of players (1-4)";pl%
  UNTIL pl%>0 AND pl%<5
  DIM x%(pl%),y%(pl%),v%(pl%),dead!(pl%),kills%(pl%),name$(pl%)
  DIM left|(pl%),right|(pl%)
  '
  FOR p%=1 TO pl%
    CLS
    PRINT "What's your name Player #";p%;"?"
    INPUT "> ",name$(p%)
    PAUSE 5
    PRINT
    PRINT "Choose left-turn key."
    KEYGET kget%
    left|(p%)=key|
    PAUSE 5
    PRINT
    PRINT "Choose right-turn key."
    KEYGET kget%
    right|(p%)=key|
    PAUSE 5
  NEXT p%
  CLS
  INPUT "How many games";games%
RETURN
> PROCEDURE reset
  LOCAL p%,c%
  dead!=FALSE
  ARRAYFILL dead!(),FALSE
  FOR p%=1 TO pl%
    x%(p%)=50+RAND(220)
    y%(p%)=50+RAND(100)
    v%(p%)=90*RAND(4)
  NEXT p%
  @screen
  FOR p%=1 TO pl%
    x%=x%(p%)+10*COSQ(v%(p%))
    y%=y%(p%)+10*SINQ(v%(p%))
    CIRCLE x%,y%,1
    ALINE x%(p%),y%(p%),x%,y%,p%,&HFFFF,0
  NEXT p%
  FOR c%=3 DOWNTO 1
    PRINT AT(20,10);c%
    PAUSE 20
  NEXT c%
RETURN
> PROCEDURE screen
  CLS
  BOX 0,0,319,199
RETURN
> PROCEDURE main
  FOR game%=1 TO games%
    '
    @reset
    @screen
    DO
      @keys
      VSYNC
      @update
    LOOP UNTIL dead!
    @dead
    '
  NEXT game%
RETURN
> PROCEDURE keys
  LOCAL p%
  IF INP?(2)
    KEYGET kget%
    FOR p%=1 TO pl%
      IF key|=left|(p%)
        SUB v%(p%),90
      ELSE IF key|=right|(p%)
        ADD v%(p%),90
      ENDIF
    NEXT p%
  ENDIF
RETURN
> PROCEDURE update
  LOCAL n%,dx%,dy%,x%,y%
  FOR n%=1 TO pl%
    dx%=COSQ(v%(n%))
    dy%=SINQ(v%(n%))
    x%=ADD(x%(n%),dx%)
    y%=ADD(y%(n%),dy%)
    IF PTST(x%,y%)
      dead!=TRUE
      dead!(n%)=TRUE
      INC kills%(n%)
    ELSE
      PSET x%,y%,n%
      x%(n%)=x%
      y%(n%)=y%
    ENDIF
  NEXT n%
RETURN
> PROCEDURE dead
  LOCAL row%,p%,k$
  row%=10
  FOR p%=1 TO pl%
    IF dead!(p%)
      PRINT AT(10,row%);"You're dead ";name$(p%);"!!!"
      ADD row%,2
    ENDIF
  NEXT p%
  REPEAT
    k$=UPPER$(INKEY$)
  UNTIL k$=CHR$(27) OR k$=" "
  IF k$=CHR$(27)
    @exit
  ENDIF
RETURN
> PROCEDURE sum
  LOCAL p%
  CLS
  PRINT AT(15,1);"Results:"
  PRINT AT(15,2);"--------"
  FOR p%=1 TO pl%
    PRINT AT(10,2+p%*4);name$(p%);":"
    PRINT AT(13,3+p%*4);games%-kills%(p%);" survivals."
  NEXT p%
  REPEAT
  UNTIL INKEY$=" "
RETURN
> PROCEDURE exit
  VSETCOLOR 0,7,7,7
  VSETCOLOR 1,0
  SHOWM
  SPOKE &H484,&X1111
  EDIT
RETURN
