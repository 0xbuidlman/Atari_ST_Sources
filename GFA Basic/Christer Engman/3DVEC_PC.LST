@init.screens
period%=1440
step%=5
ON BREAK GOSUB exit
@init
DO
  EXIT IF LEN(INKEY$)
  pos%=SUCC(pos%) MOD frames%
  @swap.screens
  @draw.vectors(pos%,1)
LOOP
@exit
'
> PROCEDURE init.screens
  HIDEM
  DIM scr|(32255)
  scr1%=XBIOS(3)
  scr2%=ARRPTR(scr|())+255 AND &HFFFF00
RETURN
> PROCEDURE swap.screens
  SWAP scr1%,scr2%
  ~XBIOS(5,L:scr1%,L:scr2%,-1)
  VSYNC
  '
  fr%=pos%-2
  IF fr%<0
    ADD fr%,frames%
  ENDIF
  @draw.vectors(fr%,0)
RETURN
> PROCEDURE init
  HIDEM
  @read.cords
  @init.cords
  @set.cols
RETURN
> PROCEDURE read.cords
  CLR cords%
  RESTORE cords
  REPEAT
    READ pos$
    INC cords%
  UNTIL pos$="END"
  points%=DIV(PRED(cords%),3)
  '
  DIM x(points%),y(points%),z(points%)
  RESTORE cords
  FOR n%=1 TO points%
    READ x(n%),y(n%),z(n%)
  NEXT n%
RETURN
> PROCEDURE init.cords
  frames%=period%/step%
  DIM x%(frames%,points%),y%(frames%,points%),col%(frames%,points%)
  VSETCOLOR 0,0
  VSETCOLOR 1,7,7,7
  FOR pos%=0 TO frames%
    PRINT AT(12,11);USING "###% calculated...",100*pos%/frames%
    '
    v%=step%*pos%
    '
    ax=180*COSQ(v%/2)
    ay=180*SINQ(v%)
    az=180*COSQ(v%/4)
    '
    cosz=COSQ(az)
    sinz=SINQ(az)
    cosy=COSQ(ay)
    siny=SINQ(ay)
    cosx=COSQ(ax)
    sinx=SINQ(ax)
    '
    FOR n%=1 TO points%
      x=x(n%)
      y=y(n%)
      z=z(n%)
      '
      x2=x*cosz+y*sinz
      y2=y*cosz-x*sinz
      z2=z*cosy+x2*siny
      x=x2*cosy-z*siny
      y=y2*cosx+z2*sinx
      z=z2*cosx-y2*sinx+150
      '
      fact=200/(200+z)
      '
      x%(pos%,n%)=160+x*fact
      y%(pos%,n%)=100+y*fact
      col%(pos%,n%)=(z+50)/60
    NEXT n%
  NEXT pos%
  CLS
RETURN
> PROCEDURE draw.vectors(p%,col%)
  FOR n%=1 TO points%-1 STEP 2
    x1%=x%(p%,n%)
    y1%=y%(p%,n%)
    x2%=x%(p%,n%+1)
    y2%=y%(p%,n%+1)
    IF col%<>0
      col%=7-(col%(p%,n%)+col%(p%,n%+1))/2
    ENDIF
    ALINE x1%,y1%,x2%,y2%,col%,&HFFFF,0
  NEXT n%
RETURN
> PROCEDURE set.cols
  FOR n%=0 TO 7
    SETCOLOR n%,n%,0,n%
  NEXT n%
RETURN
> PROCEDURE exit
  VSETCOLOR 0,7,7,7
  VSETCOLOR 1,0,0,0
  ACLIP 0,0,0,319,199
  EDIT
RETURN
'
> PROCEDURE data
  cords:
  ' "A"
  DATA -140,40,0,-110,-40,0
  DATA -110,-40,0,-80,40,0
  DATA -128.75,10,0,-91.25,10,0
  '
  ' "T"
  DATA -80,-40,0,-20,-40,0
  DATA -50,-40,0,-50,40,0
  '
  ' "A"
  DATA -20,40,0,10,-40,0
  DATA 10,-40,0,40,40,0
  DATA -8.75,10,0,28.75,10,0
  '
  ' "R"
  DATA 60,40,0,60,-40,0
  DATA 60,-40,0,110,-40,0
  DATA 110,-40,0,110,0,0
  DATA 110,0,0,60,0,0
  DATA 90,0,0,110,40,0
  '
  ' "I"
  DATA 140,-40,0,140,40,0
  '
  DATA END
RETURN
'
