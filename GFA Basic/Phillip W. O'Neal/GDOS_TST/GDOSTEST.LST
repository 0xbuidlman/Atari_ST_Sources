' RESERVE 2000000
$M300000
@init_variables
DO
  @event ! JUST WAIT FOR THE USER TO DO SOMETHING
LOOP
> PROCEDURE event
  '
  event_type&=EVNT_MULTI(&X110011,1,1,1,0,0,0,0,0,0,0,0,0,0,buffer%,1000,x_mouse&,y_mouse&,button_state&,key_state&,key_press&,num_clicks&)
  @check_event
  '
RETURN
> PROCEDURE init_variables
  '
  INLINE font_info%,1024
  '
  my_handle&=V~H  ! GET VDI HANDLE OF PROGRAMS WORKSTATION
  handle&=APPL_INIT()
  @check_jar
  '
  IF id$<>"_SPD"
    END
  ENDIF
  '
  OPTION BASE 0
  DIM font_name$(16384),face%(16384),pick%(16384)
  DIM buffer&(7)  ! EVENT BUFFER FOR EVENT_MULTI CALL
  buffer%=VARPTR(buffer&(0))
  DIM my_buffer&(7)
  my_buffer%=VARPTR(my_buffer&(0))
  '
  DEFFILL 0,2,8  ! COLOR 0 SOLID FILL PATTERN
  '
  current_drive%=GEMDOS(25)            ! DRIVE PROGRAM WAS RAN FROM
  my_path$=DIR$(current_drive%+1)+"\"  ! PATH     "     "   "   "
  IF MID$(my_path$,1,1)="\" THEN
    my_path$=CHR$(current_drive%+65)+":"+my_path$
  ENDIF
  rsc$=my_path$+"GDOSTEST.RSC"
  '
  @rsc_setup
  fonts%=VST_LOAD_FONTS(0)
  FOR x%=1 TO fonts%+1
    face%(x%)=VQT_NAME(x%,font_name$(x%))
    pick%(x%)=0
  NEXT x%
  CHAR{{OB_SPEC(main%,t_fonts%)}}=STR$(fonts%+1)
  CHAR{{OB_SPEC(title%,t_title_fonts%)}}=STR$(fonts%+1)
  CHAR{{OB_SPEC(pick%,t_pick_fonts%)}}=STR$(fonts%+1)
  IF fonts%<10 THEN
    FOR x%=fonts%+1 TO 9
      OB_STATE(main%,t_font_name%+x%)=BSET(OB_STATE(main%,t_font_name%+x%),3)
      OB_FLAGS(main%,t_font_name%+x%)=BCLR(OB_FLAGS(main%,t_font_name%+x%),0)
    NEXT x%
  ENDIF
  '
  view%=0
  min_size%=6
  max_size%=9999
  font%=1
  IF fonts%>1 THEN
    title_font%=2
  ELSE
    title_font%=1
  ENDIF
  points%=24
  @font_set
  @window_setup
  DEFMOUSE 0  ! BE SURE MOUSE SHAPE IS A POINTER
  '
RETURN
> PROCEDURE window_setup
  '
  INLINE window_title%,128
  ~MENU_BAR(menu%,1)           ! TURN MENU ON
  flag%=WIND_GET(0,4,mwx%,mwy%,mww%,mwh%)
  ~WIND_CALC(1,&X111111111,mwx%,mwy%,mww%,mwh%,d%,d%,iww%,iwh%)
  offset_width%=mww%-iww%
  offset_height%=mwh%-iwh%
  window_handle%=WIND_CREATE(&X111101111,mwx%,mwy%,mww%,mwh%)
  ~WIND_OPEN(window_handle%,mwx%,mwy%,mww%,mwh%)
  ~WIND_GET(window_handle%,5,window_x%,window_y%,window_w%,window_h%)
  ~WIND_SET(window_handle%,5,window_x%,window_y%,window_w%,window_h%)
  ~WIND_GET(window_handle%,4,window_ix%,window_iy%,window_iw%,window_ih%)
  @title
  @size_slider
  '
RETURN
> PROCEDURE check_event
  '
  IF event_type& AND &X1 THEN        ! KEY
    @event_keypress
    '
  ENDIF
  '
  IF event_type& AND &X10 THEN      ! BUTTON
    '
  ENDIF
  '
  IF event_type& AND &X10000 THEN   ! MENU OR WINDOW EVENT
    '
    command%=buffer&(0)       ! TYPE OF EVENT
    '
    IF command%=10 THEN             ! MENU ITEM
      @event_menu
    ELSE          ! WINDOW EVENT
      @event_window
    ENDIF
    '
  ENDIF
  '
RETURN
> PROCEDURE event_menu
  '
  tit%=buffer&(3)   ! WHICH MENU TITLE ?
  ~MENU_TNORMAL(menu%,tit%,1)  ! MAKE MENU TITLE NORMAL AGAIN
  item%=buffer&(4)  ! WHICH MENU ITEM WAS SELECTED
  '
  SELECT item%
  CASE m_about%  ! ABOUT
    ALERT 0," |Speedo font viewer  |½1995 by Phillip O'Neal  | ",1,"OK",d%
    '
  CASE m_set_font%  ! SET FONT
    @dialog
    '
  CASE m_print%     ! PRINT
    @on_line
    IF on_line%=1 THEN
      @print
    ENDIF
    '
  CASE m_list%      ! PRINT
    @on_line
    IF on_line%=1 THEN
      @print_list
    ENDIF
    '
  CASE m_pick%      ! PICK
    @pick_dialog
    '
  CASE m_title%     ! SET TITLE
    @title_dialog
    '
  CASE m_quit% ! QUIT
    trying_to_quit
    '
  ENDSELECT
  '
RETURN
> PROCEDURE event_window
  '
  SELECT command%
    '
  CASE 20     ! WIND UPDATE
    @repair
    '
  CASE 21     ! WINDOW CLICKED ON
    flag%=WIND_SET(window_handle%,10,0,0,0,0) ! WINDOW SET TO TOP
    DEFMOUSE 0
    '
  CASE 22 ! CLOSE WINDOW
    @trying_to_quit
    '
  CASE 23 ! FULL WINDOW
    '
    flag%=WIND_GET(window_handle%,7,window_x%,window_y%,window_w%,window_h%)
    flag%=WIND_SET(window_handle%,5,window_x%,window_y%,window_w%,window_h%)
    flag%=WIND_GET(window_handle%,4,window_ix%,window_iy%,window_iw%,window_ih%)
    @size_slider
    '
  CASE 24,26
    @event_slider
    '
  CASE 27,28 ! WINDOW SIZED, MOVED
    '
    window_x%=buffer&(4) ! GET NEW WINDOW POSITION/SIZE
    window_y%=buffer&(5)
    window_w%=buffer&(6)
    window_h%=buffer&(7)
    '
    flag%=WIND_SET(window_handle%,5,window_x%,window_y%,window_w%,window_h%)     ! SIZE WINDOW
    flag%=WIND_GET(window_handle%,4,window_ix%,window_iy%,window_iw%,window_ih%) ! GET INSIDE SIZE
    '
    IF command%=27 THEN
      @size_slider
      @update
    ENDIF
    '
  ENDSELECT
  '
RETURN
> PROCEDURE event_keypress
  '
  code&=SHR(key_press& AND &HFF00,8)
  ' UPPER BYTE OF WORD CONTAINS SCAN CODE
  '
  SELECT code&
    '
  CASE 25 ! 'P'RINT
    @on_line
    IF on_line%=1 THEN
      @print
    ENDIF
    '
  CASE 49 ! 'N'AME LIST
    @on_line
    IF on_line%=1 THEN
      @print_list
    ENDIF
    '
  CASE 37 ! PIC'K' FONTS
    @pick_dialog
    '
  CASE 16 ! 'Q'UIT
    trying_to_quit
    '
  CASE 33 ! (F)ONT SET
    @dialog
    '
  CASE 20 ! (T)ITLE FONT
    @title_dialog
    '
  CASE 80 ! DOWN
    command%=24
    SELECT key_state&
    CASE 0,1,4
      buffer&(4)=3
    CASE 2   ! SHIFT
      buffer&(4)=1
    ENDSELECT
    @event_slider
    '
  CASE 72 ! UP
    command%=24
    SELECT key_state&
    CASE 0,1,4
      buffer&(4)=2
    CASE 2   ! SHIFT
      buffer&(4)=0
    ENDSELECT
    @event_slider
    '
  CASE 71 ! CIR HOME
    command%=26
    SELECT key_state&
    CASE 0,1,4
      buffer&(4)=0
    CASE 2   ! SHIFT
      buffer&(4)=1000
    ENDSELECT
    @event_slider
    '
  ENDSELECT
  '
RETURN
> PROCEDURE event_slider
  '
  SELECT command%
  CASE 24
    slider_event%=buffer&(4)
    SELECT slider_event%
    CASE 2 ! 'UP'
      IF window_view%>0 THEN
        window_view%=window_view%-max_char%
        IF window_view%<0 THEN
          window_view%=0
        ENDIF
        @fix_slider
      ENDIF
      '
    CASE 0 ! 'ABOVE SLIDER'
      '
      IF window_view%>0 THEN
        window_view%=window_view%-(max_char%*max_row%)
        IF window_view%<0 THEN
          window_view%=0
        ENDIF
        @fix_slider
      ENDIF
      '
    CASE 1 ! 'BELOW SLIDER'
      '
      IF window_view%<last_char% THEN
        window_view%=window_view%+max_char%*max_row%
        IF window_view%>last_char% THEN
          window_view%=last_char%
        ENDIF
        @fix_slider
      ENDIF
      '
    CASE 3 ! 'DOWN'
      '
      IF window_view%<last_char% THEN
        window_view%=window_view%+max_char%
        IF window_view%>last_char% THEN
          window_view%=last_char%
        ENDIF
        @fix_slider
      ENDIF
      '
    ENDSELECT
  CASE 26 ! 'SLIDER BOX'
    old_window_view%=window_view%
    window_view%=(last_char%+1)*(buffer&(4)/1000)
    IF old_window_view%<>window_view%
      @fix_slider
    ENDIF
    '
  ENDSELECT
  '
RETURN
> PROCEDURE fix_slider
  '
  slider_position%=(window_view%/(last_char%+1))*1000
  flag%=WIND_SET(window_handle%,9,slider_position%,0,0,0)
  @update
  '
RETURN
> PROCEDURE size_slider
  '
  max_char%=INT(window_iw%/char_width%)
  IF max_char%<1 THEN
    max_char%=1
  ENDIF
  max_row%=INT(window_ih%/char_height%)
  IF max_row%<1 THEN
    max_row%=1
  ENDIF
  '
  slider_size%=((max_char%*max_row%)/(last_char%+max_char%*max_row%))*1000
  IF slider_size%>999 THEN
    window_view%=0
    slider_position%=0
  ENDIF
  '
  flag%=WIND_SET(window_handle%,9,slider_position%,0,0,0)
  flag%=WIND_SET(window_handle%,16,slider_size%,0,0,0)
  '
RETURN
> PROCEDURE title
  window_title$=font_name$+", "+STR$(points%)+" points, "+STR$(total_chars%)+" used."+CHR$(0)
  CHAR{window_title%}=window_title$
  flag%=WIND_SET(window_handle%,2,CARD(SWAP(window_title%)),CARD(window_title%),0,0)
RETURN
> PROCEDURE repair
  '
  ~WIND_UPDATE(1)      ! TELL GEM WE ARE TAKING OVER
  '
  '                    GET FIRST RECTANGLE
  ~WIND_GET(window_handle%,11,fix_x%,fix_y%,fix_w%,fix_h%)
  '
  @draw
  fix_done%=0
  '
  DO
    '                  GET NEXT RECTANGLE
    ~WIND_GET(window_handle%,12,fix_x%,fix_y%,fix_w%,fix_h%)
    '
    IF fix_w%+fix_h%=0 THEN   ! ANY MORE RECTANGLES ?
      fix_done%=1
    ELSE
      @draw
    ENDIF
    '
  LOOP UNTIL fix_done%=1
  '
  ~WIND_UPDATE(0)          ! DONE!
  '
RETURN
> PROCEDURE update
  '
  my_buffer&(0)=20
  my_buffer&(3)=window_handle%
  my_buffer&(4)=window_ix%
  my_buffer&(5)=window_iy%
  my_buffer&(6)=window_iw%
  my_buffer&(7)=window_ih%
  ~APPL_WRITE(handle&,16,my_buffer%)
  '
RETURN
> PROCEDURE draw
  '
  CLIP fix_x%,fix_y%,fix_w%,fix_h%
  PBOX fix_x%,fix_y%,fix_x%+fix_w%-1,fix_y%+fix_h%-1
  x&=0
  y&=0
  char&=window_view%
  exit%=0
  DO WHILE char&<=last_char%
    length&=1
    @show_text
    INC x&
    IF x&=max_char% THEN
      x&=0
      INC y&
      IF y&>max_row% THEN
        exit%=1
      ENDIF
    ENDIF
    INC char&
  LOOP UNTIL exit%
  '
RETURN
> PROCEDURE trying_to_quit
  '
  txt$=" |Sure you want to quit ?  | "
  ALERT 2,txt$,1,"OK|Cancel",button%
  '
  IF button%=1 THEN
    '
    ~VST_UNLOAD_FONTS(0)
    ~WIND_CLOSE(window_handle%)
    ~WIND_DELETE(window_handle%)
    '
    ~MENU_BAR(stis_menu%,0)
    ~RSRC_FREE()
    END
    '
  ENDIF
  '
RETURN
> PROCEDURE rsc_setup
  '
  IF EXIST(rsc$) THEN
    rsc%=RSRC_LOAD(rsc$)
  ELSE ! CAN'T FIND RSC FILE
    END
  ENDIF
  '
  ~RSRC_GADDR(0,0,menu%)
  ~RSRC_GADDR(0,1,main%)
  ~RSRC_GADDR(0,2,title%)
  ~RSRC_GADDR(0,3,pick%)
  '
  m_about%=7
  m_set_font%=16
  m_title%=17
  m_pick%=18
  m_print%=20
  m_list%=21
  m_quit%=23
  '
  t_font_name%=5
  t_fonts%=15
  b_font_up10%=17
  b_font_up%=18
  b_font_down%=19
  b_font_down10%=20
  t_points%=21
  b_points_bigdown%=23
  b_points_down%=24
  b_points_up%=25
  b_points_bigup%=26
  b_cancel%=27
  b_ok%=28
  '
  t_title_name%=5
  t_title_fonts%=15
  b_title_up10%=17
  b_title_up%=18
  b_title_down%=19
  b_title_down10%=20
  b_title_cancel%=21
  b_title_ok%=22
  '
  t_pick_name%=5
  t_pick_fonts%=15
  b_pick_up10%=17
  b_pick_up%=18
  b_pick_down%=19
  b_pick_down10%=20
  b_pick_set%=21
  b_pick_clear%=22
  b_pick_toggle%=23
  b_pick_ok%=24
  '
  ~FORM_CENTER(main%,main_x%,main_y%,main_w%,main_h%)
  ~FORM_CENTER(title%,title_x%,title_y%,title_w%,title_h%)
  ~FORM_CENTER(pick%,pick_x%,pick_y%,pick_w%,pick_h%)
  '
RETURN
> PROCEDURE check_jar
  jar%=LPEEK(1440) ! ADDRESS OF COOKIE JAR
  IF jar% THEN     ! IF JAR PRESENT THEN jar%=ADDRESS OF JAR
    cookie_num%=0
    DO
      cookie%=LPEEK(jar%+cookie_num%*8)
      cookie$=CHR$(SHR(cookie% AND &HFF000000,24))
      cookie$=cookie$+CHR$(SHR(cookie% AND &HFF0000,16))
      cookie$=cookie$+CHR$(SHR(cookie% AND &HFF00,8))
      cookie$=cookie$+CHR$(cookie% AND &HFF)
      IF cookie$="FSMC" THEN
        '
        id%=LPEEK(LPEEK(jar%+cookie_num%*8+4))
        id$=CHR$(SHR(id% AND &HFF000000,24))
        id$=id$+CHR$(SHR(id% AND &HFF0000,16))
        id$=id$+CHR$(SHR(id% AND &HFF00,8))
        id$=id$+CHR$(id% AND &HFF)
        '
      ENDIF
      INC cookie_num%
    LOOP UNTIL cookie%=0
  ENDIF
RETURN
> PROCEDURE font_set
  '
  face%=VQT_NAME(font%,font_name$)
  DEFTEXT ,,,,face%
  '
  CONTRL(0)=236  ! VST_CHARMAP
  CONTRL(1)=0
  CONTRL(3)=1
  CONTRL(6)=my_handle&
  INTIN(0)=0
  VDISYS
  '
  CONTRL(0)=246  ! VST_ARBPT
  CONTRL(1)=0
  CONTRL(3)=1
  CONTRL(6)=my_handle&
  INTIN(0)=points%
  VDISYS
  char_width%=PTSOUT(0)
  char_height%=PTSOUT(1)*1.25
  '
  CONTRL(0)=131  ! VQT_FONTINFO
  CONTRL(1)=0
  CONTRL(3)=0
  CONTRL(6)=my_handle&
  VDISYS
  first_char%=INTOUT(0)
  last_char%=INTOUT(1)
  total_chars%=last_char%+1-first_char%
  '
RETURN
> PROCEDURE show_text
  '
  GRAPHMODE 2
  CONTRL(0)=241  ! V_FTEXT16
  CONTRL(1)=1
  CONTRL(3)=1
  CONTRL(6)=my_handle&
  PTSIN(0)=window_ix%+x&*char_width%+1
  PTSIN(1)=window_iy%+y&*char_height%+char_height%
  INTIN(0)=char&
  INTIN(1)=0
  VDISYS
  GRAPHMODE 1
  '
RETURN
> PROCEDURE dialog
  '
  old_points%=points%
  er%=WIND_UPDATE(1)
  er%=WIND_UPDATE(3)
  view%=font%-1
  IF view%>fonts%-9 THEN
    view%=fonts%-9
    IF view%<0 THEN
      view%=0
    ENDIF
  ENDIF
  @set_text
  '
  pick_done%=0
  '
  ~FORM_DIAL(0,0,0,0,0,main_x%,main_y%,main_w%,main_h%)
  ~OBJC_DRAW(main%,0,2,main_x%,main_y%,main_w%,main_h%)
  DO
    pick_exit%=FORM_DO(main%,0)
    pick_exit%=pick_exit% AND &H7FFF
    '
    SELECT pick_exit%
      '
    CASE b_font_up10%
      IF view%>0 THEN
        view%=view%-10
        IF view%<0 THEN
          view%=0
        ENDIF
        @set_text
        ~OBJC_DRAW(main%,t_font_name%-1,2,main_x%,main_y%,main_w%,main_h%)
      ENDIF
      OB_STATE(main%,pick_exit%)=BCLR(OB_STATE(main%,pick_exit%),0)
      ~OBJC_DRAW(main%,pick_exit%,2,main_x%,main_y%,main_w%,main_h%)
      '
    CASE b_font_up%
      IF view%>0 THEN
        view%=view%-1
        IF view%<0 THEN
          view%=0
        ENDIF
        @set_text
        ~OBJC_DRAW(main%,t_font_name%-1,2,main_x%,main_y%,main_w%,main_h%)
      ENDIF
      OB_STATE(main%,pick_exit%)=BCLR(OB_STATE(main%,pick_exit%),0)
      ~OBJC_DRAW(main%,pick_exit%,2,main_x%,main_y%,main_w%,main_h%)
      '
    CASE b_font_down%
      IF view%<fonts%-9 THEN
        view%=view%+1
        IF view%>fonts%-9 THEN
          view%=fonts%-9
          IF view%<0 THEN
            view%=0
          ENDIF
        ENDIF
        @set_text
        ~OBJC_DRAW(main%,t_font_name%-1,2,main_x%,main_y%,main_w%,main_h%)
      ENDIF
      OB_STATE(main%,pick_exit%)=BCLR(OB_STATE(main%,pick_exit%),0)
      ~OBJC_DRAW(main%,pick_exit%,2,main_x%,main_y%,main_w%,main_h%)
      '
    CASE b_font_down10%
      IF view%<fonts%-9 THEN
        view%=view%+10
        IF view%>fonts%-9 THEN
          view%=fonts%-9
          IF view%<0 THEN
            view%=0
          ENDIF
        ENDIF
        @set_text
        ~OBJC_DRAW(main%,t_font_name%-1,2,main_x%,main_y%,main_w%,main_h%)
      ENDIF
      OB_STATE(main%,pick_exit%)=BCLR(OB_STATE(main%,pick_exit%),0)
      ~OBJC_DRAW(main%,pick_exit%,2,main_x%,main_y%,main_w%,main_h%)
      '
    CASE b_points_up%
      points%=points%+4
      @check_points
      CHAR{{OB_SPEC(main%,t_points%)}}=STR$(points%)
      OB_STATE(main%,pick_exit%)=BCLR(OB_STATE(main%,pick_exit%),0)
      ~OBJC_DRAW(main%,pick_exit%,2,main_x%,main_y%,main_w%,main_h%)
      ~OBJC_DRAW(main%,t_points%,2,main_x%,main_y%,main_w%,main_h%)
      '
    CASE b_points_bigup%
      points%=points%+16
      @check_points
      CHAR{{OB_SPEC(main%,t_points%)}}=STR$(points%)
      OB_STATE(main%,pick_exit%)=BCLR(OB_STATE(main%,pick_exit%),0)
      ~OBJC_DRAW(main%,pick_exit%,2,main_x%,main_y%,main_w%,main_h%)
      ~OBJC_DRAW(main%,t_points%,2,main_x%,main_y%,main_w%,main_h%)
      '
    CASE b_points_down%
      points%=points%-4
      @check_points
      CHAR{{OB_SPEC(main%,t_points%)}}=STR$(points%)
      OB_STATE(main%,pick_exit%)=BCLR(OB_STATE(main%,pick_exit%),0)
      ~OBJC_DRAW(main%,pick_exit%,2,main_x%,main_y%,main_w%,main_h%)
      ~OBJC_DRAW(main%,t_points%,2,main_x%,main_y%,main_w%,main_h%)
      '
    CASE b_points_bigdown%
      points%=points%-16
      @check_points
      CHAR{{OB_SPEC(main%,t_points%)}}=STR$(points%)
      OB_STATE(main%,pick_exit%)=BCLR(OB_STATE(main%,pick_exit%),0)
      ~OBJC_DRAW(main%,pick_exit%,2,main_x%,main_y%,main_w%,main_h%)
      ~OBJC_DRAW(main%,t_points%,2,main_x%,main_y%,main_w%,main_h%)
      '
    CASE b_ok%
      OB_STATE(main%,pick_exit%)=BCLR(OB_STATE(main%,pick_exit%),0)
      pick_done%=1
      points%=VAL(CHAR{{OB_SPEC(main%,t_points%)}})
      @check_points
      @check_font
      @title
      @size_slider
      IF window_view%>last_char% THEN
        window_view%=last_char%
      ENDIF
      @fix_slider
      @update
      '
    CASE b_cancel%
      OB_STATE(main%,pick_exit%)=BCLR(OB_STATE(main%,pick_exit%),0)
      points%=old_points%
      pick_done%=2
      '
    ENDSELECT
    '
  LOOP UNTIL pick_done%>0
  '
  ~FORM_DIAL(3,0,0,0,0,main_x%,main_y%,main_w%,main_h%)
  er%=WIND_UPDATE(2)
  er%=WIND_UPDATE(0)
  '
RETURN
> PROCEDURE set_text
  CHAR{{OB_SPEC(main%,t_points%)}}=STR$(points%)
  FOR x%=0 TO 9
    IF face%(x%+view%+1)>0 THEN
      temp$=STR$(face%(x%+view%+1))
    ELSE
      temp$=""
    ENDIF
    IF LEN(temp$)<5 THEN
      temp$=SPACE$(5-LEN(temp$))+temp$
    ENDIF
    CHAR{{OB_SPEC(main%,t_font_name%+x%)}}=temp$+" "+font_name$(x%+view%+1)
    IF x%+view%+1=font% THEN
      OB_STATE(main%,t_font_name%+x%)=BSET(OB_STATE(main%,t_font_name%+x%),0)
    ELSE
      OB_STATE(main%,t_font_name%+x%)=BCLR(OB_STATE(main%,t_font_name%+x%),0)
    ENDIF
  NEXT x%
RETURN
> PROCEDURE check_points
  IF points%>max_size% THEN
    points%=max_size%
  ENDIF
  IF points%<min_size% THEN
    points%=min_size%
  ENDIF
RETURN
> PROCEDURE check_font
  FOR x%=0 TO 9
    IF BTST(OB_STATE(main%,t_font_name%+x%),0) THEN
      font%=x%+view%+1
      @font_set
    ENDIF
  NEXT x%
  IF font%=0 THEN
    font%=fonts%
    @font_set
  ENDIF
RETURN
> PROCEDURE title_dialog
  er%=WIND_UPDATE(1)
  er%=WIND_UPDATE(3)
  temp_title_font%=title_font%
  view%=title_font%-1
  IF view%>fonts%-9 THEN
    view%=fonts%-9
    IF view%<0 THEN
      view%=0
    ENDIF
  ENDIF
  @set_title_text
  '
  pick_done%=0
  '
  ~FORM_DIAL(0,0,0,0,0,title_x%,title_y%,title_w%,title_h%)
  ~OBJC_DRAW(title%,0,2,title_x%,title_y%,title_w%,title_h%)
  DO
    pick_exit%=FORM_DO(title%,0)
    pick_exit%=pick_exit% AND &H7FFF
    '
    SELECT pick_exit%
      '
    CASE b_title_up10%
      @check_title_font
      IF view%>0 THEN
        view%=view%-10
        IF view%<0 THEN
          view%=0
        ENDIF
        @set_title_text
        ~OBJC_DRAW(title%,t_title_name%-1,2,title_x%,title_y%,title_w%,title_h%)
      ENDIF
      OB_STATE(title%,pick_exit%)=BCLR(OB_STATE(title%,pick_exit%),0)
      ~OBJC_DRAW(title%,pick_exit%,2,title_x%,title_y%,title_w%,title_h%)
      '
    CASE b_title_up%
      @check_title_font
      IF view%>0 THEN
        view%=view%-1
        IF view%<0 THEN
          view%=0
        ENDIF
        @set_title_text
        ~OBJC_DRAW(title%,t_title_name%-1,2,title_x%,title_y%,title_w%,title_h%)
      ENDIF
      OB_STATE(title%,pick_exit%)=BCLR(OB_STATE(title%,pick_exit%),0)
      ~OBJC_DRAW(title%,pick_exit%,2,title_x%,title_y%,title_w%,title_h%)
      '
    CASE b_title_down%
      @check_title_font
      IF view%<fonts%-9 THEN
        view%=view%+1
        IF view%>fonts%-9 THEN
          view%=fonts%-9
          IF view%<0 THEN
            view%=0
          ENDIF
        ENDIF
        @set_title_text
        ~OBJC_DRAW(title%,t_title_name%-1,2,title_x%,title_y%,title_w%,title_h%)
      ENDIF
      OB_STATE(title%,pick_exit%)=BCLR(OB_STATE(title%,pick_exit%),0)
      ~OBJC_DRAW(title%,pick_exit%,2,title_x%,title_y%,title_w%,title_h%)
      '
    CASE b_title_down10%
      @check_title_font
      IF view%<fonts%-9 THEN
        view%=view%+10
        IF view%>fonts%-9 THEN
          view%=fonts%-9
          IF view%<0 THEN
            view%=0
          ENDIF
        ENDIF
        @set_title_text
        ~OBJC_DRAW(title%,t_title_name%-1,2,title_x%,title_y%,title_w%,title_h%)
      ENDIF
      OB_STATE(title%,pick_exit%)=BCLR(OB_STATE(title%,pick_exit%),0)
      ~OBJC_DRAW(title%,pick_exit%,2,title_x%,title_y%,title_w%,title_h%)
      '
    CASE b_title_ok%
      OB_STATE(title%,pick_exit%)=BCLR(OB_STATE(title%,pick_exit%),0)
      pick_done%=1
      FOR x%=0 TO 9
        IF BTST(OB_STATE(title%,t_title_name%+x%),0) THEN
          title_font%=x%+view%+1
        ENDIF
      NEXT x%
      IF title_font%=0 THEN
        title_font%=fonts%
      ENDIF
      '
    CASE b_title_cancel%
      OB_STATE(title%,pick_exit%)=BCLR(OB_STATE(title%,pick_exit%),0)
      pick_done%=2
      '
    ENDSELECT
    '
  LOOP UNTIL pick_done%>0
  '
  ~FORM_DIAL(3,0,0,0,0,title_x%,title_y%,title_w%,title_h%)
  er%=WIND_UPDATE(2)
  er%=WIND_UPDATE(0)
  '
RETURN
> PROCEDURE set_title_text
  FOR x%=0 TO 9
    IF face%(x%+view%+1)>0 THEN
      temp$=STR$(face%(x%+view%+1))
      IF LEN(temp$)<5 THEN
        temp$=SPACE$(5-LEN(temp$))+temp$
      ENDIF
      CHAR{{OB_SPEC(title%,t_title_name%+x%)}}=temp$+" "+font_name$(x%+view%+1)
    ELSE
      CHAR{{OB_SPEC(title%,t_title_name%+x%)}}=""
    ENDIF
    IF x%+view%+1=temp_title_font% THEN
      OB_STATE(title%,t_title_name%+x%)=BSET(OB_STATE(title%,t_title_name%+x%),0)
    ELSE
      OB_STATE(title%,t_title_name%+x%)=BCLR(OB_STATE(title%,t_title_name%+x%),0)
    ENDIF
  NEXT x%
RETURN
> PROCEDURE check_title_font
  FOR x%=0 TO 9
    IF BTST(OB_STATE(title%,t_title_name%+x%),0) THEN
      temp_title_font%=x%+view%+1
    ENDIF
  NEXT x%
  IF temp_title_font%=0 THEN
    temp_title_font%=fonts%
  ENDIF
RETURN
> PROCEDURE pick_dialog
  er%=WIND_UPDATE(1)
  er%=WIND_UPDATE(3)
  view%=pick_font%
  IF view%>fonts%-9 THEN
    view%=fonts%-9
    IF view%<0 THEN
      view%=0
    ENDIF
  ENDIF
  @set_pick_text
  '
  pick_done%=0
  '
  ~FORM_DIAL(0,0,0,0,0,pick_x%,pick_y%,pick_w%,pick_h%)
  ~OBJC_DRAW(pick%,0,2,pick_x%,pick_y%,pick_w%,pick_h%)
  DO
    pick_exit%=FORM_DO(pick%,0)
    pick_exit%=pick_exit% AND &H7FFF
    '
    SELECT pick_exit%
      '
    CASE b_pick_up10%
      @check_pick_font
      IF view%>0 THEN
        view%=view%-9
        IF view%<0 THEN
          view%=0
        ENDIF
        @set_pick_text
        ~OBJC_DRAW(pick%,t_pick_name%-1,2,pick_x%,pick_y%,pick_w%,pick_h%)
      ENDIF
      '
    CASE b_pick_up%
      @check_pick_font
      IF view%>0 THEN
        view%=view%-1
        IF view%<0 THEN
          view%=0
        ENDIF
        @set_pick_text
        ~OBJC_DRAW(pick%,t_pick_name%-1,2,pick_x%,pick_y%,pick_w%,pick_h%)
      ENDIF
      '
    CASE b_pick_down%
      @check_pick_font
      IF view%<fonts%-9 THEN
        view%=view%+1
        IF view%>fonts%-9 THEN
          view%=fonts%-9
          IF view%<0 THEN
            view%=0
          ENDIF
        ENDIF
        @set_pick_text
        ~OBJC_DRAW(pick%,t_pick_name%-1,2,pick_x%,pick_y%,pick_w%,pick_h%)
      ENDIF
      '
    CASE b_pick_down10%
      @check_pick_font
      IF view%<fonts%-9 THEN
        view%=view%+10
        IF view%>fonts%-9 THEN
          view%=fonts%-9
          IF view%<0 THEN
            view%=0
          ENDIF
        ENDIF
        @set_pick_text
        ~OBJC_DRAW(pick%,t_pick_name%-1,2,pick_x%,pick_y%,pick_w%,pick_h%)
      ENDIF
      '
    CASE b_pick_set%
      FOR x%=1 TO fonts%+1
        pick%(x%)=1
      NEXT x%
      @set_pick_text
      ~OBJC_DRAW(pick%,t_pick_name%-1,2,pick_x%,pick_y%,pick_w%,pick_h%)
      '
    CASE b_pick_clear%
      FOR x%=1 TO fonts%+1
        pick%(x%)=0
      NEXT x%
      @set_pick_text
      ~OBJC_DRAW(pick%,t_pick_name%-1,2,pick_x%,pick_y%,pick_w%,pick_h%)
      '
    CASE b_pick_toggle%
      @check_pick_font
      FOR x%=1 TO fonts%+1
        IF pick%(x%)=0 THEN
          pick%(x%)=1
        ELSE
          pick%(x%)=0
        ENDIF
      NEXT x%
      @set_pick_text
      ~OBJC_DRAW(pick%,t_pick_name%-1,2,pick_x%,pick_y%,pick_w%,pick_h%)
      '
    CASE b_pick_ok%
      pick_done%=1
      pick_font%=view%
      @check_pick_font
      '
    ENDSELECT
    OB_STATE(pick%,pick_exit%)=BCLR(OB_STATE(pick%,pick_exit%),0)
    ~OBJC_DRAW(pick%,pick_exit%,2,pick_x%,pick_y%,pick_w%,pick_h%)
    '
  LOOP UNTIL pick_done%>0
  '
  ~FORM_DIAL(3,0,0,0,0,title_x%,title_y%,title_w%,title_h%)
  er%=WIND_UPDATE(2)
  er%=WIND_UPDATE(0)
  '
RETURN
> PROCEDURE set_pick_text
  FOR x%=0 TO 9
    temp$=SPACE$(5)
    IF face%(x%+view%+1)>0 THEN
      temp$=STR$(face%(x%+view%+1))
      IF LEN(temp$)<5 THEN
        temp$=SPACE$(5-LEN(temp$))+temp$
      ENDIF
    ENDIF
    CHAR{{OB_SPEC(pick%,t_pick_name%+x%)}}=temp$+" "+font_name$(x%+view%+1)
    IF pick%(x%+view%+1)=1 THEN
      OB_STATE(pick%,t_pick_name%+x%)=BSET(OB_STATE(pick%,t_pick_name%+x%),0)
    ELSE
      OB_STATE(pick%,t_pick_name%+x%)=BCLR(OB_STATE(pick%,t_pick_name%+x%),0)
    ENDIF
  NEXT x%
RETURN
> PROCEDURE check_pick_font
  FOR x%=0 TO 9
    IF BTST(OB_STATE(pick%,t_pick_name%+x%),0) THEN
      pick%(x%+view%+1)=1
    ELSE
      pick%(x%+view%+1)=0
    ENDIF
  NEXT x%
RETURN
> PROCEDURE open_workstation
  CONTRL(0)=1  ! V_OPNWK
  CONTRL(1)=0
  CONTRL(3)=11
  CONTRL(6)=0
  INTIN(0)=21
  INTIN(1)=1
  INTIN(2)=1
  INTIN(3)=1
  INTIN(4)=1
  INTIN(5)=font%
  INTIN(6)=1
  INTIN(7)=1
  INTIN(8)=1
  INTIN(9)=1
  INTIN(10)=2
  VDISYS
  gdos_handle%=CONTRL(6)
  '
  IF gdos_handle%<>0 THEN
    '
    gdos_x_rez%=INTOUT(0)+1
    gdos_y_rez%=INTOUT(1)+1
    '
  ENDIF
  '
RETURN
> PROCEDURE setup_workstation
  IF gdos_handle%<>0 THEN
    V~H=gdos_handle%
    '
    CLIP 0,0,gdos_x_rez%-1,gdos_y_rez%-1
    '
    CONTRL(0)=119  ! VST_LOAD_FONTS
    CONTRL(1)=0
    CONTRL(3)=1
    CONTRL(6)=gdos_handle%
    INTIN(0)=0
    VDISYS
    '
  ENDIF
RETURN
> PROCEDURE close_workstation
  '
  ~VST_UNLOAD_FONTS(0)
  '
  CONTRL(0)=2  ! V_CLSWK
  CONTRL(1)=0
  CONTRL(3)=0
  CONTRL(6)=gdos_handle%
  VDISYS
  '
  V~H=-1
RETURN
> PROCEDURE print
  @open_workstation
  IF gdos_handle%<>0 THEN
    @setup_workstation
    '
    total%=0
    FOR current_font%=2 TO fonts%
      IF pick%(current_font%)=1 THEN
        INC total%
        print_do
        x&=0
        y&=0
        char&=gdos_first_char%
        exit%=0
        GRAPHMODE 2
        '
        DO
          CONTRL(0)=241  ! V_FTEXT16
          CONTRL(1)=1
          CONTRL(3)=1
          CONTRL(6)=gdos_handle%
          PTSIN(0)=x&*gdos_char_width%
          PTSIN(1)=y&*gdos_char_height%+title_height%
          INTIN(0)=char&
          INTIN(1)=0
          VDISYS
          INC x&
          IF x&=gdos_max_char% THEN
            x&=0
            INC y&
            IF y&>gdos_max_row% THEN
              exit%=1
            ENDIF
          ENDIF
          INC char&
          IF char&>gdos_last_char% THEN
            exit%=1
          ENDIF
        LOOP UNTIL exit%=1
        GRAPHMODE 1
        '
        @eject_page
        '
      ENDIF
    NEXT current_font%
    IF total%=0 AND font%<>1 THEN
      current_font%=font%
      print_do
      x&=0
      y&=0
      char&=gdos_first_char%
      exit%=0
      GRAPHMODE 2
      '
      DO
        CONTRL(0)=241  ! V_FTEXT16
        CONTRL(1)=1
        CONTRL(3)=1
        CONTRL(6)=gdos_handle%
        PTSIN(0)=x&*gdos_char_width%
        PTSIN(1)=y&*gdos_char_height%+title_height%
        INTIN(0)=char&
        INTIN(1)=0
        VDISYS
        INC x&
        IF x&=gdos_max_char% THEN
          x&=0
          INC y&
          IF y&>gdos_max_row% THEN
            exit%=1
          ENDIF
        ENDIF
        INC char&
        IF char&>gdos_last_char% THEN
          exit%=1
        ENDIF
      LOOP UNTIL exit%=1
      GRAPHMODE 1
      '
      @eject_page
      '
    ENDIF
    @close_workstation
  ENDIF
RETURN
> PROCEDURE print_do
  gdos_points%=128
  gdos_font%=current_font%
  '
  gdos_face%=VQT_NAME(gdos_font%,gdos_font_name$)
  DEFTEXT 1,0,,,gdos_face%
  '
  CONTRL(0)=39  ! VST_ALIGNMENT
  CONTRL(1)=0
  CONTRL(3)=2
  CONTRL(6)=gdos_handle%
  INTIN(0)=0    ! LEFT JUSTIFIED
  INTIN(1)=5    ! TOP LINE
  VDISYS
  '
  CONTRL(0)=236  ! VST_CHARMAP
  CONTRL(1)=0
  CONTRL(3)=1
  CONTRL(6)=gdos_handle%
  INTIN(0)=0
  VDISYS
  '
  CONTRL(0)=131  ! VQT_FONTINFO
  CONTRL(1)=0
  CONTRL(3)=0
  CONTRL(6)=gdos_handle%
  VDISYS
  gdos_first_char%=INTOUT(0)
  gdos_last_char%=INTOUT(1)
  '
  gdos_chars%=gdos_last_char%+1-gdos_first_char%
  '
  title_face%=VQT_NAME(title_font%,title_name$)
  DEFTEXT 1,0,,,title_face%
  '
  CONTRL(0)=236  ! VST_CHARMAP
  CONTRL(1)=0
  CONTRL(3)=1
  CONTRL(6)=gdos_handle%
  INTIN(0)=1
  VDISYS
  '
  CONTRL(0)=246  ! VST_ARBPT
  CONTRL(1)=0
  CONTRL(3)=1
  CONTRL(6)=gdos_handle%
  INTIN(0)=12
  VDISYS
  '
  temp$=gdos_font_name$+" ("+STR$(gdos_chars%)+" characters used)"
  ~VQT_EXTENT(temp$,d%,d%,d%,d%,d%,title_height%,d%,d%)
  '
  TEXT 0,0,temp$
  '
  gdos_face%=VQT_NAME(gdos_font%,gdos_font_name$)
  DEFTEXT 1,0,,,gdos_face%
  '
  CONTRL(0)=236  ! VST_CHARMAP
  CONTRL(1)=0
  CONTRL(3)=1
  CONTRL(6)=gdos_handle%
  INTIN(0)=0
  VDISYS
  '
  exit%=0
  DO
    CONTRL(0)=246  ! VST_ARBPT
    CONTRL(1)=0
    CONTRL(3)=1
    CONTRL(6)=gdos_handle%
    INTIN(0)=gdos_points%
    VDISYS
    gdos_char_width%=PTSOUT(0)
    gdos_char_height%=PTSOUT(1)*1.25
    '
    gdos_max_char%=INT(gdos_x_rez%/gdos_char_width%)
    IF gdos_max_char%<1 THEN
      gdos_max_char%=1
    ENDIF
    gdos_max_row%=INT((gdos_y_rez%-title_height%)/gdos_char_height%)
    IF gdos_max_row%<1 THEN
      gdos_max_row%=1
    ENDIF
    '
    temp%=gdos_max_char%*gdos_max_row%
    '
    IF temp%<gdos_chars% THEN
      DEC gdos_points%
      '
    ELSE
      IF temp%>gdos_chars%*1.25 THEN
        INC gdos_points%
        '
      ELSE
        exit%=1
      ENDIF
      '
    ENDIF
    '
  LOOP UNTIL exit%=1
  '
RETURN
> PROCEDURE print_list
  @open_workstation
  IF gdos_handle%<>0 AND fonts%>1 THEN
    @setup_list_workstation
    '
    GRAPHMODE 2
    current_font%=2
    x&=0
    y&=0
    current_width%=0
    current_height%=0
    current_num_width%=0
    DO
      '
      IF face%(current_font%)>0 THEN
        temp$=STR$(face%(current_font%))
      ELSE
        temp$=""
      ENDIF
      IF LEN(temp$)<5 THEN
        temp$=SPACE$(5-LEN(temp$))+temp$
      ENDIF
      ~VQT_EXTENT(temp$,d%,d%,d%,d%,temp_width%,d%,d%,d%)
      current_num_width%=MAX(current_num_width%,temp_width%)
      '
      temp$=" "+font_name$(current_font%)+" "
      ~VQT_EXTENT(temp$,d%,d%,d%,d%,temp_width%,temp_height%,d%,d%)
      current_width%=MAX(current_width%,temp_width%)
      current_height%=MAX(current_height%,temp_height%)
      '
      INC current_font%
    LOOP UNTIL current_font%>fonts%+1
    '
    current_font%=2
    x&=0
    y&=0
    DO
      '
      IF face%(current_font%)>0 THEN
        temp$=STR$(face%(current_font%))
      ELSE
        temp$=""
      ENDIF
      ~VQT_EXTENT(temp$,d%,d%,d%,d%,temp_width%,d%,d%,d%)
      TEXT x&+current_num_width%-temp_width%,y&,temp$
      '
      temp$=" "+font_name$(current_font%)+"  "
      TEXT x&+current_num_width%,y&,temp$
      '
      x&=x&+current_num_width%+current_width%
      IF x&+current_num_width%+current_width%>gdos_x_rez% THEN
        x&=0
        y&=y&+current_height%
        IF y&+current_height%>gdos_y_rez% THEN
          @eject_page
          x&=0
          y&=0
          '
        ENDIF
      ENDIF
      '
      INC current_font%
    LOOP UNTIL current_font%>fonts%+1
    '
    GRAPHMODE 1
    '
    @eject_page
    ~VST_UNLOAD_FONTS(0)
    @close_workstation
  ENDIF
RETURN
> PROCEDURE setup_list_workstation
  IF gdos_handle%<>0 THEN
    V~H=gdos_handle%
    '
    CLIP 0,0,gdos_x_rez%-1,gdos_y_rez%-1
    '
    CONTRL(0)=119  ! VST_LOAD_FONTS
    CONTRL(1)=0
    CONTRL(3)=1
    CONTRL(6)=gdos_handle%
    INTIN(0)=0
    VDISYS
    '
    title_face%=VQT_NAME(title_font%,title_name$)
    DEFTEXT 1,0,,,title_face%
    '
    CONTRL(0)=236  ! VST_CHARMAP
    CONTRL(1)=0
    CONTRL(3)=1
    CONTRL(6)=gdos_handle%
    INTIN(0)=1
    VDISYS
    '
    CONTRL(0)=39  ! VST_ALIGNMENT
    CONTRL(1)=0
    CONTRL(3)=2
    CONTRL(6)=gdos_handle%
    INTIN(0)=0    ! LEFT JUSTIFIED
    INTIN(1)=5    ! TOP LINE
    VDISYS
    '
    CONTRL(0)=246  ! VST_ARBPT
    CONTRL(1)=0
    CONTRL(3)=1
    CONTRL(6)=gdos_handle%
    INTIN(0)=8
    VDISYS
    '
  ENDIF
RETURN
> PROCEDURE eject_page
  CONTRL(0)=4  ! V_UPDWK
  CONTRL(1)=0
  CONTRL(3)=0
  CONTRL(6)=gdos_handle%
  VDISYS
  '
  CONTRL(0)=3  ! V_CLRWK
  CONTRL(1)=0
  CONTRL(3)=0
  CONTRL(6)=gdos_handle%
  VDISYS
  '
RETURN
> PROCEDURE on_line
  on_line%=0
  DO
    IF OUT?(0) THEN
      on_line%=1
    ELSE
      ALERT 0,"Sorry, but your printer is  |off-line. Please check the  |printer and I will try again.  | ",1,"OK|Cancel",button%
      IF button%=2 THEN
        on_line%=-1
      ENDIF
    ENDIF
  LOOP UNTIL on_line%<>0
RETURN
