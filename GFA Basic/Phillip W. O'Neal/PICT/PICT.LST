$m200000
' PICT READER ½1994 BY PHILLIP O'NEAL
RESERVE 200000
@main
'
> PROCEDURE main
  @init_variables
  '
  DO
    IF MOUSEK=1 THEN
      @get_file
      CLS
      IF buffer%>0 THEN
        @show_file
      ENDIF
    ENDIF
    IF MOUSEK=2 THEN
      @quit
    ENDIF
  LOOP
  '
RETURN
'
> PROCEDURE init_variables
  old_dta%=FGETDTA()
  dta%=GEMDOS(72,L:1024)
  ~FSETDTA(dta%)
  '
  current_drive%=GEMDOS(25)
  view_path$=DIR$(current_drive%+1)+"\"
  DEFMOUSE 0
  '
RETURN
'
> PROCEDURE get_file
  '
  FILESELECT #"Select file to view",view_path$+"*.*","",view_name$
  OPEN "I",#1,view_name$
  size%=LOF(#1)
  CLOSE #1
  IF buffer%>0 THEN
    d%=GEMDOS(73,L:buffer%)  ! RELEASE MEMORY
  ENDIF
  buffer%=GEMDOS(72,L:size%)
  IF buffer%>0 THEN
    BLOAD view_name$,buffer%
  ENDIF
  '
RETURN
'
> PROCEDURE show_file
  PRINT "File name:";view_name$
  PRINT "File size:";size%
  version%=DPEEK(buffer%+522)
  PRINT "Version:";HEX$(version%)
  IF version%=&H11 THEN
    offset%=buffer%+512+14
    DO
      opcode%=DPEEK(offset%)
      @show_opcode
    LOOP UNTIL opcode%=&HFF
  ENDIF
  '
RETURN
'
PROCEDURE show_opcode
  PRINT HEX$(opcode%);">";
  SELECT opcode%
  CASE &H0,&H17 TO &H19,&H1C,&H1E,&H38 TO &H3F,&H48 TO &H4C,&H58 TO &H5F,&H78 TO &H7F,&H88 TO &H8B,&H8D TO &H8F,&HB0 TO &HCF,&HFF
    offset%=offset%+2
    PRINT
    '
  CASE &H4,&H11,&H8C
    offset%=offset%+3
    PRINT
    '
  CASE &H3,&H5,&H8,&HD,&H15,&H16,&H23,&HA0
    offset%=offset%+4
    PRINT
    '
  CASE &H1A,&H1B,&H1D,&H1F,&H22
    offset%=offset%+8
    PRINT
    '
  CASE &H2,&H9,&HA,&H10,&H20,&H30 TO &H37,&H40 TO &H47,&H50 TO &H5F
    offset%=offset%+10
    PRINT
    '
  CASE &H60 TO &H67
    offset%=offset%+14
    PRINT
    '
  CASE &H29,&H2A
    length%=PEEK(offset%+3)
    offset%=offset%+4+length%
    txt$=""
    FOR x%=0 TO length%-1
      txt$=txt$+CHR$(PEEK(offset%+4+x%))
    NEXT x%
    PRINT length%;" TEXT:";txt$
    '
  CASE &H2B
    length%=PEEK(offset%+4)
    offset%=offset%+5+length%
    txt$=""
    FOR x%=0 TO length%-1
      txt$=txt$+CHR$(PEEK(offset%+5+x%))
    NEXT x%
    PRINT length%;" TEXT:";txt$
    '
  CASE &HA1
    length%=DPEEK(offset%+4)
    offset%=offset%+6+length%
    PRINT length%
    '
  CASE &H28
    length%=PEEK(offset%+6)
    offset%=offset%+7+length%
    txt$=""
    FOR x%=0 TO length%-1
      txt$=txt$+CHR$(PEEK(offset%+7+x%))
    NEXT x%
    PRINT length%;" TEXT:";txt$
    '
  CASE &H24 TO &H27,&H2C TO &H2F,&H92 TO &H97,&H9A TO &H9F,&HA2 TO &HAF
    length%=DPEEK(offset%+2)
    offset%=offset%+4+length%
    PRINT length%
    '
  CASE &HA2 TO &HAF
    length%=LPEEK(offset%+2)
    offset%=offset%+6+length%
    PRINT length%
    '
  CASE &H1,&H70 TO &H74,&H80 TO &H84
    length%=DPEEK(offset%+2)
    offset%=offset%+2+length%
    PRINT length%
    '
  CASE &H90,&H91,&H98,&H99
    width%=DPEEK(offset%+8)
    height%=DPEEK(offset%+10)
    bits%=DPEEK(offset%+30)
    pens%=DPEEK(offset%+50)
    pic_size%=width%*height%
    '
    offset%=offset%+52+pens%
    '
    x1%=DPEEK(offset%)
    x2%=DPEEK(offset%+2)
    y1%=DPEEK(offset%+4)
    y2%=DPEEK(offset%+6)
    '
    offset%=offset%+18
    '
    IF opcode%=&H91 OR opcode%=&H99 THEN
      length%=DPEEK(offset%)
      offset%=offset%+2+length%
    ENDIF
    '
    offset%=offset%+pic_size%
    PRINT "WIDTH:";width%;" HEIGHT:";height%;" BITS:";bits%;" PENS:";pens%;" X1:";x1%;" Y1:";y1%;" X2:";x2%;" Y2:";y2%
    '
    opcode%=&HFF
  CASE &HC00 ! HEADDER
    offset%=offset%+26
    PRINT
    '
  DEFAULT
    PRINT "UNKNOWN:";HEX$(opcode%)
    opcode%=&HFF
    '
  ENDSELECT
  '
RETURN
'
> PROCEDURE quit
  ~FSETDTA(old_dta%)
  IF dta%>0 THEN
    d%=GEMDOS(73,L:dta%)  ! RELEASE MEMORY
  ENDIF
  IF buffer%>0 THEN
    d%=GEMDOS(73,L:buffer%)  ! RELEASE MEMORY
  ENDIF
  END
  '
RETURN
