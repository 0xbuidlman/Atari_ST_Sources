PROCEDURE calc_print_pages
  gcolumn_width=gdos_x_rez/columns%
  print_page%=0
  print_lines%=0
  place%=0
  '
  IF buffer_lines%>0 THEN
    temp_line$=""
    ~VQT_EXTENT(" ",d%,d%,d%,d%,gwidth%,gheight%,d%,d%)
    DO     ! PAGE
      print_page%(print_page%)=print_lines%
      current_column%=0
      DO   ! COLUMN
        y%=0
        DO ! LINE
          IF temp_line$="" THEN
            DO
              temp_line$=txt$(place%)
              IF ltrim%=1 AND rtrim%=1 THEN
                temp_line$=TRIM$(temp_line$)
              ELSE
                IF ltrim%=1 THEN
                  trim_exit%=0
                  DO
                    IF MID$(temp_line$,1,1)=" " THEN
                      IF LEN(temp_line$)>0 THEN
                        temp_line$=MID$(temp_line$,2)
                      ELSE
                        trim_exit%=1
                      ENDIF
                    ELSE
                      trim_exit%=1
                    ENDIF
                  LOOP UNTIL trim_exit%=1
                ENDIF
                IF rtrim%=1 THEN
                  trim_exit%=0
                  DO
                    IF MID$(temp_line$,LEN(temp_line$),1)=" " THEN
                      IF LEN(temp_line$)>0 THEN
                        temp_line$=MID$(temp_line$,1,LEN(temp_line$)-1)
                      ELSE
                        trim_exit%=1
                      ENDIF
                    ELSE
                      trim_exit%=1
                    ENDIF
                  LOOP UNTIL trim_exit%=1
                ENDIF
              ENDIF
              INC place%
            LOOP UNTIL place%>=buffer_lines% OR temp_line$<>""
          ENDIF
          exit%=0
          temp$=temp_line$
          DO
            ~VQT_EXTENT(temp$+" ",d%,d%,d%,d%,twidth%,d%,d%,d%)
            '
            IF twidth%>gcolumn_width THEN
              IF formated% THEN
                IF LEN(temp$)>1 THEN
                  test%=RINSTR(MID$(temp$,1,LEN(temp$)-1)," ")
                ELSE
                  test%=0
                ENDIF
              ELSE
                test%=0
              ENDIF
              IF test%>1 THEN
                temp$=MID$(temp$,1,test%)
              ELSE
                IF LEN(temp$)>1 THEN
                  temp$=MID$(temp$,1,LEN(temp$)-1)
                ELSE
                  print$(print_lines%)=temp$
                  print_x%(print_lines%)=current_column%*gcolumn_width
                  print_y%(print_lines%)=y%
                  INC print_lines%
                  IF LEN(temp$)<LEN(temp_line$)+1 THEN
                    temp_line$=MID$(temp_line$,LEN(temp$)+1)
                  ELSE
                    temp_line$=""
                  ENDIF
                  exit%=1
                ENDIF
              ENDIF
              '
            ELSE
              print$(print_lines%)=temp$
              print_x%(print_lines%)=current_column%*gcolumn_width
              print_y%(print_lines%)=y%
              INC print_lines%
              IF LEN(temp$)<LEN(temp_line$)+1 THEN
                temp_line$=MID$(temp_line$,LEN(temp$)+1)
              ELSE
                temp_line$=""
              ENDIF
              exit%=1
            ENDIF
            '
          LOOP UNTIL exit%
          '
          y%=y%+gheight%
        LOOP UNTIL y%+gheight%>=gdos_y_rez OR (place%>=buffer_lines% AND temp_line$="")
        INC current_column%
      LOOP UNTIL current_column%=columns% OR (place%>=buffer_lines% AND temp_line$="")
      INC print_page%
    LOOP UNTIL place%>=buffer_lines% AND temp_line$=""
  ENDIF
  print_page%(print_page%)=print_lines%
  print$(print_lines%)=""
  print_x%(print_lines%)=0
  print_y%(print_lines%)=0
RETURN
