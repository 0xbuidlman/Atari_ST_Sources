' HP SET
$m60000
'
@init
@main
'
> PROCEDURE init
  '
  ' COMMAND LINE, COMMAND$ IS PATH+FILENAME OF PROCESS THAT
  ' STARTED PROGRAM. TAIL$ IS FILENAME THAT WAS DROPED ON ME
  shell%=SHEL_READ(command$,tail$)
  '
  ' GET CURRENT DIRECTORY
  current_drive%=GEMDOS(25)
  from_path$=DIR$(current_drive%+1)+"\"
  '
  ' ADD DRIVE LETTER IF NOT PRESENT
  IF MID$(from_path$,1,1)="\" THEN
    from_path$=CHR$(current_drive%+65)+":"+from_path$
    '
  ELSE
    IF MID$(from_path$,2,1)<>":" THEN
      from_path$=CHR$(current_drive%+65)+":\"+from_path$
      '
    ENDIF
    '
  ENDIF
  '
  ' STRIP FILENAME FROM PATH
  test%=RINSTR(command$,"\")
  IF test% THEN
    my_path$=MID$(command$,1,test%)
  ELSE
    my_path$=from_path$
  ENDIF
  '
  @check_for_mint
  '
  ' BUFFER FOR WINDOW TITLE
  INLINE window_title%,16
  '
  OPTION BASE 0
  DIM pitch(6),points(7),pro%(3,6)
  '
  RESTORE font_data
  '
  pitch(0)=5
  pitch(1)=6
  pitch(2)=10
  pitch(3)=12
  pitch(4)=16.67
  pitch(5)=20
  pitch(6)=24
  '
  points(0)=5
  points(1)=6
  points(2)=7
  points(3)=8
  points(4)=10
  points(5)=12
  points(6)=14
  points(7)=24
  '
  pro%(1,0)=150
  pro%(1,1)=133
  pro%(1,2)=109
  pro%(1,3)=96
  pro%(1,4)=77
  pro%(1,5)=64
  pro%(1,6)=55
  '
  pro%(3,0)=160
  pro%(3,1)=133
  pro%(3,2)=114
  pro%(3,3)=100
  pro%(3,4)=80
  pro%(3,5)=66
  pro%(3,6)=57
  '
  d$="abcdefg hifklmno"
  acc$="  HP 550c Setup  "
  '
  @rsc_setup
  '
  font%=0
  page_orientation%=0
  pitch%=2
  points%=5
  '
  main_win%=0
  OB_STATE(main%,t_portrait%)=BSET(OB_STATE(main%,t_portrait%),0)
  @set_form
  '
  ' CALC SIZE OF WINDOW FROM RSC FORM SIZE
  ~WIND_GET(0,4,mwx%,mwy%,mww%,mwh%)
  ~WIND_CALC(0,&X1011,main_x%,main_y%,main_w%,main_h%,main_wx%,main_wy%,main_ww%,main_wh%)
  '
  ' MESSAGE BUFFER FOR EVENT CALL
  DIM buffer&(15)
  buffer%=VARPTR(buffer&(0))
  '
  ' MESSAGE BUFFER FOR ME TO SEND EVENT MESSAGES
  DIM my_buffer&(15)
  my_buffer%=VARPTR(my_buffer&(0))
RETURN
'
> PROCEDURE main
  '
  ' PUT PRG NAME IN MENU
  ap_id&=APPL_INIT()
  acc_id&=MENU_REGISTER(ap_id&,acc$)
  '
  DO
    ~EVNT_MESAG(buffer%)
    IF buffer&(0)=40 THEN ! 40=OPEN ACC
      done%=0
      DEFMOUSE 0
      @window_setup
      '
      DO
        c_type%=EVNT_MULTI(&X110010,1,1,1,0,0,0,0,0,0,0,0,0,0,buffer%,10,mx%,my%,b%,key_state%,key%,d%)
        @check_event
        '
      LOOP UNTIL done%
      '
      flag%=WIND_CLOSE(main_win%)
      flag%=WIND_DELETE(main_win%)
    ENDIF
    '
  LOOP
  '
RETURN
'
> PROCEDURE check_for_mint
  '
  ' CHECK FOR MINT COOKIE
  '
  INLINE cookie%,6
  CHAR{cookie%}="    "+CHR$(0)+CHR$(0)
  '
  ' GET ADDRESS OF COOKIE JAR
  jar%=LPEEK(1440)
  '
  mint%=0
  '
  IF jar% THEN
    DO
      IF LPEEK(jar%) THEN
        LPOKE cookie%,LPEEK(jar%)
        cookie$=CHAR{cookie%}
        jar%=jar%+8
        IF cookie$="MiNT" THEN
          mint%=1
          jar%=0
        ENDIF
        '
      ELSE
        jar%=0
        '
      ENDIF
      '
    LOOP UNTIL jar%=0
    '
  ENDIF
  '
RETURN
'
> PROCEDURE rsc_setup
  '
  rsc%=RSRC_LOAD(my_path$+"HP_SET.RSC")
  '
  IF rsc%=0 THEN
    DO
      ~EVNT_MESAG(buffer%)
    LOOP
  ELSE
    @rsc_do
  ENDIF
  '
RETURN
'
> PROCEDURE rsc_do
  '
  ' GET ADDRESS TREE
  ~RSRC_GADDR(0,0,main%)
  '
  ' SET VARS TO OBJ NUMBERS
  '
  t_courier%=1
  t_times%=2
  t_gothic%=3
  t_univers%=4
  '
  t_pitch_5%=6
  t_pitch_24%=12
  '
  t_points_5%=14
  t_points_24%=21
  '
  t_portrait%=22
  t_landscape%=23
  '
  t_page_size%=25
  '
  b_draft%=26
  b_bold%=27
  b_italic%=28
  b_line_wrap%=29
  b_eject%=30
  b_reset%=31
  b_ok%=32
  b_functions%=33
  b_cancel%=34
  '
  ' GET SIZE OF FORM
  ~FORM_CENTER(main%,main_x%,main_y%,main_w%,main_h%)
  '
RETURN
'
> PROCEDURE window_setup
  '
  ' CREATE WINDOW WITH TITLE, MOVE, AND CLOSE BOX
  main_win%=WIND_CREATE(&X1011,main_wx%,main_wy%,main_ww%,main_wh%)
  '
  ' SET TITLE
  CHAR{window_title%}=acc$
  flag%=WIND_SET(main_win%,2,CARD(SWAP(window_title%)),CARD(window_title%),0,0)
  '
  IF main_wy%<mwy% THEN
    main_wy%=mwy%
  ENDIF
  flag%=WIND_OPEN(main_win%,main_wx%,main_wy%,main_ww%,main_wh%)
  flag%=WIND_GET(main_win%,4,main_iwx%,main_iwy%,main_iww%,main_iwh%)
  OB_X(main%,0)=main_iwx%
  OB_Y(main%,0)=main_iwy%
  '
RETURN
'
> PROCEDURE check_event
  '
  IF c_type% AND &X10 THEN
    '
    ' MOUSE EVENT
    @button
    '
  ENDIF
  '
  ' MENU/WINDOW EVENT
  IF c_type% AND &X10000 THEN
    '
    ' TYPE OF EVENT
    command%=buffer&(0)
    '
    ' WINDOW EVENT
    @a_window
    '
  ENDIF
  '
RETURN
'
> PROCEDURE button
  '
  window_over%=WIND_FIND(mx%,my%)
  IF main_win%=window_over% THEN
    '
    main_found%=OBJC_FIND(main%,0,2,mx%,my%)
    ' DID MOUSE CLICK ON A DESKTOP BUTTON ?
    '
    SELECT main_found%
      '
    CASE t_courier%
      IF BTST(OB_STATE(main%,main_found%),0)=0
        font%=0
        @set_form
      ENDIF
      '
    CASE t_times%
      IF BTST(OB_STATE(main%,main_found%),0)=0
        font%=1
        @set_form
      ENDIF
      '
    CASE t_gothic%
      IF BTST(OB_STATE(main%,main_found%),0)=0
        font%=2
        @set_form
      ENDIF
      '
    CASE t_univers%
      IF BTST(OB_STATE(main%,main_found%),0)=0
        font%=3
        @set_form
      ENDIF
      '
    CASE t_pitch_5% TO t_pitch_24%
      IF BTST(OB_STATE(main%,main_found%),3)=0
        IF BTST(OB_STATE(main%,main_found%),0)=0
          pitch%=main_found%-t_pitch_5%
          @set_form
        ENDIF
      ENDIF
      '
    CASE t_points_5% TO t_points_24%
      IF BTST(OB_STATE(main%,main_found%),3)=0
        IF BTST(OB_STATE(main%,main_found%),0)=0
          points%=main_found%-t_points_5%
          @set_form
        ENDIF
      ENDIF
      '
    CASE t_portrait%
      IF BTST(OB_STATE(main%,main_found%),3)=0
        IF BTST(OB_STATE(main%,main_found%),0)=0
          page_orientation%=0
          OB_STATE(main%,t_portrait%)=BSET(OB_STATE(main%,t_portrait%),0)
          OB_STATE(main%,t_landscape%)=BCLR(OB_STATE(main%,t_landscape%),0)
          @set_form
        ENDIF
      ENDIF
      '
    CASE t_landscape%
      IF BTST(OB_STATE(main%,main_found%),3)=0
        IF BTST(OB_STATE(main%,main_found%),0)=0
          page_orientation%=1
          OB_STATE(main%,t_landscape%)=BSET(OB_STATE(main%,t_landscape%),0)
          OB_STATE(main%,t_portrait%)=BCLR(OB_STATE(main%,t_portrait%),0)
          @set_form
        ENDIF
      ENDIF
      '
    CASE b_draft%
      @main_select
      '
    CASE b_bold%
      @main_select
      '
    CASE b_italic%
      IF BTST(OB_STATE(main%,b_italic%),3)=0
        @main_select
        @set_form
      ENDIF
      '
    CASE b_line_wrap%
      @main_select
      '
    CASE b_eject%
      @main_select
      @on_line
      IF on_line% THEN
        OUT 0,27,38,108,48,72
        '
      ENDIF
      @main_select
      '
    CASE b_reset%
      @main_select
      font%=0
      page_orientation%=0
      pitch%=2
      points%=5
      OB_STATE(main%,t_landscape%)=BCLR(OB_STATE(main%,t_landscape%),0)
      OB_STATE(main%,t_landscape%)=BCLR(OB_STATE(main%,t_landscape%),3)
      OB_STATE(main%,b_italic%)=BCLR(OB_STATE(main%,b_italic%),0)
      OB_STATE(main%,b_draft%)=BCLR(OB_STATE(main%,b_draft%),0)
      OB_STATE(main%,b_bold%)=BCLR(OB_STATE(main%,b_bold%),0)
      OB_STATE(main%,b_line_wrap%)=BCLR(OB_STATE(main%,b_line_wrap%),0)
      '
      @set_form
      main_found%=b_reset%
      @main_select
      '
    CASE b_ok%
      @main_select
      @on_line
      IF on_line% THEN
        @print_set
        done%=1
        '
      ENDIF
      @main_select
      '
    CASE b_functions%
      @main_select
      @on_line
      IF on_line% THEN
        OUT 0,27,89
        '
      ENDIF
      '
      @main_select
      '
    CASE b_cancel%
      @main_select
      '
      @main_select
      done%=1
      '
    ENDSELECT
    '
  ENDIF
  '
RETURN
'
> PROCEDURE a_window
  '
  ' HANDLE OF WINDOW THAT TRIGGERED EVENT
  win%=buffer&(3)
  '
  ' BE SURE IT WAS OUR WINDOW
  IF win%=main_win% THEN
    SELECT command%
      '
      ' WIND UPDATE
    CASE 20
      main_obj%=0
      @repair
      '
      ' WINDOW CLICKED ON
    CASE 21
      ' WINDOW SET TO TOP
      flag%=WIND_SET(main_win%,10,0,0,0,0)
      DEFMOUSE 0
      '
      ' CLOSE WINDOW
    CASE 22
      flag%=WIND_CLOSE(main_win%)
      flag%=WIND_DELETE(main_win%)
      done%=1
      '
      ' WINDOW MOVED
    CASE 28
      main_wx%=buffer&(4)
      main_wy%=buffer&(5)
      flag%=WIND_SET(main_win%,5,main_wx%,main_wy%,main_ww%,main_wh%)
      flag%=WIND_GET(main_win%,4,main_iwx%,main_iwy%,main_iww%,main_iwh%)
      '
      ' SET FORM POS TO WINDOW POS
      OB_X(main%,0)=main_iwx%
      OB_Y(main%,0)=main_iwy%
      '
    ENDSELECT
    '
  ENDIF
  '
RETURN
'
> PROCEDURE main_select
  '
  ' MAKE BUTTON SELECTED / DRAW IT
  OB_STATE(main%,main_found%)=BCHG(OB_STATE(main%,main_found%),0)
  main_obj%=main_found%
  @repair
  '
  DO
  LOOP UNTIL MOUSEK=0
  '
RETURN
'
> PROCEDURE repair
  '
  ' TELL GEM WE ARE TAKING OVER SCREEN AND MOUSE
  er%=WIND_UPDATE(1)
  er%=WIND_UPDATE(3)
  '
  ' GET FIRST RECTANGLE FROM LIST
  er%=WIND_GET(main_win%,11,fx%,fy%,fx2%,fy2%)
  '
  ' GET DIRTY AREA
  ' fix_wx%=buffer&(4)
  ' fix_wy%=buffer&(5)
  ' fix_ww%=buffer&(6)
  ' fix_wh%=buffer&(7)
  '
  ' CALC AREA THAT OVERLAPS DIRTY AREA
  ' fix_flag%=RC_INTERSECT(fix_wx%,fix_wy%,fix_ww%,fix_wh%,fx%,fy%,fx2%,fy2%)
  '
  ' IF fix_flag% THEN
  ~OBJC_DRAW(main%,main_obj%,2,fx%,fy%,fx2%,fy2%)
  '
  ' ENDIF
  '
  fix_done%=0
  '
  ' SAME AS ABOVE FOR THE OTHER RECTANGLES FROM LIST
  DO
    er%=WIND_GET(main_win%,12,fx%,fy%,fx2%,fy2%)
    '
    IF fx2%+fy2%=0 THEN
      fix_done%=1
    ELSE
      ' fix_wx%=buffer&(4)
      ' fix_wy%=buffer&(5)
      ' fix_ww%=buffer&(6)
      ' fix_wh%=buffer&(7)
      ' fix_flag%=RC_INTERSECT(fix_wx%,fix_wy%,fix_ww%,fix_wh%,fx%,fy%,fx2%,fy2%)
      ' IF fix_flag% THEN
      ~OBJC_DRAW(main%,main_obj%,2,fx%,fy%,fx2%,fy2%)
      ' ENDIF
    ENDIF
    '
  LOOP UNTIL fix_done%=1
  '
  er%=WIND_UPDATE(2)
  er%=WIND_UPDATE(0)                      !  DONE!
  '
RETURN
'
> PROCEDURE update_display
  '
  ~OBJC_OFFSET(main%,main_found%,x%,y%)
  w%=OB_W(main%,main_found%)
  h%=OB_H(main%,main_found%)
  my_buffer&(0)=20
  my_buffer&(3)=main_win%
  my_buffer&(4)=x%
  my_buffer&(5)=y%
  my_buffer&(6)=w%
  my_buffer&(7)=h%
  ~APPL_WRITE(ap_id&,16,my_buffer%)
  '
RETURN
'
> PROCEDURE set_form
  FOR set%=t_courier% TO t_univers%
    OB_STATE(main%,set%)=BCLR(OB_STATE(main%,set%),0)
  NEXT set%
  FOR set%=t_pitch_5% TO t_pitch_24%
    OB_STATE(main%,set%)=BCLR(OB_STATE(main%,set%),0)
    OB_STATE(main%,set%)=BSET(OB_STATE(main%,set%),3)
  NEXT set%
  FOR set%=t_points_5% TO t_points_24%
    OB_STATE(main%,set%)=BCLR(OB_STATE(main%,set%),0)
    OB_STATE(main%,set%)=BSET(OB_STATE(main%,set%),3)
    '
  NEXT set%
  '
  OB_STATE(main%,t_points_5%+1)=BCLR(OB_STATE(main%,t_points_5%+1),3)
  OB_STATE(main%,t_points_5%+5)=BCLR(OB_STATE(main%,t_points_5%+5),3)
  '
  SELECT font%
  CASE 0 ! COURIER
    @courier
    '
  CASE 1 ! TIMES
    @times
    '
  CASE 2 ! GOTHIC
    @gothic
    '
  CASE 3 ! UNIVERS
    @univers
    '
  ENDSELECT
  '
  OB_STATE(main%,t_courier%+font%)=BSET(OB_STATE(main%,t_courier%+font%),0)
  IF pitch%<>-1 THEN
    OB_STATE(main%,t_pitch_5%+pitch%)=BSET(OB_STATE(main%,t_pitch_5%+pitch%),0)
  ENDIF
  OB_STATE(main%,t_points_5%+points%)=BSET(OB_STATE(main%,t_points_5%+points%),0)
  '
  IF pitch%<>-1 THEN
    IF page_orientation%=0 THEN
      page_width%=pitch(pitch%)*8
      page_height%=(72/points(points%))*10.5-1
    ELSE
      page_width%=pitch(pitch%)*10.25
      page_height%=(72/points(points%))*8-1
    ENDIF
  ELSE
    page_width%=pro%(font%,points%)
    page_height%=(INT(72/points(points%))*10.5)-1
  ENDIF
  temp$=STR$(page_width%)+"x"+STR$(page_height%)
  CHAR{{OB_SPEC(main%,t_page_size%)}}=temp$
  '
  IF main_win%>0 THEN
    main_found%=0
    @update_display
  ENDIF
  '
RETURN
'
> PROCEDURE courier
  font%=0
  OB_STATE(main%,t_portrait%)=BCLR(OB_STATE(main%,t_portrait%),3)
  OB_STATE(main%,t_landscape%)=BCLR(OB_STATE(main%,t_landscape%),3)
  OB_STATE(main%,b_italic%)=BCLR(OB_STATE(main%,b_italic%),3)
  '
  OB_STATE(main%,t_pitch_5%+2)=BCLR(OB_STATE(main%,t_pitch_5%+2),3)
  OB_STATE(main%,t_pitch_5%+5)=BCLR(OB_STATE(main%,t_pitch_5%+5),3)
  IF page_orientation%=0 THEN
    OB_STATE(main%,t_portrait%)=BSET(OB_STATE(main%,t_portrait%),0)
    '
    OB_STATE(main%,t_pitch_5%+0)=BCLR(OB_STATE(main%,t_pitch_5%+0),3)
    '
    IF BTST(OB_STATE(main%,b_italic%),0) THEN
      '
      IF pitch%<>0 AND pitch%<>2 AND pitch%<>5 THEN
        pitch%=2
      ENDIF
      '
    ELSE
      OB_STATE(main%,t_pitch_5%+4)=BCLR(OB_STATE(main%,t_pitch_5%+4),3)
      IF pitch%<>0 AND pitch%<>2 AND pitch%<>4 AND pitch%<>5 THEN
        pitch%=2
      ENDIF
      '
    ENDIF
    IF points%<>1 AND points%<>5 THEN
      points%=5
    ENDIF
    '
  ELSE
    OB_STATE(main%,t_landscape%)=BSET(OB_STATE(main%,t_landscape%),0)
    '
    OB_STATE(main%,b_italic%)=BCLR(OB_STATE(main%,b_italic%),0)
    OB_STATE(main%,b_italic%)=BSET(OB_STATE(main%,b_italic%),3)
    '
    OB_STATE(main%,t_pitch_5%+4)=BCLR(OB_STATE(main%,t_pitch_5%+4),3)
    '
    OB_STATE(main%,t_points_5%+7)=BCLR(OB_STATE(main%,t_points_5%+7),3)
    '
    IF pitch%<>2 AND pitch%<>4 AND pitch%<>5 THEN
      pitch%=2
    ENDIF
    IF points%<>1 AND points%<>5 AND points%<>7 THEN
      points%=5
    ENDIF
    '
  ENDIF
  '
RETURN
'
> PROCEDURE times
  font%=1
  pitch%=-1
  OB_STATE(main%,t_portrait%)=BCLR(OB_STATE(main%,t_portrait%),3)
  OB_STATE(main%,t_portrait%)=BSET(OB_STATE(main%,t_portrait%),0)
  OB_STATE(main%,t_landscape%)=BSET(OB_STATE(main%,t_landscape%),3)
  OB_STATE(main%,t_landscape%)=BCLR(OB_STATE(main%,t_landscape%),0)
  OB_STATE(main%,b_italic%)=BCLR(OB_STATE(main%,b_italic%),3)
  page_orientation%=0
  '
  FOR set%=0 TO 6
    OB_STATE(main%,t_points_5%+set%)=BCLR(OB_STATE(main%,t_points_5%+set%),3)
  NEXT set%
  '
  IF points%=7 THEN
    points%=5
  ENDIF
  '
RETURN
'
> PROCEDURE gothic
  font%=2
  OB_STATE(main%,t_portrait%)=BCLR(OB_STATE(main%,t_portrait%),3)
  OB_STATE(main%,t_landscape%)=BCLR(OB_STATE(main%,t_landscape%),3)
  OB_STATE(main%,b_italic%)=BCLR(OB_STATE(main%,b_italic%),3)
  '
  OB_STATE(main%,t_pitch_5%+3)=BCLR(OB_STATE(main%,t_pitch_5%+3),3)
  OB_STATE(main%,t_pitch_5%+6)=BCLR(OB_STATE(main%,t_pitch_5%+6),3)
  IF page_orientation%=0 THEN
    OB_STATE(main%,t_portrait%)=BSET(OB_STATE(main%,t_portrait%),0)
    '
    OB_STATE(main%,t_pitch_5%+1)=BCLR(OB_STATE(main%,t_pitch_5%+1),3)
    '
    IF pitch%<>1 AND pitch%<>3 AND pitch%<>6 THEN
      pitch%=3
    ENDIF
    IF points%<>1 AND points%<>5 THEN
      points%=5
    ENDIF
    '
  ELSE
    OB_STATE(main%,t_landscape%)=BSET(OB_STATE(main%,t_landscape%),0)
    '
    OB_STATE(main%,b_italic%)=BCLR(OB_STATE(main%,b_italic%),0)
    OB_STATE(main%,b_italic%)=BSET(OB_STATE(main%,b_italic%),3)
    '
    OB_STATE(main%,t_points_5%+7)=BCLR(OB_STATE(main%,t_points_5%+7),3)
    '
    IF pitch%<>3 AND pitch%<>6 THEN
      pitch%=3
    ENDIF
    IF points%<>1 AND points%<>5 AND points%<>7 THEN
      points%=5
    ENDIF
    '
  ENDIF
  '
RETURN
'
> PROCEDURE univers
  font%=3
  pitch%=-1
  OB_STATE(main%,t_portrait%)=BCLR(OB_STATE(main%,t_portrait%),3)
  OB_STATE(main%,t_portrait%)=BSET(OB_STATE(main%,t_portrait%),0)
  OB_STATE(main%,t_landscape%)=BSET(OB_STATE(main%,t_landscape%),3)
  OB_STATE(main%,t_landscape%)=BCLR(OB_STATE(main%,t_landscape%),0)
  OB_STATE(main%,b_italic%)=BCLR(OB_STATE(main%,b_italic%),3)
  page_orientation%=0
  '
  OB_STATE(main%,t_points_5%+0)=BCLR(OB_STATE(main%,t_points_5%+0),3)
  OB_STATE(main%,t_points_5%+4)=BCLR(OB_STATE(main%,t_points_5%+4),3)
  IF BTST(OB_STATE(main%,b_italic%),0) THEN
    '
    IF points%<>0 AND points%<>1 AND points%<>4 AND points%<>5 THEN
      points%=5
    ENDIF
    '
  ELSE
    OB_STATE(main%,t_points_5%+2)=BCLR(OB_STATE(main%,t_points_5%+2),3)
    OB_STATE(main%,t_points_5%+3)=BCLR(OB_STATE(main%,t_points_5%+3),3)
    OB_STATE(main%,t_points_5%+6)=BCLR(OB_STATE(main%,t_points_5%+6),3)
    '
    IF points%=7 THEN
      points%=5
    ENDIF
    '
  ENDIF
  '
RETURN
'
> PROCEDURE print_set
  ' RESET
  OUT 0,27,69
  '
  ' LINE WRAP
  IF BTST(OB_STATE(main%,b_line_wrap%),0) THEN
    OUT 0,27,38,115,48,67
    '
  ELSE
    OUT 0,27,38,115,49,67
    '
  ENDIF
  '
  ' LETTER SIZED PAGE
  OUT 0,27,38,108,50,65
  '
  ' PORTRAIT / LANDSCAPE
  IF page_orientation%=0 THEN
    OUT 0,27,38,108,48,79
  ELSE
    OUT 0,27,38,108,49,79
  ENDIF
  '
  ' LINE SPACING
  spacing$=STR$(72/points(points%))
  OUT 0,27,38,108
  FOR char%=1 TO LEN(spacing$)
    OUT 0,ASC(MID$(spacing$,char%,1))
  NEXT char%
  OUT 0,68
  '
  ' PAGE LENGTH
  IF page_orientation%=0 THEN
    lines$=STR$(INT((72/points(points%))*11))
  ELSE
    lines$=STR$(INT((72/points(points%))*8))
  ENDIF
  OUT 0,27,38,108
  FOR char%=1 TO LEN(lines$)
    OUT 0,ASC(MID$(lines$,char%,1))
  NEXT char%
  OUT 0,80
  '
  IF page_orientation%=0 THEN
    ' SKIP MODE = ON
    OUT 0,27,38,108,49,76
    '
  ELSE
    ' SKIP MODE = OFF
    OUT 0,27,38,108,48,76
    '
  ENDIF
  '
  ' TOP MARGIN = 1
  OUT 0,27,38,108,49,69
  '
  ' FIXED / PROPORTIONAL
  IF pitch%<>-1 THEN
    OUT 0,27,40,115,48,80
    pitch$=STR$(pitch(pitch%))
    OUT 0,27,40,115
    FOR char%=1 TO LEN(pitch$)
      OUT 0,ASC(MID$(pitch$,char%,1))
    NEXT char%
    OUT 0,72
    '
  ELSE
    OUT 0,27,40,115,49,80
    '
  ENDIF
  '
  ' POINTS
  points$=STR$(points(points%))
  OUT 0,27,40,115
  FOR char%=1 TO LEN(points$)
    OUT 0,ASC(MID$(points$,char%,1))
  NEXT char%
  OUT 0,86
  '
  ' ITALIC
  IF BTST(OB_STATE(main%,b_italic%),0) THEN
    OUT 0,27,40,115,49,83
    '
  ELSE
    OUT 0,27,40,115,48,83
    '
  ENDIF
  '
  ' BOLD
  IF BTST(OB_STATE(main%,b_bold%),0) THEN
    OUT 0,27,40,115,51,66
    '
  ELSE
    OUT 0,27,40,115,48,66
    '
  ENDIF
  '
  SELECT font%
  CASE 0 ! COURIER
    OUT 0,27,40,115,51,84
    '
  CASE 1 ! TIMES
    OUT 0,27,40,115,52,49,48,49,84
    '
  CASE 2 ! GOTHIC
    OUT 0,27,40,115,54,84
    '
  CASE 3 ! UNIVERS
    OUT 0,27,40,115,53,50,84
    '
  ENDSELECT
  '
  ' DRAFT
  IF BTST(OB_STATE(main%,b_draft%),0) THEN
    OUT 0,27,40,115,49,81
    '
  ELSE
    OUT 0,27,40,115,50,81
    '
  ENDIF
  '
RETURN
'
> PROCEDURE on_line
  IF OUT?(0) THEN
    on_line%=1
    '
  ELSE
    txt$=" |Sorry, I can not complete  |this task, because the  |printer is OFF-LINE."
    ALERT 0,txt$,1," OK ",d%
    on_line%=0
    '
  ENDIF
  '
RETURN
