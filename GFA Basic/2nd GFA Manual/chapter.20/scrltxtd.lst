' *** SCRLTXTD.LST ***                               2nd GFA-Manual, page 20-30
> PROCEDURE scroll_text_down(begin,end)
  '
  ' *** Scroll text-lines begin&-end& one line down on logical TOS-screen
  ' *** The (remaining) first line at begin& is erased
  '
  ' *** Standard:  logbase%  lin.max&  char.height&  x.max&
  '
  LOCAL sy,h,dy
  IF end<lin.max AND end>=begin
    sy=MUL(PRED(begin),char.height)
    h=MUL(SUCC(SUB(end,begin)),char.height)
    dy=ADD(sy,char.height)
    RC_COPY logbase%,0,sy,x.max,h TO logbase%,0,dy
    PRINT AT(1,begin);CHR$(27);"K";             ! clear first line
  ELSE
    OUT 2,7                                     ! illegal scroll: ping
  ENDIF
RETURN
' *********
'
