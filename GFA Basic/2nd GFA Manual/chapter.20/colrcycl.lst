' *** COLRCYCL.LST ***                               2nd GFA-Manual, page 20-24
> PROCEDURE color_cycle(reg1,reg2,ticks,flag!)
  '
  ' *** Use EVERY to colour-cycle from VDI colour-index reg1& - reg2&
  ' *** If flag!=TRUE colour-cycling starts, if flag!=FALSE it stops
  ' *** Colours change every ticks& * .005 seconds (200 ticks = 1 second)
  '
  ' *** Standard:  setcolor&()
  ' *** Global  :  col.reg1&  col.reg2&  [for Procedure Cycle_every]
  '
  IF flag!
    col.reg1=reg1
    col.reg2=reg2
    EVERY ticks GOSUB cycle_every
  ELSE
    EVERY STOP
  ENDIF
RETURN
' ***
> PROCEDURE cycle_every
  LOCAL col1%,col2%
  col1%=XBIOS(7,setcolor(col.reg2),-1)
  FOR reg=col.reg1 TO PRED(col.reg2)
    col2%=XBIOS(7,setcolor(reg),-1)
    ~XBIOS(7,setcolor(reg),col1%)
    SWAP col1%,col2%
  NEXT reg
  ~XBIOS(7,setcolor(col.reg2),col1%)
RETURN
' *********
'
