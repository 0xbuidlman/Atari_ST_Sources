' *** TEXT_BOX.LST ***                               2nd GFA-Manual, page 20-30
> PROCEDURE text_box(column,line,width,line.color,text.color,text$)
  '
  ' *** Put text$ (at column&,line&) in a box on the screen with TEXT
  ' *** Box is drawn with line.color& and width&, text with text.color&
  ' *** If width&=0, a double line is used for the box
  '
  ' *** Standard:  char.width&  char.height&  high.res!
  '
  LOCAL x,y,x1,y1,x2,y2,length,i
  x=(column-1)*char.width
  y=line*char.height-3
  length=char.width*LEN(text$)
  x1=x-char.width/2
  x2=x+length+char.width/4
  y1=y-7*char.height/8
  y2=y+char.height/4
  IF high.res!
    DEFTEXT text.color,0,0,13
  ELSE
    DEFTEXT text.color,0,0,6
  ENDIF
  DEFFILL line.color,1
  DEFLINE 1,1
  IF width=0
    FOR i=4 DOWNTO 2
      DEFFILL ABS(i-3)*line.color
      PBOX x1-i,y1-i,x2+i,y2+i
    NEXT i
  ELSE
    PBOX x1-width,y1-width,x2+width,y2+width
  ENDIF
  DEFFILL 0
  PBOX x1,y1,x2,y2
  TEXT x,y,length,text$
RETURN
' *********
'
