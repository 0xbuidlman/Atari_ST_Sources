' *** TEXT_CEN.LST ***                               2nd GFA-Manual, page 20-30
> PROCEDURE text_box_center(width,line.color,text.color,text$)
  '
  ' *** Put text$ in a centered box on the screen with TEXT
  ' *** Box is drawn with line.color& and width&, text with text.color&
  ' *** If width&=0, a double line is used for the box
  '
  ' *** Standard:  char.width&  char.height&  high.res!  x.max&  y.max&
  '
  LOCAL x,y,x1,y1,x2,y2,length,i
  length=char.width*LEN(text$)
  x=x.max/2-length/2+char.width/2
  y=y.max/2+char.height/2
  x1=x-char.width/2
  x2=x+length+char.width/4
  y1=y-7*char.height/8
  y2=y+char.height/4
  IF high.res!
    DEFTEXT text.color,0,0,13
  ELSE
    DEFTEXT text.color,0,0,6
  ENDIF
  DEFFILL line.color,1
  DEFLINE 1,1
  IF width=0
    FOR i=4 DOWNTO 2
      DEFFILL ABS(i-3)*line.color
      PBOX x1-i,y1-i,x2+i,y2+i
    NEXT i
  ELSE
    PBOX x1-width,y1-width,x2+width,y2+width
  ENDIF
  DEFFILL 0
  PBOX x1,y1,x2,y2
  TEXT x,y,length,text$
RETURN
' *********
'
