' *** POINT.LST ***                                  2nd GFA-Manual, page 20-27
> PROCEDURE point(text$,VAR x,y)
  '
  ' *** User chooses point (x&,y&) on screen with mouse-click
  ' *** Instruction text$ for user is shown reverse on bottom-line of TOS-screen
  '
  ' *** Standard:  x.max&  y.max&  char.height&  line.max&  col.max&
  '
  LOCAL message.scrn$,x1,y1,x2,y2,k
  DEFMOUSE 0
  GET 0,y.max-char.height,x.max,y.max,message.scrn$ ! save screen under message
  LOCATE 1,line.max
  PRINT CHR$(27);"K";                               ! clear line
  PRINT SPACE$((col.max-LEN(text$))/2)+CHR$(27)+"p"+text$+CHR$(27)+"q";
  x1=MOUSEX
  y1=MOUSEY
  SHOWM
  REPEAT
    MOUSE x2,y2,k
  UNTIL x2<>x1 OR y2<>y1                            ! wait for mouse-movement
  PUT 0,y.max-char.height,message.scrn$,3           ! restore screen
  REPEAT
  UNTIL MOUSEK                                      ! wait for mouse-click
  x=MOUSEX
  y=MOUSEY
  HIDEM
  PAUSE 10                             ! give user time to release mouse-button
RETURN
' *********
'
