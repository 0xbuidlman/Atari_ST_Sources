' *** IMG_SHOW.LST ***                               2nd GFA-Manual, page 20-25
> PROCEDURE gem_image_load_show(img$,VAR loaded!)
  '
  ' *** Put GEM IMG-picture directly on physical screen (High resolution only)
  ' *** Variabele loaded!=TRUE if successful
  ' *** GEM_IMG.INL (958 bytes) must be in INLINE-line gem.img%
  '
  ' *** Standard :  high.res!  physbase%
  ' *** Global   :  gem.img% [INLINE-address]
  ' *** Procedure:  Program_exit
  '
  LOCAL img%,header.length,bit.planes,pattern.length,width,height,m$,k,error%
  '
  LET loaded!=FALSE
  '
  IF high.res!
    IF EXIST(img$)
      DIM img|({FGETDTA()+26}-1)
      img%=V:img|(0)
      BLOAD img$,img%
      header.length=WORD{img%+2}        ! 'normal' header consists of 8 words
      bit.planes=WORD{img%+4}           ! 1 bit-plane in High resolution
      pattern.length=WORD{img%+6}       ! should be 2 bytes (= 1 word)
      width=WORD{img%+12}               ! width < 640 possible (for PUT-string)
      height=WORD{img%+14}              ! height < 400 possible (PUT-string)
      IF header.length<>8
        m$="   *** Warning ***|File "+RIGHT$(img$,LEN(img$)-RINSTR(img$,"\"))
        m$=m$+" has a header of |"+string$(header.length)+" words (8 expected)"
        ALERT 3,m$,1,"Continue|Quit",k
        IF k=2
          @program_exit
        ENDIF
      ENDIF
      IF width=640 AND height=400 AND bit.planes=1 AND pattern.length=2
        error%=C:gem.img%(2,L:physbase%,L:img%,width,height)
        IF error%=0
          LET loaded!=TRUE              ! success
        ELSE
          m$="   *** Error ***|File "+RIGHT$(img$,LEN(img$)-RINSTR(img$,"\"))
          m$=m$+" was|not unpacked by Procedure |Gem_image_load_show"
          ALERT 3,m$,1,"Continue|Quit",k
          IF k=2
            @program_exit
          ENDIF
        ENDIF
      ELSE
        m$="   *** Error ***|File "+RIGHT$(img$,LEN(img$)-RINSTR(img$,"\"))
        m$=m$+" has wrong parameters:|width="+string$(width)+" height="
        m$=m$+string$(height)+"|bit-planes="+string$(bit.planes)
        m$=m$+" pattern-length="+string$(pattern.length)
        ALERT 3,m$,1,"Continue|Quit",k
        IF k=2
          @program_exit
        ENDIF
      ENDIF
    ELSE
      m$="   *** Error ***|File "+RIGHT$(img$,LEN(img$)-RINSTR(img$,"\"))
      m$=m$+" was|not found by Procedure |Gem_image_load_show"
      ALERT 3,m$,1,"Continue|Quit",k
      IF k=2
        @program_exit
      ENDIF
    ENDIF
    ERASE img|()
  ELSE
    m$="   *** Error ***| |Procedure Gem_image_load_show|needs High resolution"
    ALERT 3,m$,1,"Continue|Quit",k
    IF k=2
      @program_exit
    ENDIF
  ENDIF
  '
RETURN
' *********
'
