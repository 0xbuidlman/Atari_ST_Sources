' *** NEO_SHOW.LST ***                               2nd GFA-Manual, page 20-26
> PROCEDURE neochrome_load_show(neo$,VAR loaded!)
  '
  ' *** Put Neochrome-picture directly on physical screen (Low resolution only)
  ' *** Palette of Neochrome-picture is used (save current palette first!)
  ' *** Channel #90 is used for loading
  ' *** Variabele loaded!=TRUE if successful
  '
  ' *** Standard:  low.res!  physbase%
  ' *** Procedure:  Program_exit
  '
  LOCAL pal$,pal%,m$,k
  '
  LET loaded!=FALSE
  '
  IF low.res!
    IF EXIST(neo$)
      IF {FGETDTA()+26}=32128
        pal$=SPACE$(32)
        pal%=V:pal$
        OPEN "I",#90,neo$
        SEEK #90,4
        BGET #90,pal%,32                ! load palette
        ~XBIOS(6,L:pal%)                ! change current palette
        SEEK #90,128
        BGET #90,physbase%,32000        ! put picture on physical screen
        CLOSE #90
        LET loaded!=TRUE                ! success
      ELSE
        m$="   *** Error ***|File "+RIGHT$(neo$,LEN(neo$)-RINSTR(neo$,"\"))
        m$=m$+" should |have a length of exactly |32128 bytes (Neochrome)"
        ALERT 3,m$,1,"Continue|Quit",k
        IF k=2
          @program_exit
        ENDIF
      ENDIF
    ELSE
      m$="   *** Error ***|File "+RIGHT$(neo$,LEN(neo$)-RINSTR(neo$,"\"))
      m$=m$+" was|not found by Procedure |Neochrome_load_show"
      ALERT 3,m$,1,"Continue|Quit",k
      IF k=2
        @program_exit
      ENDIF
    ENDIF
  ELSE
    m$="   *** Error ***| |Neochrome-pictures |need Low resolution"
    ALERT 3,m$,1,"Continue|Quit",k
    IF k=2
      @program_exit
    ENDIF
  ENDIF
  '
RETURN
' *********
'
