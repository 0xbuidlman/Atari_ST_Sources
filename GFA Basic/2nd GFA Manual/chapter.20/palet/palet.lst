' *** PALET.LST ***                                  2nd GFA-Manual, page 20-26
> PROCEDURE palet(x,y,h,w)
  '
  ' *** Show palette in rectangle at (x&,y&)
  ' *** Each colour in the rectangle has height h& and width w& pixels
  ' *** Use in Medium or Low resolution
  '
  ' *** Standard:  low.res!  med.res!  setcolor&()
  '
  LOCAL arect.fill,fill.adr%,i,x1,x2
  COLOR 1
  arect.fill=-1                 ! fill-pattern is &X1111111111111111
  fill.adr%=V:arect.fill
  IF low.res!
    x2=x+16*w+2
    BOX x,y,x2,y+h
    FOR i=0 TO 15
      x1=ADD(SUCC(x),MUL(i,w))
      ARECT x1,SUCC(y),ADD(x1,w),PRED(ADD(y,h)),setcolor(i),0,fill.adr%,0
    NEXT i
  ELSE IF med.res!
    x2=x+4*w+2
    BOX x,y,x2,y+h
    FOR i=0 TO 3
      x1=ADD(SUCC(x),MUL(i,w))
      ARECT x1,SUCC(y),ADD(x1,w),PRED(ADD(y,h)),setcolor(i),0,fill.adr%,0
    NEXT i
  ENDIF
RETURN
' *********
'
