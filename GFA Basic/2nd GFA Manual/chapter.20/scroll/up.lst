' *** UP.LST ***                                     2nd GFA-Manual, page 20-29
> PROCEDURE scroll_up(scroll.lines)
  '
  ' *** Scroll-screen is scrolled upwards on physical screen
  ' *** Slow scroll: 2 lines (interference-lines if 1 line)
  ' *** Fast scroll: 8 (Low resolution) or 16 (Medium or High) lines
  ' *** A dummy-scroll is executed to maintain a constant scroll-speed
  '
  ' *** Standard:  high.res!  physbase%  logbase%  y.max&
  ' *** Global:  scroll.screen%
  '
  LOCAL bytes,i.bytes%,i,end%,offset%
  IF high.res!
    LET bytes=80*scroll.lines
  ELSE
    LET bytes=160*scroll.lines
  ENDIF
  end%=physbase%+32000
  offset%=32000+bytes
  FOR i=1 TO DIV(y.max,scroll.lines)
    i.bytes%=MUL(i,bytes)
    BMOVE scroll.screen%,SUB(end%,i.bytes%),i.bytes%
    BMOVE scroll.screen%,scroll.screen%,SUB(offset%,i.bytes%)
  NEXT i
  BMOVE scroll.screen%,physbase%,32000       ! make it neat
  ~XBIOS(5,L:logbase%,L:-1,-1)               ! restore logical screen
RETURN
' *********
'
