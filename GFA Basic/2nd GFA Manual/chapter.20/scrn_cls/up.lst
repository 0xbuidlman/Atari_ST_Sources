' *** UP.LST ***                                     2nd GFA-Manual, page 20-29
> PROCEDURE screen_cls_up_(scroll.lines,scroll.color)
  '
  ' *** Clear screen with upwards scrolling lines (with colour scroll.color&)
  ' *** Use 2 or 3 lines for a smooth scroll
  ' *** Use 8 (Low resolution) or 16 (Medium or High) lines for fast scrolling
  '
  ' *** Standard:  high.res!  physbase%  logbase%  x.max&  y.max&
  '
  LOCAL i,bytes,move.bytes,source%
  IF high.res!
    LET bytes=80*scroll.lines
  ELSE
    LET bytes=160*scroll.lines
  ENDIF
  move.bytes=32000-bytes
  source%=physbase%+bytes
  VSYNC
  BMOVE source%,physbase%,move.bytes               ! first scroll
  DEFFILL scroll.color,1
  PBOX 0,y.max-scroll.lines+1,x.max,y.max          ! clear lower part of screen
  FOR i=1 TO DIV(y.max,scroll.lines)               ! continue...
    VSYNC
    BMOVE source%,physbase%,move.bytes
  NEXT i
RETURN
' *********
'
