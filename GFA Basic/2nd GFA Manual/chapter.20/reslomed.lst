' *** RESLOMED.LST ***                               2nd GFA-Manual, page 20-28
> PROCEDURE res_low_medium(c0$,c1$,c2$,c3$,VAR low.pal$)
  '
  ' *** Change from Low to Medium resolution
  ' *** Temporary switch to Medium resolution is suitable for showing text
  ' *** Rgb-strings c0$ - c3$ contain Medium-palette
  ' *** Advice: use c0$="000" (black background) and c1$="770" (yellow letters)
  ' *** Current Low palette is returned in low.pal$
  ' *** Call Procedure Res_medium_low to return to Low resolution
  '
  ' *** Standard:  low.res!  med.res!
  '
  LOCAL i
  low.pal$=""
  FOR i=0 TO 15
    low.pal$=low.pal$+MKI$(XBIOS(7,i,-1))     ! save current Low palette
  NEXT i
  ~XBIOS(5,L:-1,L:-1,W:1)                     ! switch to Medium resolution
  SETCOLOR 0,VAL("&H"+c0$)                    ! VDI-index 0 (background)
  SETCOLOR 3,VAL("&H"+c1$)                    ! VDI-index 1 (foreground)
  SETCOLOR 1,VAL("&H"+c2$)                    ! VDI-index 2
  SETCOLOR 2,VAL("&H"+c3$)                    ! VDI-index 3
  low.res!=FALSE
  med.res!=TRUE
RETURN
' ***
> PROCEDURE res_medium_low(low.pal$)
  '
  ' *** Change from Medium back to Low resolution and restore Low-palette
  ' *** Use this Procedure only to switch back from Medium to Low resolution
  '
  ~XBIOS(5,L:-1,L:-1,W:0)                     ! switch to Low resolution
  ~XBIOS(6,L:V:low.pal$)                      ! change to Low-palette
  VSYNC
  low.res!=TRUE
  med.res!=FALSE
RETURN
' *********
'
