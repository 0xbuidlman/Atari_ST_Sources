' *** BITBLT.LST ***                                 2nd GFA-Manual, page 20-23
> PROCEDURE bitblt(sx1,sy1,sx2,sy2,dx1,dy1,mode)
  '
  ' *** Copy source-rectangle to destination-rectangle using mode& (=PUT-mode)
  ' *** Coordinates source and destination: (sx1,sy1,sx2,sy2) (dx1,dy1,dx2,dy2)
  ' *** Coordinates dx2 and dy2 are calculated in this Procedure
  ' *** Call Procedure Bitblt_screen_init or Bitblt_get_init first
  '
  ' *** Global:  s.mfdb%()  d.mfdb%()  bit.par%()
  '
  bit.par%(0)=sx1                             ! x1 of source-rectangle
  bit.par%(1)=sy1                             ! y1 of source-rectangle
  bit.par%(2)=sx2                             ! x2 of source-rectangle
  bit.par%(3)=sy2                             ! y2 of source-rectangle
  bit.par%(4)=dx1                             ! x1 of destination-rectangle
  bit.par%(5)=dy1                             ! y1 of destination-rectangle
  bit.par%(6)=ADD(dx1,SUB(sx2,sx1))           ! x2 of destination-rectangle
  bit.par%(7)=ADD(dy1,SUB(sy2,sy1))           ! y2 of destination-rectangle
  bit.par%(8)=mode                            ! PUT-mode
  BITBLT s.mfdb%(),d.mfdb%(),bit.par%()       ! do the bitblock transfer
RETURN
' *********
'
