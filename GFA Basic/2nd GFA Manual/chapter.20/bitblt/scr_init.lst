' *** SCR_INIT.LST ***                               2nd GFA-Manual, page 20-23
> PROCEDURE bitblt_screen_init(screen2%)
  '
  ' *** Fill Source/Destination Memory Form Description Blocks
  ' *** Source is screen at address screen2% (could be invisible second screen)
  ' *** The destination is the physical screen (monitor)
  ' *** The three integer-arrays are used by Procedure Bitblt
  '
  ' *** Standard: physbase%  x.max&  y.max&
  ' *** Global:  s.mfdb%()  d.mfdb%()  bit.par%()
  '
  IF DIM?(s.mfdb%())=0                          ! if arrays don't exist yet,
    DIM s.mfdb%(5),d.mfdb%(5),bit.par%(8)       ! create them now
  ENDIF
  '
  s.mfdb%(0)=screen2%                   ! source-screen
  s.mfdb%(1)=SUCC(x.max)                ! screen-width in pixels
  s.mfdb%(2)=SUCC(y.max)                ! screen-height in pixels
  s.mfdb%(3)=DIV(s.mfdb%(1),16)         ! screen-width in words
  s.mfdb%(4)=0                          ! reserved, always 0
  s.mfdb%(5)=INT{L~A}                   ! number of bitplanes (Line-A used!)
  '
  d.mfdb%(0)=physbase%                  ! destination-screen (monitor)
  d.mfdb%(1)=s.mfdb%(1)                 ! screen-width in pixels
  d.mfdb%(2)=s.mfdb%(2)                 ! screen-height in pixels
  d.mfdb%(3)=s.mfdb%(3)                 ! screen-width in words
  d.mfdb%(4)=0                          ! reserved, always 0
  d.mfdb%(5)=s.mfdb%(5)                 ! number of bitplanes (Line-A used!)
  '
RETURN
' *********
'
