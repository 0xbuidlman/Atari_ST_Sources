' *** INL_LOW.LST ***                                2nd GFA-Manual, page 20-24
> PROCEDURE degas_inline_med_low(degas%,sec)
  '
  ' *** Show Degas-picture in Low resolution from program in Medium resolution
  ' *** Procedure switches temporarily from Medium to Low resolution
  ' *** Procedure waits sec& seconds before switching back to Medium
  ' *** If sec&=0, after any keypress or mouse-click switch back to Medium
  ' *** Picture must be available in INLINE-line degas% (32034 or 32066 bytes)
  ' *** Colour animation data are ignored
  '
  ' *** Standard:  med.res!  physbase%
  ' *** Global:  degas% [INLINE-address]
  '
  LOCAL i,pal$
  IF med.res!
    FOR i=0 TO 15                                 ! save current palette
      pal$=pal$+MKI$(XBIOS(7,i,-1))
    NEXT i
    ~XBIOS(5,L:-1,L:-1,W:0)                       ! switch to Low resolution
    ~XBIOS(6,L:degas%+2)                          ! change palette
    BMOVE degas%+34,physbase%,32000               ! show picture
    IF sec=0
      WHILE MOUSEK                                ! mouse released?
      WEND
      {ADD(XBIOS(14,1),6)}=0                      ! clear keyboard-buffer
      REPEAT
      UNTIL LEN(INKEY$) OR MOUSEK                 ! wait...
    ELSE
      PAUSE sec*50                                ! wait...
    ENDIF
    ~XBIOS(5,L:-1,L:-1,W:1)                       ! switch back to Medium
    ~XBIOS(6,L:V:pal$)                            ! restore palette
  ENDIF
RETURN
' *********
'
