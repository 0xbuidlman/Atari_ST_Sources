' *** SAVE.LST ***                                   2nd GFA-Manual, page 20-25
> PROCEDURE degas_save(file$,VAR saved!)
  '
  ' *** Save logical screen as uncompressed Degas-picture (with palette)
  ' *** Use correct extension:  PI1=low  PI2=middle  PI3=high
  ' *** The optional colour animation data (32 bytes) are not saved
  ' *** Variabele saved!=TRUE if successful
  ' *** A garbage collection is forced
  '
  ' *** Standard:  high.res!  med.res!  low.res!
  '
  LOCAL screen$,r$,pal$,i%,degas$,res$,target$,name$,m$,k,p,back$,e$
  SGET screen$
  file$=UPPER$(file$)
  r$=UPPER$(RIGHT$(file$,4))
  LET saved!=TRUE
  pal$=""
  target$=file$
  name$=UPPER$(RIGHT$(file$,LEN(file$)-RINSTR(file$,"\")))
  FOR i%=0 TO 15
    pal$=pal$+MKI$(XBIOS(7,i%,-1))      ! current palette
  NEXT i%
  IF high.res! AND r$=".PI3"
    res$=MKI$(2)                        ! current resolution
  ELSE IF med.res! AND r$=".PI2"
    res$=MKI$(1)
  ELSE IF low.res! AND r$=".PI1"
    res$=MKI$(0)
  ELSE
    LET saved!=FALSE                    ! wrong extension: don't save picture
    IF high.res!
      e$="|Use extension .PI3 (High)"
    ELSE IF medium.res!
      e$="|Use extension .PI2 (Medium)"
    ELSE IF low.res!
      e$="|Use extension .PI1 (Low)"
    ENDIF
    m$="       *** Error ***|Procedure Degas_save refused|to save the file "
    m$=m$+name$+e$
    ALERT 3,m$,1," OK ",k
  ENDIF
  IF saved!
    IF EXIST(target$)                   ! does file exist already?
      m$="The file "+name$+"|exists already: delete file |or rename as *.BAK?"
      ALERT 3,m$,0,"Delete|*.BAK",k
      IF k=1
        KILL target$                    ! kill that file
      ELSE
        p=INSTR(target$,".")
        back$=LEFT$(target$,p)+"BAK"
        IF EXIST(back$)                 ! kill old BAK-file
          KILL back$
        ENDIF
        RENAME target$ AS back$         ! rename that file as *.BAK"
      ENDIF
    ENDIF
    degas$=res$+pal$+screen$            ! Degas-picture (no colour-animation)
    BSAVE file$,V:degas$,LEN(degas$)    ! save the Degas-picture
    CLR screen$,pal$,degas$
    ~FRE(0)                             ! garbage collection
  ENDIF
RETURN
' *********
'
