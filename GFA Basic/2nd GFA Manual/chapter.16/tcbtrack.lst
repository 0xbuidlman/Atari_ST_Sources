' *** TCBTRACK.LST ***                               2nd GFA-Manual, page 16-13
> PROCEDURE tcb_tracker(file$,loudness!)
  '
  ' *** Play TCB Tracker-song file$ (*.MOD); only once in program!
  ' *** Replay-routine TRACKER.ROT (for ST) must be in default-path
  ' *** Use in Low/Medium resolution with vertical frequency of 50 Hz
  ' *** Music is played faster with 60 Hz, too fast in High resolution (72 Hz)
  '
  ' *** Standard:  default.path$
  ' *** Global:  tracker|()  tracker.song|()
  ' *** Procedure:  Program_exit
  '
  LOCAL tracker$,tracker%,length%,tracker.song%
  tracker$=default.path$+"TRACKER.ROT"
  IF EXIST(tracker$)
    DIM tracker|(48000)
    tracker%=V:tracker|(0)
    BLOAD tracker$,tracker%                               ! load replay-routine
    '
    IF EXIST(file$)
      length%={FGETDTA()+26}
      DIM tracker.song|(length%-1)
      tracker.song%=V:tracker.song|(0)
      BLOAD file$,tracker.song%                           ! load song
      ADD tracker%,&H1C                                   ! move replay-pointer
      LPOKE tracker%,tracker.song%                        ! song-address
      ADD tracker%,4
      IF loudness!
        LPOKE tracker%,-1
      ELSE
        LPOKE tracker%,0
      ENDIF
      ADD tracker%,12                                     ! start of routine
      CALL tracker%                                       ! play the song
    ELSE
      m$="*** Error ***| |File "+RIGHT$(file$,LEN(file$)-RINSTR(file$,"\"))
      m$=m$+"|not found"
      ALERT 3,m$,1,"Continue|Quit",k
      IF k=2
        @program_exit
      ENDIF
    ENDIF
  ELSE
    m$="*** Error ***| |File TRACKER.ROT"
    m$=m$+"|not found"
    ALERT 3,m$,1,"Continue|Quit",k
    IF k=2
      @program_exit
    ENDIF
  ENDIF
RETURN
' *********
'
