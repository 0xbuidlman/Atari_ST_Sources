' *** MSM_INIT.LST ***                               2nd GFA-Manual, page 16-12
> PROCEDURE msm_init(file$,VAR msm.ok!)
  '
  ' *** Reserve memory for Mini-Soundmachin object-file and load file
  ' *** Procedure uses RESERVE and MALLOC
  ' *** Returns msm.ok!=TRUE if object-file was loaded
  ' *** Always call Procedure Msm_exit to restore memory
  '
  ' *** Procedure:  Program_exit
  ' *** Global:  msm.adr%  msm.flags%  msm.systim%
  '
  LOCAL file%,res.mem%,m$,k
  IF EXIST(file$)
    msm.ok!=TRUE
    file%={FGETDTA()+26}
    res.mem%=file%+256-MOD(file%,256)  ! multiple of 256
    RESERVE -res.mem%                  ! reserve memory for object-file
    msm.adr%=MALLOC(file%)             ! memory now available
    msm.flags%=msm.adr%+&HC64          ! flag-addresses
    msm.systim%=LPEEK(&H114)           ! 200 Hz Systemtimer (Timer C)
    BLOAD file$,msm.adr%               ! load object-file
    ~XBIOS(38,L:msm.adr%)              ! assembler-routine in Supervisor-mode
  ELSE
    m$="*** Error ***| |File "+RIGHT$(file$,LEN(file$)-RINSTR(file$,"\"))
    m$=m$+"|not found "
    ALERT 3,m$,1,"Continue|Quit",k
    IF k=1
      msm.ok!=FALSE
    ELSE
      @program_exit
    ENDIF
  ENDIF
RETURN
' *********
'
