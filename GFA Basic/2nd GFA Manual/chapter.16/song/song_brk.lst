' *** SONG_BRK.LST ***                               2nd GFA-Manual, page 16-13
> PROCEDURE song_break
  '
  ' *** Interrupt song and switch key-click on
  ' *** Continue song again with @song_continue
  '
  ' *** Global:  song.pointer%  song.register()  [for Song_continue]
  '
  LOCAL i
  DIM song.register(15)
  song.pointer%=XBIOS(32,L:-1)           ! where are we?
  FOR i=0 TO 15
    song.register(i)=XBIOS(28,0,i)       ! store registers
  NEXT i
  SPOKE &H484,BSET(PEEK(&H484),0)        ! key-click on
  WAVE 0,0
RETURN
' ***
> PROCEDURE song_continue
  '
  ' *** Continue a song that was interrupted by Procedure Song_break
  '
  LOCAL i
  SPOKE &H484,BCLR(PEEK(&H484),0)        ! key-click off
  FOR i=0 TO 15
    ~XBIOS(28,song.register(i),i OR 128) ! restore registers
  NEXT i
  IF song.pointer%=0                     ! song was just finished
    song.pointer%=song.adr%
  ENDIF
  ~XBIOS(32,L:song.pointer%)             ! play the music
  ERASE song.register()
RETURN
' *********
'
