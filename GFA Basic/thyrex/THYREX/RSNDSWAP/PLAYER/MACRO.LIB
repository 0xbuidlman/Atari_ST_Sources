;	toutes le macro		NEW-FACE
env	MACRO
	btst	#1,\1
	beq.s	*-4-2
	endm
rec	MACRO
	btst	#0,\1
	beq.s	*-4-2
	endm

ReadHost	MACRO	dst			
.\@	btst	#0,dsp_base+2.w
	beq.s	.\@
	move.l	dsp_base+4.w,\1
	ENDM
WritHost	MACRO	src				
.\@	btst	#1,dsp_base+2.w
	beq.s	.\@
	move.l	\1,dsp_base+4.w
	ENDM

save_host	macro
	move.b	#$80+$13,1(a5)		;HOST commande $13
	move.w	#1000,d0		
.loop	tst.b	1(a5)			;Attendre le d‚clenchement cot‚ DSP
	dbpl	d0,.loop	
	move.l	#'RSP',d3		;"Real Sound Player"
	move.l	d3,d2			;On va recevoir au maximum 2 valeurs+"RSP"
	move.l	d3,d1			;Principe des "registres en d‚calages"
.link	move.l	d1,d0			
	move.l	d2,d1			;
	ReadHost	d2		;r‚cupere ds le Host...
	cmp.l	d3,d2			;="RSP" ?
	bne.s	.link			;Non, on recommence
	WritHost	d3		;Balance notre identification, et lire
	ReadHost	-(sp)		;ce que le mixer a capt‚ pour Host->DSP
	ReadHost	-(sp)		
	WritHost	d0		;On lui envoie ce que l'on a capt‚.
	WritHost	d1		
	endm

restore_host	macro
.wc	btst	#3,2(a5)		;on attend la fin du traitement
	beq.s	.wc			;par le DSP
	moveq	#1,d0			
.resend	move.l	(sp)+,d1		;On restitue au DSP 
	cmpi.l	#'RSP',d1		;le(s) valeurs qu'on
	beq.s	.nosend			;a r‚cup‚r‚ au
	move.l	d1,4(a5)		;d‚clenchement de la commande
.nosend	dbf	d0,.resend		
	endm




se_period	macro	\1,\2,\3,\4
			;adresse_table
			;valeur a rechercher
			; offset dans la table (-2)
			; increment (variable)
	moveq	#0,\3
	moveq	#37,\4
.loop
	lsr	\4
	addx	\4,\3
.loop2
	cmp	-2(\1,\3*2),\2
	blt.s	.loop
	beq.s	.find
	lsr	\4
	subx	\4,\3
	bra.s	.loop2

.find
		endm


se_period2	macro	\1,\2,\3
			;adresse_table
			;valeur a rechercher
			;compteur
	moveq	#3,\3
.loop
	lea	8*2(\1),\1
	cmp	(\1),\2
	dbge	\3,.loop
	beq.s	.find
	bgt.s	.search	
	lea	5*2(\1),\1
.search	
	move	#7,\3
.loop2
	cmp	-(\1),\2
	dblt	\3,.loop2
	beq.s	.find
	lea	2(\1),\1
.find
;	\1
	endm
	




;tous les effects
set_wave_ctrl	macro
	move.b	n_wavecontrol+1(a4),d1
	btst	#2,d1
	bne.s	.pt_vibnoc
	clr.b	n_vibratopos(A4)
.pt_vibnoc
	btst	#6,d1
	bne.s	.pt_trenoc
	clr.b	n_tremolopos(A4)
.pt_trenoc
	move.l	n_lengt(A4),s_lengt(A5)	;Set length
	move.l	n_start(a4),s_start(a5)		;Set start
.pt_sdmaskp
	move.w	n_period(A4),s_period(a5)
	move.b	#$fe,s_flag_info(a5)	;s_ptr_sample(a5)=s_loopstart(a5)	
	st	n_trigger(A4)
	endm

