'
DO
  c%=@custom_alert(5,5,0,1,0,1,"GOOD MORNING!|What A Super|Day!!!","Yes|No|Bog Off!")
  PRINT ,c%
LOOP UNTIL c%=3
EDIT
' *************************************************************************
' * Custom Alert Boxes Coded by Brimstone Computers ½ 1990                *
' *                                                                       *
' * To use, Merge the procedures below into your code                     *
' * Use:                                                                  *
' * r%=@custom_alert(x%,y%,fc%,bc%,bord%,shad%,l_text$,b_text$)           *
' * Where:                                                                *
' *  x% & y% = Top left hand coordinates of the alert box in              *
' *                Character cells                                        *
' *      fc% = Is the pen colour of the Alert Box Text                    *
' *      bc% = Is the paper colour of the Alert Box                       *
' *    bord% = Is the border colour of the Alert Box                      *
' *    shad% = Is the drop shadow colour of the Alert Box                 *
' *  l_text$ = Is the text for the Alert Box lines                        *
' *            Each line must be spereated by a vertical bar '|'          *
' *            The first line is assumed to be a title                    *
' *  b_text$ = Is the text for the buttons                                *
' *            Each button must be seperated by a vertical bar '|'        *
' *       r% = Is a variable to return the selected option                *
' * WARNING: The colour of text, line, fill pattern, etc. are smashed     *
' *************************************************************************
'
FUNCTION custom_alert(xpos%,ypos%,fore_col%,back_col%,bord_col%,shad_col%,l_text$,b_text$)
  LOCAL c%,t%,max_len%,depth%,tx1%,ty1%,tx2%,ty2%,opt%,key$,maximum%,button1$,button2$,button3$,but_len%,num_but%,mx%,my%,mk%,retval%,repair$,rx%,ry%
  '
  ' Seperate the 3 possible buttons
  '
  but_len%=0
  num_but%=0
  IF INSTR(b_text$,"|")
    button1$=LEFT$(b_text$,INSTR(b_text$,"|")-1)
    but_len%=but_len%+LEN(button1$)
    INC num_but%
    b_text$=MID$(b_text$,INSTR(b_text$,"|")+1)
    IF INSTR(b_text$,"|")
      button2$=LEFT$(b_text$,INSTR(b_text$,"|")-1)
      but_len%=but_len%+LEN(button2$)
      INC num_but%
      b_text$=MID$(b_text$,INSTR(b_text$,"|")+1)
      IF INSTR(b_text$,"|")
        button3$=LEFT$(b_text$,INSTR(b_text$,"|")-1)
        but_len%=but_len%+LEN(button3$)
        INC num_but%
        b_text$=MID$(b_text$,INSTR(b_text$,"|")+1)
      ELSE
        IF LEN(b_text$)
          button3$=b_text$
          INC num_but%
          but_len%=but_len%+LEN(button3$)
        ELSE
          button3$=""
        ENDIF
      ENDIF
    ELSE
      button3$=""
      IF LEN(b_text$)
        button2$=b_text$
        INC num_but%
        but_len%=but_len%+LEN(button2$)
      ELSE
        button3$=""
      ENDIF
    ENDIF
  ELSE
    button2$=""
    button3$=""
    IF LEN(b_text$)
      button1$=b_text$
      INC num_but%
      but_len%=but_len%+LEN(button1$)
    ELSE
      button1$=""
    ENDIF
  ENDIF
  IF num_but%=0
    c%=0
    GOTO leave_now
  ENDIF
  but_len%=but_len%+3*num_but%-1
  '
  ' Determine the longest line & total parameters
  '
  max_len%=0
  maximum%=0
  c%=1
  REPEAT
    t%=INSTR(c%,l_text$,"|")
    IF t% THEN
      INC maximum%
      IF max_len%<t%-c% THEN
        max_len%=t%-c%
      ENDIF
      c%=t%+1
    ELSE
      IF LEN(MID$(l_text$,c%))
        INC maximum%
        t%=LEN(l_text$)+1
        IF max_len%<t%-c%
          max_len%=t%-c%
        ENDIF
        c%=t%+1
      ENDIF
    ENDIF
  UNTIL t%=0
  IF but_len%>max_len%
    max_len%=but_len%
  ENDIF
  '
  ' Adjust character height in mono
  '
  IF XBIOS(4)=2
    depth%=16
  ELSE
    depth%=8
  ENDIF
  '
  ' Set up line & fill parameters
  '
  DEFLINE ,1
  COLOR bord_col%
  GRAPHMODE 2
  DEFTEXT fore_col%
  '
  '
  ' Draw `window` in which menu is to appear
  '
  tx1%=xpos%*8
  ty1%=ypos%*depth%
  tx2%=(xpos%+max_len%+2)*8
  ty2%=(ypos%+maximum%+8)*depth%
  '
  ' Grab the area below for later repair
  '
  GET tx1%,ty1%,tx2%+4,ty2%+4,repair$
  rx%=tx1%
  ry%=ty1%
  '
  ' First draw the drop shadow
  '
  DEFFILL shad_col%,2,8
  PBOX tx1%+4,ty1%+4,tx2%+4,ty2%+4
  '
  ' Now draw the alert box
  '
  DEFFILL back_col%,2,8
  PBOX tx1%,ty1%,tx2%,ty2%
  BOX tx1%+2,ty1%+2,tx2%-2,ty2%-2
  BOX tx1%+4,ty1%+4,tx2%-4,ty2%-4
  '
  ' Insert Text into Alert Box
  '
  IF maximum%
    '
    ' Display Title as underlined & Emboldened
    '
    DEFTEXT ,9
    c%=INSTR(l_text$,"|")
    IF c%
      key$=LEFT$(l_text$,c%-1)
      l_text$=MID$(l_text$,c%+1)
    ELSE
      key$=l_text$
      l_text$=""
    ENDIF
    TEXT tx1%+8+max_len%*4-LEN(key$)*4,ty1%+depth%*3,key$
    '
    ' Display the remaining text lines (if there are any)
    '
    DEFTEXT ,0
    t%=ty1%+depth%*5
    WHILE LEN(l_text$)
      c%=INSTR(l_text$,"|")
      IF c%
        key$=LEFT$(l_text$,c%-1)
        l_text$=MID$(l_text$,c%+1)
      ELSE
        key$=l_text$
        l_text$=""
      ENDIF
      TEXT tx1%+8+max_len%*4-LEN(key$)*4,t%,key$
      t%=t%+depth%
    WEND
  ELSE
    t%=depth%*5+depth%/2
  ENDIF
  '
  ' Now display the buttons
  '
  IF max_len%>but_len%
    tx1%=tx1%+max_len%*4-but_len%*4
  ENDIF
  @show_button(tx1%+8,t%,button1$,depth%)
  IF num_but%>1
    @show_button(tx1%+32+LEN(button1$)*8,t%,button2$,depth%)
    IF num_but%=3
      @show_button(tx1%+56+(LEN(button1$)+LEN(button2$))*8,t%,button3$,depth%)
    ENDIF
  ENDIF
  '
  ' Now wait for either F1, F2 or F3
  '
  c%=0
  REPEAT
    key$=INKEY$
    MOUSE mx%,my%,mk%
    IF key$=CHR$(0)+CHR$(59)
      c%=1
    ELSE
      IF key$=CHR$(0)+CHR$(60) AND num_but%>1
        c%=2
      ELSE
        IF key$=CHR$(0)+CHR$(61) AND num_but%=3
          c%=3
        ENDIF
      ENDIF
    ENDIF
    '
    ' Test for mouse read
    '
    IF mk%=1 AND c%=0
      c%=@check_mouse(mx%,my%,tx1%+8,t%,depth%,LEN(button1$))
      c%=-1*(c%=1)
      IF num_but%>1 AND c%=0
        c%=@check_mouse(mx%,my%,tx1%+32+LEN(button1$)*8,t%,depth%,LEN(button2$))
        c%=-2*(c%=1)
        IF num_but%=3 AND c%=0
          c%=@check_mouse(mx%,my%,tx1%+56+(LEN(button1$)+LEN(button2$))*8,t%,depth%,LEN(button3$))
          c%=-3*(c%=1)
        ENDIF
      ENDIF
    ENDIF
  UNTIL c%
  leave_now:
  PUT rx%,ry%,repair$
  RETURN c%
ENDFUNC
'
> PROCEDURE show_button(tx%,ty%,tx$,dp%)
  BOX tx%+2,ty%+2,tx%+(LEN(tx$)+2)*8-2,ty%+dp%*3-2
  BOX tx%+4,ty%+4,tx%+(LEN(tx$)+2)*8-4,ty%+dp%*3-4
  TEXT tx%+8,ty%+dp%*2-1,tx$
RETURN
'
FUNCTION check_mouse(mx%,my%,tx%,ty%,dp%,lt%)
  LOCAL retbut%
  IF mx%>=tx%+8 AND mx%<=tx%+lt%*8+7 AND my%>=ty%+dp%-2 AND my%<=ty%+dp%*2+2
    retbut%=1
  ELSE
    retbut%=0
  ENDIF
  RETURN retbut%
ENDFUNC
