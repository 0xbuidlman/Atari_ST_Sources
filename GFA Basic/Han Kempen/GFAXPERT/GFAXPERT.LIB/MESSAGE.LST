' *******************
' *** MESSAGE.LST ***
' *******************
'
DEFWRD "a-z"
'
> PROCEDURE message(text$)
  ' *** put message Text$ on bottom of screen and wait for <Return>
  ' *** uses Standard Globals
  LOCAL temp$,w1$,w2$
  GET 0,scrn.y.max-2*char.height,scrn.x.max,scrn.y.max,temp$
  PRINT CHR$(27);"Y";CHR$(31+scrn.lin.max-1);CHR$(31+1);
  PRINT CHR$(27);"J";
  PRINT SPACE$(scrn.col.max/2-(LEN(text$)+2)/2);
  PRINT CHR$(27);"p";
  PRINT " "+text$+" ";
  PRINT CHR$(27);"q";
  w1$="<RETURN>"
  w2$=SPACE$(8)
  SOUND 1,10,12,4,25
  SOUND 1,10,6,4,25
  SOUND 1,10,12,4,50
  SOUND 1,0
  PRINT AT(scrn.col.max/2-3,scrn.lin.max);w1$;
  WHILE INKEY$<>return$
    PAUSE 30
    SWAP w1$,w2$
    PRINT AT(scrn.col.max/2-3,scrn.lin.max);w1$;
  WEND
  PUT 0,scrn.y.max-2*char.height,temp$,3      ! restore screen
RETURN
' **********
'
> PROCEDURE warning(text$)
  ' *** put warning Text$ on bottom of screen and wait for <Return> or <Esc>
  ' *** uses Standard Globals
  ' *** global :  OK!
  LOCAL temp$,w1$,w2$
  GET 0,scrn.y.max-2*char.height,scrn.x.max,scrn.y.max,temp$
  PRINT CHR$(27);"Y";CHR$(31+scrn.lin.max-1);CHR$(31+1);
  PRINT CHR$(27);"J";
  PRINT SPACE$(scrn.col.max/2-(LEN(text$)+2)/2);
  PRINT CHR$(27);"p";
  PRINT " "+text$+" ";
  PRINT CHR$(27);"q";
  w1$="<RETURN> or <ESC>"
  w2$=SPACE$(17)
  SOUND 1,10,12,4,25
  SOUND 1,10,6,4,25
  SOUND 1,10,12,4,50
  SOUND 1,0
  PRINT AT(scrn.col.max/2-8,scrn.lin.max);w1$;
  REPEAT
    key$=INKEY$
    PAUSE 30
    SWAP w1$,w2$
    PRINT AT(scrn.col.max/2-8,scrn.lin.max);w1$;
  UNTIL key$=return$ OR key$=esc$
  IF key$=return$
    ok!=TRUE
  ELSE
    ok!=FALSE
  ENDIF
  PUT 0,scrn.y.max-2*char.height,temp$,3     ! restore screen
RETURN
' **********
'
> PROCEDURE message.on(text$)
  ' *** put message Text$ on bottom line of screen
  ' *** call Procedure Message.off to restore original screen
  ' *** uses Standard Globals
  ' *** global :  MESSAGE.TEMP$
  GET 0,scrn.y.max-char.height,scrn.x.max,scrn.y.max,message.temp$
  PRINT CHR$(27);"Y";CHR$(31+scrn.lin.max);CHR$(31+1);
  PRINT CHR$(27);"J";
  PRINT SPACE$(scrn.col.max/2-(LEN(text$)+2)/2);
  PRINT CHR$(27);"p";
  PRINT " "+text$+" ";
  PRINT CHR$(27);"q";
RETURN
' ***
> PROCEDURE message.off
  PUT 0,scrn.y.max-char.height,message.temp$,3 ! restore screen
RETURN
' **********
'
> PROCEDURE achtung
  ' *** little joke for user who presses the wrong key
  ' *** High resolution only !
  LOCAL screen$,i,n,in$
  ON BREAK CONT
  SGET screen$
  SOUND 1,15,1,8,5
  WAVE 8,8,10,5,5
  FOR n=1 TO 5
    SETCOLOR 0,0
    CLS                                  ! black screen
    PAUSE 10
    SETCOLOR 0,1
    CLS                                  ! white screen
  NEXT n
  WAVE 0,0
  SETCOLOR 0,0
  CLS
  PAUSE 100
  SETMOUSE 639,399
  DEFTEXT 1,1,0,32
  TEXT 140,40,"!!   A C H T U N G   !!"
  FOR n=1 TO 5
    FOR i=1 TO 8
      SOUND 1,15,i,4,1
    NEXT i
    PAUSE 5
    FOR i=1 TO 8
      SOUND 1,15,i,4,1
    NEXT i
    PAUSE 5
  NEXT n
  WAVE 0,0
  DELAY 3
  DEFTEXT ,0,,16
  TEXT 230,70,"Alles Lookenpeepers"
  DELAY 1
  TEXT 10,120,"Das Computenmachin is nicht fšr Gefingerpoken und Mittengrabben."
  DELAY 6
  TEXT 10,170,"Ist easy schnappen der Springenwerk, Blowenfusen und Poppen-"
  TEXT 10,220,"corken mit Spitzensparken !"
  DELAY 10
  DEFTEXT ,4
  TEXT 10,270,"Das ST-Computer ist nicht fšr gewerken by das Dummkopfen !!"
  DELAY 4
  DEFTEXT ,0
  TEXT 10,320,"Das rubbernecken Sightseeren keepen Hands in das Pockets."
  TEXT 10,370,"Relaxen und next Time warten bis Computer Ready !"
  DELAY 6
  DEFTEXT ,,,8
  TEXT 10,397,"Fingerpoken any Key vor Returning to Programm ..."
  WHILE INKEY$<>""
    in$=INKEY$
  WEND
  WHILE INKEY$=""
  WEND
  SETCOLOR 0,1
  SPUT screen$
  ON BREAK
RETURN
' **********
'
> PROCEDURE busy
  ' *** fill screen with the busy bee (High resolution only)
  LOCAL i
  SETMOUSE 8,8
  DEFMOUSE 2
  IF DIM?(busy.bee())=0
    DIM busy.bee(15)
  ENDIF
  FOR i=0 TO 15
    busy.bee(i)=INT{XBIOS(2)+i*80}
  NEXT i
  ACLIP 1,0,0,639,399
  ARECT 0,0,639,399,1,0,V:busy.bee(0),15
  PRINT AT(25,13);"  Please wait, I am busy ...  "
  ACLIP 0,0,0,639,399
  DEFMOUSE 0
  HIDEM
RETURN
' **********
'
