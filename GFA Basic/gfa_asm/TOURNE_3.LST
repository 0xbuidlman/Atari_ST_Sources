' tourne_3.lst
' pour faire tourner un graphique d'un angle quelconque
' -----------------------------------------------------
' cr‚ation du graphique
DEFFILL 1,3,8
PBOX 1,1,208,48
DEFTEXT 1,0,0,26
TEXT 13,36,"GFA - Basic"
'
' Offset du coin sup‚rieur gauche
x%=1
y%=1
' Largeur et hauteur
w%=208
h%=48
' angle de rotation
tour%=45
'
' suavegarde du graphique
GET x%,y%,x%+w%-1,y%+h%-1,s$
s%=V:s$+6
'
' longueur en octets du graphique au format GET
xb%=((w%+15) DIV 16)*2
'
' la destination est toujours l'‚cran
'
ALERT 2,"Rotation du graphique en|GFA Basic ,GFA Ass.(exact)|ou en GFA Ass.(rapide)?",1,"Basic|Exact|Rapide",question%
'
' Rotation en GFA Basic
IF question%=1
  t=TIMER
  ' Adresse du stockage correspond au coin
  ' inf‚rieur gauche de l'‚cran
  dh%=XBIOS(2)+80*399
  '
  ' valeurs de d‚part pour les coordonn‚es cible
  s=SINQ(tour%)
  c=COSQ(tour%)
  ' amplitude
  pas=2/3
  ' afficher le graphique tourn‚ au milieu
  xm%=320-((h%-1)*s+(w%-1)*c)/2+0.5
  ym%=200-((h%-1)*c-(w%-1)*s)/2+0.5
  ' faire disparaitre temporairement la souris
  HIDEM
  ' poser une bande en bas de l'‚cran calculer coordonn‚es
  ' afficher le point de rotation … l'‚cran
  ' condition IF indispensable car clipping impossible
  FOR y0=0 TO h%-1+pas STEP pas
    BMOVE s%+INT(y0)*xb%,dh%,xb%
    yh=ym%+y0*c+0.5
    xh=xm%+y0*s+0.5
    FOR x0=0 TO w%-1+pas STEP pas
      yk%=yh-x0*s
      IF yk%<399
        xk%=xh+x0*c
        IF yk%>=0 AND xk%>=0 AND xk%<=639
          PSET xk%,yk%,PTST(x0,399)
        ENDIF
      ENDIF
    NEXT x0
  NEXT y0
  '
  ' effacer la ligne inf‚rieure en bas de l'‚cran
  COLOR 0
  LINE 0,399,639,399
  COLOR 1
  SHOWM
ENDIF
'
' Rotation avec GFA Assembleur
IF question%>1
  t=TIMER
  '
  ass_1$=SPACE$(1098)
  ass_1%=VARPTR(ass_1$)
  BLOAD "tour_a1.prg",ass_1%
  '
  ass_2$=SPACE$(1178)
  ass_2%=VARPTR(ass_2$)
  BLOAD "tour_a2.prg",ass_2%
  '
  ' appel du programme en Assembleur dans lequel
  sadr%=s%              ! Adresse du bitmap source  paire
  sb%=xb%               ! largeur en octets     paire
  x0%=0                 ! point de d‚part abscisse x en q
  y0%=0                 ! point de d‚part ordonn‚e y en q
  w%=w%                 ! largeur du fragment image
  h%=h%                 ! hauteur du fragment image
  x1%=320               ! coordonn‚e de destination x
  y1%=200               ! coordonn‚e de destination y
  tadr%=XBIOS(2)        ! adresse bitmap destination paire
  tb%=80                ! largeur bitmap en octets   paire
  phi%=tour%*182        ! angle compris entre 0 et 65535
  '
  IF question%=2
    VOID C:ass_1%(L:sadr%,sb%,x0%,y0%,w%,h%,L:tadr%,tb%,x1%,y1%,phi%)
  ELSE
    VOID C:ass_2%(L:sadr%,sb%,x0%,y0%,w%,h%,tadr%,L:tb%,x1%,y1%,phi%)
  ENDIF
ENDIF
'
tt=(TIMER-t)/200
t$=STR$(tt)
'
IF question%=1
  ALERT 1,"En GFA Basic le|graphique|tourne en "+t$+" secondes.",1," OK ",d%
ELSE
  ALERT 1,"En GFA Assembleur le |graphique|tourne en "+t$+" secondes.",1," OK ",d%
ENDIF
EDIT
