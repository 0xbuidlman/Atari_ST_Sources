' 90_BSET3.LST
' -------------
'
tourner$=SPACE$(213)
tourner%=VARPTR(tourner$)
BLOAD "90_ass.prg",tourner%
DEFFILL 1,3,8
PBOX 1,1,208,48
DEFTEXT 1,0,0,26
TEXT 13,36,"GFA - BASIC"
'
' La largeur et la hauteur, mesur‚es en pixels, doivent ˆtre
' divisibles par 8
x%=208
y%=48
' Largeur et hauteur en pixels/8
xl%=x%/8
yl%=y%/8
'
' s$= graphique d'origine, s=source
GET 1,1,1+x%-1,1+y%-1,s$
' d$= r‚serve la place pour le graphique tourn‚, d=destination
d$=LEFT$(s$,6)+STRING$(xl%*y%,0)
' modifier le header de d$: XXYY -> YYXX
LPOKE V:d$,SWAP(LPEEK(V:d$))
'
' adresse de l'information graphique (octet 0)
s%=V:s$+6
'
' adresse du dernier octet de l'information graphique
se%=s%+LEN(s$)-7
'
' adresse du graphique cible (octet 0)
d%=V:d$+6
' adresse du dernier octet du graphique cible
de%=d%+LEN(s$)-7
'
ALERT 2,"Tourner un graphique de 90 degr‚s|en GFA Basic ou|en GFA ASSEMBLEUR ?",1,"Basic|Ass.",question%
'
' Tourner le graphique avec le GFA Basic
IF question%=1
  t=TIMER
  ' compteur de bit de 7 … 0
  bit%=7
  '
  dh%=0
  '
  ' analyser les lignes de graphique l'une aprŠs l'autre
  FOR p%=s% TO se% STEP xl%
    ' adresse de l'octet cible
    dd%=d%+(x%-1)*yl%+dh%
    ' traiter la ligne graphique octet par octet
    FOR m%=p% TO p%+xl%-1
      ' lecture de l'octet
      a%=BYTE{m%}
      ' examiner les 8 bits
      FOR n%=7 DOWNTO 0
        b%=BTST(a%,n%)
        IF b%
          ' mettre un bit dans le graphique cible, c.….d.:
          ' lire l'octet, mettre un bit, r‚‚crire l'octet
          BYTE{dd%}=BSET(BYTE{dd%},bit%)
        ENDIF
        ' sauter d'une ligne graphique vers le haut
        SUB dd%,yl%
      NEXT n%
    NEXT m%
    ' diminuer de 1 le num‚ro de bit (pour le graphique cible)
    DEC bit%
    ' passer … l'octet suivant
    IF bit%<0
      bit%=7
      INC dh%
    ENDIF
  NEXT p%
ENDIF
'
' tourner le graphique, … l'aide du GFA Assembleur
IF question%=2
  ' appel du programme en Assembleur, dans lequel
  ' s% et d% sont les adresses de l'information graphique
  ' x% et y% sont les dimensions du graphique d'origine, en
  ' pixels
  ' x% et y% devant ˆtre divisibles par 8.
  t=TIMER
  ~C:tourner%(L:s%,L:d%,W:x%,W:y%)
ENDIF
'
tt=(TIMER-t)/200
'
' g‚n‚rer le graphique tourn‚
PUT 222,1,d$
'
t$=STR$(tt)
IF question%=1
  ALERT 1,"En GFA Basic, faire tourner le |graphique de 90 degr‚s demande|"+t$+" secondes.",1," OK ",d%
ELSE
  ALERT 1,"En GFA Assembleur, |faire tourner le graphique de|90 degr‚s demande|"+t$+" secondes.",1," OK ",d%
ENDIF
'
EDIT
