;***     SOURCE FOR 'BIG FF' SCREEN      ***
;*** WRITTEN BY STALIN OF FLEXIBLE FRONT ***
;***     FOR THE WHATTAHECK DEMO-89      ***
;***    RUNS WITH ASSEMPRO ASSEMBLER     ***


SCR=$F8000              ;$78000 FOR 520
SCR1=$F0000             ;$70000 FOR 520

 BRA START
BUFF:
 DS.B 3000,0

START:
 CLR.L -(SP)     ;SV MODE
 MOVE.W #$20,-(SP)
 TRAP #1
 ADDQ.L #6,SP
 MOVE.L D0,SPAR+4

 MOVE.W #5,-(SP)
 MOVE.W #26,-(SP)
 TRAP #14
 ADDQ.L #4,SP

 PEA MOUSE
 MOVE.W #1,-(SP)
 MOVE.W #25,-(SP)
 TRAP #14
 ADDQ.L #8,SP

 CLR.W -(SP)     ;SETSCR
 PEA SCR
 PEA SCR
 MOVE.W #5,-(SP)
 TRAP #14
 ADD.L #12,SP

 LEA $FF8240,A0
 LEA OPAL,A1
 MOVEQ #7,D0
 BSR COPY
 LEA PAL,A0
 LEA $FF8240,A1
 MOVEQ #7,D0
 BSR COPY

 BSR ROTATEFF
 BSR FIRSTAID

 LEA SCR1,A0
 MOVE.W #15999,D0
CLS:
 CLR.L (A0)+
 DBRA D0,CLS

 MOVEQ #1,D0
 JSR MUSIC+$54

 MOVE.W #$2700,SR
 JSR INITRAST
 MOVE.L $70,OVBL
 MOVE.L #VBL,$70
 MOVE.W #$2300,SR

DEADLOP:
 CMP.B #$66,$FFFC02
 BNE DEADLOP

EXIT:
 MOVE.W #$2700,SR
 MOVE.L OVBL,$70
 JSR REMOVERAST
 MOVE.W #$2300,SR

 MOVE.W #1,-(SP)
 PEA SCR
 PEA SCR
 MOVE.W #5,-(SP)
 TRAP #14
 ADD.L #12,SP

 LEA OPAL,A0
 LEA $FF8240,A1
 MOVEQ #7,D0
 BSR COPY

 MOVE.W #5,-(SP)
 MOVE.W #27,-(SP)
 TRAP #14
 ADDQ.L #4,SP

 MOVE.L SPAR+4,-(SP)
 MOVE.W #$20,-(SP)
 TRAP #1
 ADDQ.L #6,SP

 PEA MOUSE+2
 MOVE.W #1,-(SP)
 MOVE.W #25,-(SP)
 TRAP #14
 ADDQ.L #8,SP
 ILLEGAL
COPY:
 MOVE.L (A0)+,(A1)+
 DBRA D0,COPY
 RTS


VBL:
 MOVE.W #$0,$FF8240
 CLR.B $FFFA1B
 MOVEQ #1,D0
 MOVE.B D0,$FFFA21
 MOVE.B #8,$FFFA1B

 LEA COLTAB,A5

 LEA SCREENS,A0
 MOVE.L (A0),D0
 MOVE.L 4(A0),D1
 EXG.L D0,D1
 MOVE.L D0,(A0)
 MOVE.L D1,4(A0)
 MOVE.L D0,A6

 JSR BIGFF
 JSR DEMOLOG
 JSR SCROLL
 JSR MUSIC+$17C

 MOVE.B SCREENS+1,$FF8201
 MOVE.B SCREENS+2,$FF8203
 ADDQ.W #1,CLOCK
 RTE

DEMOLOG:
 MOVE.L A6,A1
DEMPEK:
 ADD.W SWEDESIN,A1
 LEA BIGSWEDE,A0

 MOVEQ #0,D0
 MOVE.W D0,-160(A1)
 MOVE.W D0,-152(A1)
 MOVE.W D0,-144(A1)
 MOVE.W D0,-136(A1)
 MOVE.W D0,-320(A1)
 MOVE.W D0,-312(A1)
 MOVE.W D0,-304(A1)
 MOVE.W D0,-296(A1)
 MOVE.W D0,-480(A1)
 MOVE.W D0,-472(A1)
 MOVE.W D0,-464(A1)
 MOVE.W D0,-456(A1)
 MOVE.W D0,-640(A1)
 MOVE.W D0,-632(A1)
 MOVE.W D0,-624(A1)
 MOVE.W D0,-616(A1)
N@=0
 REPEAT
 MOVEM.W (A0)+,D0-D7
 MOVE.W D0,(A1)
 MOVE.W D1,8(A1)
 MOVE.W D2,16(A1)
 MOVE.W D3,24(A1)
 ADD.W #160,A1
 MOVE.W D4,(A1)
 MOVE.W D5,8(A1)
 MOVE.W D6,16(A1)
 MOVE.W D7,24(A1)
 ADD.W A3,A1
N@=N@+1
 UNTILEQ N@,16
 MOVEQ #0,D0
 MOVE.W D0,(A1)
 MOVE.W D0,8(A1)
 MOVE.W D0,16(A1)
 MOVE.W D0,24(A1)
 MOVE.W D0,160(A1)
 MOVE.W D0,168(A1)
 MOVE.W D0,176(A1)
 MOVE.W D0,184(A1)
 MOVE.W D0,320(A1)
 MOVE.W D0,328(A1)
 MOVE.W D0,336(A1)
 MOVE.W D0,344(A1)
 MOVE.W D0,480(A1)
 MOVE.W D0,488(A1)
 MOVE.W D0,496(A1)
 MOVE.W D0,504(A1)
 MOVE.W D0,640(A1)
 MOVE.W D0,648(A1)
 MOVE.W D0,656(A1)
 MOVE.W D0,664(A1)

 CMP.L #SWEDEND-2,DEMPEK+2
 BNE DEM1
 MOVE.L #SWEDESIN-2,DEMPEK+2
DEM1:
 ADDQ.L #2,DEMPEK+2
 RTS

BIGFF:
BIGPEK7:
 LEA GRAF+(4800*15),A0
BIGPEK8:
 LEA GRAF+(4800*2),A4
 MOVE.L A6,A1
BIGPEK1:
 ADD.W #0,A1
 MOVE.L A6,A2
BIGPEK2:
 ADD.W #0,A2
 MOVE.L #160,A3

N@=0
 REPEAT
  MOVEQ #0,D0
  MOVE.W D0,-8(A1)
  MOVE.W D0,-8(A2)
  MOVEM.W (A0)+,D0-D7
  MOVE.W D0,(A1)
  MOVE.W D1,8(A1)
  MOVE.W D2,16(A1)
  MOVE.W D3,24(A1)
  MOVE.W D4,32(A1)
  MOVE.W D5,40(A1)
  MOVE.W D6,48(A1)
  MOVE.W D7,56(A1)
  MOVEM.W (A4)+,D0-D7
  MOVE.W D0,(A2)
  MOVE.W D1,8(A2)
  MOVE.W D2,16(A2)
  MOVE.W D3,24(A2)
  MOVE.W D4,32(A2)
  MOVE.W D5,40(A2)
  MOVE.W D6,48(A2)
  MOVE.W D7,56(A2)
  MOVEM.W (A0)+,D0-D7
  MOVE.W D0,64(A1)
  MOVE.W D1,72(A1)
  MOVE.W D2,80(A1)
  MOVE.W D3,88(A1)
  MOVE.W D4,96(A1)
  MOVE.W D5,104(A1)
  MOVE.W D6,112(A1)
  MOVE.W D7,120(A1)
  MOVEM.W (A4)+,D0-D7
  MOVE.W D0,64(A2)
  MOVE.W D1,72(A2)
  MOVE.W D2,80(A2)
  MOVE.W D3,88(A2)
  MOVE.W D4,96(A2)
  MOVE.W D5,104(A2)
  MOVE.W D6,112(A2)
  MOVE.W D7,120(A2)
  MOVEQ #0,D0
  MOVE.W D0,128(A1)
  MOVE.W D0,128(A2)
  ADD.L A3,A1
  ADD.L A3,A2
N@=N@+1
 UNTILEQ N@,150

FIRSTAID:

BIGPEK3:                ;UPDATE SINUS Y
 MOVE.W BIGYSIN,D0
 BNE BIG1
 MOVE.L #BIGYSIN,BIGPEK3+2
 MOVE.W BIGYSIN,D0
BIG1:
 MOVE.W D0,BIGPEK1+2
 ADDQ.L #2,BIGPEK3+2

BIGPEK4:
 MOVE.W BIGYSIN+32,D0
 BNE BIG2
 MOVE.L #BIGYSIN,BIGPEK4+2
 MOVE.W BIGYSIN,D0
BIG2:
 ADDQ.W #2,D0
 MOVE.W D0,BIGPEK2+2
 ADDQ.L #2,BIGPEK4+2

 CLR.W D0
BIGPEK5:
 MOVE.B BIGXSIN,D0
 BNE BIG3
 MOVE.L #BIGXSIN,BIGPEK5+2
 MOVE.B BIGXSIN,D0
BIG3:
 ADDQ.L #1,BIGPEK5+2
 MOVE.W D0,D1
 AND.W #%1111111111110000,D0   ;ADDER
 AND.W #%0000000000001111,D1   ;ANIMATION NUMBER
 LSR.W #1,D0
 ADD.W D0,BIGPEK1+2
 LEA ANTAB,A0
 LSL.W #2,D1
 MOVE.L (A0,D1.W),A0
 MOVE.L A0,BIGPEK7+2

 CLR.W D0
BIGPEK6:
 MOVE.B BIGXSIN+16,D0
 BNE BIG4
 MOVE.L #BIGXSIN,BIGPEK6+2
 MOVE.B BIGXSIN,D0
BIG4:
 ADDQ.L #1,BIGPEK6+2
 MOVE.W D0,D1
 AND.W #%1111111111110000,D0   ;ADDER
 AND.W #%0000000000001111,D1   ;ANIMATION NUMBER
 LSR.W #1,D0
 ADD.W D0,BIGPEK2+2
 LEA ANTAB,A0
 LSL.W #2,D1
 MOVE.L (A0,D1.W),A0
 MOVE.L A0,BIGPEK8+2

 RTS
ANTAB:
 DC.L GRAF,GRAF+4800,GRAF+(4800*2),GRAF+(4800*3)
 DC.L GRAF+(4800*4),GRAF+(4800*5),GRAF+(4800*6),GRAF+(4800*7)
 DC.L GRAF+(4800*8),GRAF+(4800*9),GRAF+(4800*10),GRAF+(4800*11)
 DC.L GRAF+(4800*12),GRAF+(4800*13),GRAF+(4800*14),GRAF+(4800*15)

SCROLL:     ;PEK!
 MOVE.W #0,D0
 BNE SCROL3
 MOVE.W #15,SCROLL+2
SCPEK2:  ;PEK!
 MOVE.B ASCCI,D0
 BNE SCROL4
 MOVE.L #ASCCI-1,SCPEK2+2
 MOVEQ #32,D0
SCROL4:
 ADDQ.L #1,SCPEK2+2
 SUB.B #32,D0
 LSL.W #5,D0
 LEA FONT,A0
 ADD.W D0,A0
 LEA BUFF+40,A1
 MOVEM.W (A0)+,D0-D7
 MOVE.W D0,(A1)
 MOVE.W D1,42(A1)
 MOVE.W D2,84(A1)
 MOVE.W D3,126(A1)
 MOVE.W D4,168(A1)
 MOVE.W D5,210(A1)
 MOVE.W D6,252(A1)
 MOVE.W D7,294(A1)
 MOVEM.W (A0)+,D0-D7
 MOVE.W D0,336(A1)
 MOVE.W D1,378(A1)
 MOVE.W D2,420(A1)
 MOVE.W D3,462(A1)
 MOVE.W D4,504(A1)
 MOVE.W D5,546(A1)
 MOVE.W D6,588(A1)
 MOVE.W D7,630(A1)

SCROL3:
 SUBQ.W #1,SCROLL+2
 LEA BUFF+42,A0
 MOVEQ #84,D0
 MOVEQ #15,D1
SCROL2:
 MOVE #0,CCR
 ROXL.W -(A0)
 ROXL.W -(A0)
 ROXL.W -(A0)
 ROXL.W -(A0)
 ROXL.W -(A0)
 ROXL.W -(A0)
 ROXL.W -(A0)
 ROXL.W -(A0)
 ROXL.W -(A0)
 ROXL.W -(A0)
 ROXL.W -(A0)
 ROXL.W -(A0)
 ROXL.W -(A0)
 ROXL.W -(A0)
 ROXL.W -(A0)
 ROXL.W -(A0)
 ROXL.W -(A0)
 ROXL.W -(A0)
 ROXL.W -(A0)
 ROXL.W -(A0)
 ROXL.W -(A0)
 ADD.W D0,A0
 DBRA D1,SCROL2

 SUB.W #10*160,A6
 MOVE.L A6,A1
SCPEK3:   ;PEK!
 LEA WOBTAB,A2
 LEA BUFF,A0
 MOVEQ #0,D3
 MOVEQ #4,D2
 MOVEQ #19,D0
SCROL1:
 MOVE.L A6,A1
 ADD.W (A2)+,A1
 ADD.W D2,A1
 MOVE.B D3,-320(A1)
 MOVE.B D3,-160(A1)
 MOVE.B (A0),(A1)
 MOVE.B 42(A0),160(A1)
 MOVE.B 84(A0),320(A1)
 MOVE.B 126(A0),480(A1)
 MOVE.B 168(A0),640(A1)
 MOVE.B 210(A0),800(A1)
 MOVE.B 252(A0),960(A1)
 MOVE.B 294(A0),1120(A1)
 MOVE.B 336(A0),1280(A1)
 MOVE.B 378(A0),1440(A1)
 MOVE.B 420(A0),1600(A1)
 MOVE.B 462(A0),1760(A1)
 MOVE.B 504(A0),1920(A1)
 MOVE.B 546(A0),2080(A1)
 MOVE.B 588(A0),2240(A1)
 MOVE.B 630(A0),2400(A1)
 MOVE.B D3,2560(A1)
 MOVE.B D3,2720(A1)
 ADDQ.W #1,D2
 MOVE.L A6,A1
 ADD.W (A2)+,A1
 ADD.W D2,A1
 ADDQ.W #1,A0
 MOVE.B D3,-320(A1)
 MOVE.B D3,-160(A1)
 MOVE.B (A0),(A1)
 MOVE.B 42(A0),160(A1)
 MOVE.B 84(A0),320(A1)
 MOVE.B 126(A0),480(A1)
 MOVE.B 168(A0),640(A1)
 MOVE.B 210(A0),800(A1)
 MOVE.B 252(A0),960(A1)
 MOVE.B 294(A0),1120(A1)
 MOVE.B 336(A0),1280(A1)
 MOVE.B 378(A0),1440(A1)
 MOVE.B 420(A0),1600(A1)
 MOVE.B 462(A0),1760(A1)
 MOVE.B 504(A0),1920(A1)
 MOVE.B 546(A0),2080(A1)
 MOVE.B 588(A0),2240(A1)
 MOVE.B 630(A0),2400(A1)
 MOVE.B D3,2560(A1)
 MOVE.B D3,2720(A1)
 ADDQ.W #1,A0
 ADDQ.W #7,D2
 DBRA D0,SCROL1

 CMP.L #WOBTAB+1040,SCPEK3+2
 BNE SCROL5
 MOVE.L #WOBTAB-2,SCPEK3+2
SCROL5:
 ADDQ.L #2,SCPEK3+2
 RTS


ROTATEFF:
 LEA GRAF,A2
 LEA GRAF+4800,A3

 MOVEQ #14,D2
ROT3:
 MOVE.L A2,A0
 MOVE.L A3,A1
 MOVE.W #4800/4,D0
 BSR COPY
 ADD.W #4800,A2
 ADD.W #4800,A3
 MOVE.L A2,A0
 MOVE.W #149,D1
ROT2:
 MOVE.L A0,A1
 MOVEQ #7,D0
 MOVE #0,CCR
ROT1:
 ROXR.W (A1)+
 ROXR.W (A1)+
 DBRA D0,ROT1
 ADD.W #32,A0
 DBRA D1,ROT2
 DBRA D2,ROT3
 RTS

INITRAST:
 MOVE.L $120,SPAR2
 MOVE.B $FFFA09,SPAR4
 MOVE.B $FFFA07,SPAR4+1
 MOVE.B $FFFA13,SPAR4+2
 LEA COLTAB,A5
 MOVE.L #RASTER1,$120
 ORI.B #1,$FFFA07
 ORI.B #1,$FFFA13
 RTS

REMOVERAST:
 MOVE.B SPAR4,$FFFA09
 MOVE.B SPAR4+1,$FFFA07
 MOVE.B SPAR4+2,$FFFA13
 MOVE.L SPAR2,$120
 RTS

RASTER1:
 MOVE.W (A5)+,$FF8240
 BCLR #0,$FFFA0F
 RTE
SPAR1: DC.L 0
SPAR2: DC.L 0
SPAR3: DC.L 0
SPAR4: DC.L 0
SPAR5: DC.W 0
COLTAB:
 DC.W  $111,$222,$333,$444
 DC.W  $555,$666,$777,$777,$667,$667,$557,$557,$447,$447,$337
 DC.W  $337,$227,$227,$117,$117,$007
 DC.W  $006,$005,$004,$003,$002,$001,$000,$000,$001,$002,$003,$004,$005
 DC.W  $006,$007,$107,$107,$207,$207,$307,$307,$407,$407
 DC.W  $507,$507,$607,$607,$707,$606
 DC.W  $505,$404,$303,$202,$101,$000,$000,$101,$202,$303,$404,$505,$606
 DC.W  $707,$706,$706,$705,$705,$704,$704,$703,$703,$702
 DC.W  $702,$701,$701,$700,$600,$500
 DC.W  $400,$300,$200,$100,$000,$000,$100,$200,$300,$400,$500,$600,$700
 DC.W  $710,$710,$720,$720,$730,$730,$740,$740,$750,$750
 DC.W  $760,$760,$770,$660,$550,$440
 DC.W  $330,$220,$110,$000,$000,$110,$220,$330,$440,$550,$660,$770,$670
 DC.W  $670,$570
 DC.W  $570,$470,$470,$370,$370,$270,$270,$170,$170,$070
 DC.W  $060,$050,$040,$030
 DC.W  $020,$010,$000,$000,$010,$020,$030,$040,$050,$060,$070,$071,$071
 DC.W  $072,$072,$073
 DC.W  $073,$074,$074,$075,$075,$076,$076,$077,$066,$055
 DC.W  $044,$033,$022
 DC.W  $011,$000,$000,$011,$022,$033,$044,$055,$066,$077,$177,$177,$277
 DC.W  $277,$377,$377,$477,$477,$577,$577,$677,$677,$777
 DC.W  $666,$555,$444,$333,$222,$111
 DS.W  4,$000

SPAR: DS.L 10

PAL:
 DC.W $000,$117,$770,$006,$040,$006,$040,$006
 DC.W $000,$117,$000,$115,$040,$006,$040,$006
OPAL:
 DS.W 16,0
OVBL:
 DC.L 0

MOUSE:
 DC.B $12,$1A,$14,8

L=160
WOBTAB:
 IBYTES BIGSIN.DAT
 IBYTES BIGSIN.DAT,90

SWEDESIN:
 IBYTES BIGSWEDE.SIN
SWEDEND:

SCREENS:
 DC.L SCR1,SCR

CLOCK:
 DC.W 0

ASCCI:
 DC.B "FLEXIBLE FRONT PRESENTS THE 'BIG FF' SCREEN. PROGRAM, GRAPHICS, SINE TABLES "
 DC.B "AND IDEA BY STALIN. THE DAVID WHITTAKER MUSIC IS RIPPED FROM THE FRENCH "
 DC.B "GAME 'ZOMBIE'."
 DC.B " MEGA GREETINGS TO THE FOLLOWING: "
 DC.B "THE CAREBEARS (YOUR DEMO WILL WRITE HISTORY!), THE EXCEPTIONS (MAD MAX MUSIC IS "
 DC.B "STILL THE BEST!), THE REPLICANTS (HI COBRA! LOOKING FORWARD TO SEE THE REPLICANTS "
 DC.B "DEMO), PHALANX (NICE DEMO. HI BLACK!), OMEGA, SYNC (GREAT DEMO!), "
 DC.B "LEGEND (THANKS FOR THE GRAFIX EARTHLIGHT), AUTOMATION (BRILL CD'S), THE BLADE "
 DC.B "RUNNERS (HI GIGABYTE CREW! YOU ARE THE ONLY TBR GROUP WHO HAVE GREETED US!)"
 DC.B ", DELTA FORCE, LEVEL 16. "
 DC.B "NORMAL GREETINGS TO: MCA, APA, THE LIGHT TEAM, THE BLACK CATS, EQUINOX"
 DC.B ", BARBARIANS. "
 DC.B "ALSO A HELLO TO THE FOLLOWING: MR MAGOO, BEAR OF BLOCKBUSTERS, ST MABUSE, SOA. "
 DC.B "                ARE YOU STILL WITH ME?     DAMN! THAT MEANS I HAVE TO COME UP WITH "
 DC.B "SOMETHING MORE TO WRITE! I WAS HOPING THAT YOU WOULD GIVE UP READING THIS "
 DC.B "SCROLLER IF I PUT GREETINGS FIRST, BUT OBVIOUSLY MY ATTEMPT FAILED. "
 DC.B "HMMM... WHAT MORE CAN I WRITE TO BORE YOU TO DEATH?    HE, HE,(SADISTIC SMILE) "
 DC.B "I KNOW: SYNTH RECORDS! WHEN I WRITE THIS (890510) THERE IS STILL NO SIGN OF "
 DC.B "THE FRONT LINE ASSEMBLY LP WHICH SHOULD HAVE BEEN RELEASED MONTHS AGO, ALTHOUGH "
 DC.B "RUMOURS SAY THAT IT HAS BEEN ALREADY RELEASED IN ENGLAND. THE 12' SINGEL DIGITAL "
 DC.B "TENSION DEMENTIA IS STILL THE BEST I'VE HEARD WITH THIS GROUP. (AND I'VE HEARD "
 DC.B "A LOT!) DO YOU KNOW THAT FRONTLINE MEMBERS BILL LEEB AND MICHAEL BALCH HAVE ALSO "
 DC.B "MADE 2 'DELERIUM' ALBUMS WITH SLIGHTLY SOFTER MUSIC, BUT STILL BRILLIANT. "
 DC.B "I'VE RECENTLY DISCOVERED THAT THE GERMAN INDUSTRIAL NOISE GROUP 'EINSTURZENDE "
 DC.B "NEU BAUTEN' ARE REALLY GOOD! EARLIER I THOUGHT THAT THEY MADE ONLY RANDOM "
 DC.B "CRAZY NOISES. WELL, SOMETIMES THEY DO, BUT IT'S INTELLIGENT CRAZY NOISE! "
 DC.B "I DON'T ONLY LISTEN TO SYNTH MUSIC, SO HERE'S A LIST OF SOME MORE NORMAL RECORDS "
 DC.B "WHO ARE ALSO WORTH OWNING... THE CURE: DISINTEGRATION (REALLY, REALLY, GOOD "
 DC.B "DEPRESSIVE GUITAR BASED MUSIC), NEW ORDER: TECHNIQUE (NEW ORDER ARE "
 DC.B "AS GOOD AS ALWAYS, BUT MAYBE THIS RECORD IS A BIT SOFTER THAN THEIR PREVIOUS) "
 DC.B ", SIOUXSIE AND THE BANSHEES: PEEPSHOW (OLD, BUT STILL GOOD)... "
 DC.B "   WELL THAT WAS IT ABOUT MUSIC, AND YOU ARE STILL READING! EITHER YOU HAVE THE "
 DC.B "SAME MUSIC TASTE AS I AND CCK (HIGHLY UNLIKELY), OR YOU ARE DETERMINED TO READ "
 DC.B "THIS SCROLLER TO IT'S END, AND HOPING THAT I WILL WRITE SOMETHING MORE "
 DC.B "INTERESTING. HATE TO DISAPPOINT YOU, BUT I WON'T!   OK, THEN, HERE IS A CONTACT "
 DC.B "ADRESS, IF THAT CAN BE CONSIDERED AS 'INTERESTING'. WRITE TO: "
 DC.B "FLEXIBLE FRONT,    C/O VILLE KRUMLINDE,    TORSVIKSSVANGEN 17, "
 DC.B "   S-181 34 LIDINGO,    SWEDEN. WE'RE LOOKING FORWARD TO HEAR FROM YOU! "
 DC.B "OVER TO ANOTHER SUBJECT, GFA BASIC. IT IS GOOD FOR MANY THINGS, MAKING SINE "
 DC.B "TABLES FOR INSTANCE, BUT (LISTEN CAREFULLY!) IT WAS NOT MADE FOR WRITING DEMOS "
 DC.B "WITH SO, IF YOU WANT TO MAKE DEMOS, LEARN SOME MACHINE CODE. YOU MIGHT IMPRESS "
 DC.B "SOME LAMERS WITH A GFA DEMO, BUT NOONE ELSE. OK THEN, ONE GFA DEMO IMPRESSES "
 DC.B "ME TOO, THE PREDATORS DEMO, BUT ALL THESE 'ONE DEGAS PICTURE AND A SAMPLE AND "
 DC.B "MAYBE ONE COLOR CYKLE' STUFF MAKES ME WANT TO PUKE. MACHINE CODE IS NOT THAT "
 DC.B "DIFFICULT, BUY A BOOK NOW AND START READING. (AN MC BOOK SILLY, NOT BIGGLES!) "
 DC.B "OK. NOW I HAVE WRITTEN GREETINGS, I HAVE CHATTED A LITTLE ABOUT MUSIC, AND "
 DC.B "I HAVE INSULTED ALL THE GFA BASIC PROGRAMMERS. WHAT IS THERE MORE TO SAY? "
 DC.B "A LOT, PROBABLY, BUT I HAVEN'T GOT MORE TIME NOW SO I HAVE TO SAY GOOD BYE. "
 DC.B "HOPEFULLY, IF WE GET SOME IDEAS AND SOME SPARE TIME, THEN YOU WILL SEE "
 DC.B "MORE DEMO SCREENS FROM US IN THE FUTURE, WHETHER YOU LIKE IT OR NOT! "
 DC.B "THIS SCROLLER WAS AS USUAL WRITTEN BY THE NEVER-TIRING STALIN. "
 DC.B "BYE, EVERYBODY!                              "
 DC.B 0
 ALIGN.W

MUSIC:
 IBYTES ZOMBIE.MUS
 ALIGN.W

BIGSWEDE:
 IBYTES BIGSWEDE.DAT

FONT:
 IBYTES SMALLFNT.DAT

BIGYSIN:
 IBYTES BIGYSIN.DAT
 DC.W 0

BIGXSIN:
 IBYTES BIGXSIN.DAT
 ALIGN.W

GRAF:
 IBYTES BIFF.PIC

 END

 