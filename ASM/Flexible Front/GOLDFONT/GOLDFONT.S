;***   SOURCE FOR 'A-WHOLE-LOT' SCREEN   ***
;*** WRITTEN BY STALIN OF FLEXIBLE FRONT ***
;***     FOR THE WHATTAHECK DEMO-89      ***
;***    RUNS WITH ASSEMPRO ASSEMBLER     ***

SCR=$F8000             ;$78000 FOR 520
SCR1=$F0000-(30*256)   ;$70000 FOR 520

 BRA START
BUFF:
 DS.B 3000,0

START:
 CLR.L -(SP)     ;SV MODE
 MOVE.W #$20,-(SP)
 TRAP #1
 ADDQ.L #6,SP
 MOVE.L D0,SPAR+4

 MOVE.W #5,-(SP)
 MOVE.W #26,-(SP)
 TRAP #14
 ADDQ.L #4,SP

 PEA MOUSE
 MOVE.W #1,-(SP)
 MOVE.W #25,-(SP)
 TRAP #14
 ADDQ.L #8,SP

 CLR.W -(SP)     ;SETSCR
 PEA SCR
 PEA SCR
 MOVE.W #5,-(SP)
 TRAP #14
 ADD.L #12,SP

 LEA ZOOMZ,A0         ;DEBUG SINUS
NONO:
 CMP.W #$4800,(A0)+
 BNE NONO
 MOVE.W #$3C00,-2(A0)

 BSR FIXFONT

 LEA $FF8240,A0
 LEA OPAL,A1
 MOVEQ #7,D0
 BSR COPY
 LEA PAL,A0
 LEA $FF8240,A1
 MOVEQ #7,D0
 BSR COPY

 LEA SCR1,A0
 MOVE.W #15999,D0
CLS:
 CLR.L (A0)+
 DBRA D0,CLS

 MOVEQ #0,D0
 JSR MUSIC
 MOVE.W #$2700,SR
 MOVE.L $70,OVBL
 MOVE.L #VBL,$70
 MOVE.W #$2300,SR

WAIT1:
 CMP.W #500,CLOCK
 BNE WAIT1
 MOVE.L #2,ZZ3+2  ;START Y BENDING OF FLEXIBLE

DEADLOP:
 CMP.B #$66,$FFFC02
 BNE DEADLOP

EXIT:
 MOVE.W #$2700,SR
 MOVE.L OVBL,$70
 MOVE.W #$2300,SR

 MOVE.W #1,-(SP)
 PEA SCR
 PEA SCR
 MOVE.W #5,-(SP)
 TRAP #14
 ADD.L #12,SP

 LEA OPAL,A0
 LEA $FF8240,A1
 MOVEQ #7,D0
 BSR COPY

 MOVE.W #5,-(SP)
 MOVE.W #27,-(SP)
 TRAP #14
 ADDQ.L #4,SP

 MOVE.L SPAR+4,-(SP)
 MOVE.W #$20,-(SP)
 TRAP #1
 ADDQ.L #6,SP

 PEA MOUSE+2
 MOVE.W #1,-(SP)
 MOVE.W #25,-(SP)
 TRAP #14
 ADDQ.L #8,SP
 ILLEGAL
COPY:
 MOVE.L (A0)+,(A1)+
 DBRA D0,COPY
 RTS

PICC1:
 LEA CLTAB1,A5
 LEA SCR1,A6
 JMP PIBACK

VBL:
 MOVE.W #$2700,SR
 MOVE.W #$700,$FF8240+16
 CMP.L #SCR,A6
 BEQ PICC1
 LEA CLTAB2,A5
 LEA SCR,A6
PIBACK:
 MOVE.L A6,SCREENS

 MOVE.L A5,FRONTER+2
 JSR CLSTARS         ;CLEAR OLD STARS
 JSR SCROLL          ;DRAW GOLD SCROLLER
 JSR ZOOM            ;DRAW FF SPRITES
 JSR FRONTER         ;DRAW 'FRONT' SPRITES
 MOVE.W #$447,$FF8240+16
 JSR ZOOMFLEX        ;DRAW 'FLEXIBLE' LOGO
 JSR SCROLL2         ;DRAW SMALL SCROLLER
 JSR MUSIC+$14C
 JSR USWOB           ;DRAW STALIN AND CCK
 JSR STARS           ;DRAW STARS

 MOVE.B SCREENS+1,$FF8201
 MOVE.B SCREENS+2,$FF8203
 ADDQ.W #1,CLOCK
 MOVE.W #$2300,SR
 RTE


USWOB:
 LEA USGRAF,A0
USPEK:
 LEA USTAB,A3
 CMP.L #USTAB+56,A3
 BEQ USSPEC
 CMP.L #SCR,A6
 BEQ USBAC
 ADDQ.L #2,USPEK+2
USBAC:
 MOVE.L A6,A1
 ADD.W #160*150+16,A1

 MOVEQ #15,D6
US2:
 MOVEM.L (A0)+,D0-D5/A4-A5
 MOVE.W (A3)+,D7
US1:
 MOVEM.L D0-D3,(A1)
 MOVEM.L D4-D5/A4-A5,112(A1)
 ADD.W #160,A1
 DBRA D7,US1
 DBRA D6,US2

 MOVEQ #0,D0
 MOVE.W D0,(A1)
 MOVE.W D0,8(A1)
 MOVE.W D0,112(A1)
 MOVE.W D0,120(A1)
 MOVE.W D0,160(A1)
 MOVE.W D0,168(A1)
 MOVE.W D0,160+112(A1)
 MOVE.W D0,160+120(A1)
 RTS
USSPEC:
 MOVE.L #USTAB,USPEK+2
 JMP USBAC
USTAB:
 DS.W 20,0
 DC.W 1,1,1,1,1,1,1,1
 DS.W 20,0

SCROLL2:  ;PEK!
 MOVE.W #15,D0
 BNE SC21

 MOVE.W #15,SCROLL2+2
SC2PEK1:
 MOVE.B ASCCI2,D0
 BNE SC22
 MOVE.B ASCCI2,D0
 MOVE.L #ASCCI2,SC2PEK1+2
SC22:
 ADDQ.L #1,SC2PEK1+2
 SUB.B #32,D0
 LSL.W #5,D0
 LEA SMALLFONT,A0
 ADD.W D0,A0
 LEA BUFF+1040,A1
 MOVEM.W (A0)+,D0-D7
 MOVE.W D0,(A1)
 MOVE.W D1,42(A1)
 MOVE.W D2,84(A1)
 MOVE.W D3,126(A1)
 MOVE.W D4,168(A1)
 MOVE.W D5,210(A1)
 MOVE.W D6,252(A1)
 MOVE.W D7,294(A1)
 MOVEM.W (A0)+,D0-D7
 MOVE.W D0,336(A1)
 MOVE.W D1,378(A1)
 MOVE.W D2,420(A1)
 MOVE.W D3,462(A1)
 MOVE.W D4,504(A1)
 MOVE.W D5,546(A1)
 MOVE.W D6,588(A1)
 MOVE.W D7,630(A1)

SC21:
 SUBQ.W #1,SCROLL2+2

 LEA BUFF+1042,A0
 MOVEQ #84,D0
 MOVEQ #15,D1
SCROL2:
 MOVE #0,CCR
 ROXL.W -(A0)
 ROXL.W -(A0)
 ROXL.W -(A0)
 ROXL.W -(A0)
 ROXL.W -(A0)
 ROXL.W -(A0)
 ROXL.W -(A0)
 ROXL.W -(A0)
 ROXL.W -(A0)
 ROXL.W -(A0)
 ROXL.W -(A0)
 ROXL.W -(A0)
 ROXL.W -(A0)
 ROXL.W -(A0)
 ROXL.W -(A0)
 ROXL.W -(A0)
 ROXL.W -(A0)
 ROXL.W -(A0)
 ROXL.W -(A0)
 ROXL.W -(A0)
 ROXL.W -(A0)
 ADD.W D0,A0
 DBRA D1,SCROL2

 LEA BUFF+1028,A0
 MOVEQ #42+18,D0
 MOVEQ #1,D3
 MOVEQ #15,D1
SCROL3:
 MOVE #0,CCR
 MOVE.W (A0),D2
 AND.W D3,D2
 ROXL.W (A0)
 OR.W D2,(A0)
 ROXL.W -(A0)
 ROXL.W -(A0)
 ROXL.W -(A0)
 ROXL.W -(A0)
 ROXL.W -(A0)
 ROXL.W -(A0)
 ROXL.W -(A0)
 ROXL.W -(A0)
 ROXL.W -(A0)
 ADD.W D0,A0
 DBRA D1,SCROL3

 LEA BUFF+1022,A0
 MOVEQ #42+10,D0
 MOVEQ #1,D3
 MOVEQ #15,D1
SCROL4:
 MOVE #0,CCR
 MOVE.W (A0),D2
 AND.W D3,D2
 ROXL.W (A0)
 OR.W D2,(A0)
 ROXL.W -(A0)
 ROXL.W -(A0)
 ROXL.W -(A0)
 ROXL.W -(A0)
 ROXL.W -(A0)
 ADD.W D0,A0
 DBRA D1,SCROL4

 ADD.W #70*160,A6

 LEA SCWOB,A2
 LEA BUFF+1000,A0
 MOVEQ #6,D2
 MOVEQ #19,D0
SCROL1:
 MOVE.L A6,A1
 ADD.W (A2)+,A1
 ADD.W D2,A1
 MOVE.B (A0),(A1)
 MOVE.B 42(A0),160(A1)
 MOVE.B 84(A0),320(A1)
 MOVE.B 126(A0),480(A1)
 MOVE.B 168(A0),640(A1)
 MOVE.B 210(A0),800(A1)
 MOVE.B 252(A0),960(A1)
 MOVE.B 294(A0),1120(A1)
 MOVE.B 336(A0),1280(A1)
 MOVE.B 378(A0),1440(A1)
 MOVE.B 420(A0),1600(A1)
 MOVE.B 462(A0),1760(A1)
 MOVE.B 504(A0),1920(A1)
 MOVE.B 546(A0),2080(A1)
 MOVE.B 588(A0),2240(A1)
 MOVE.B 630(A0),2400(A1)
 ADDQ.W #1,D2
 MOVE.L A6,A1
 ADD.W (A2)+,A1
 ADD.W D2,A1
 ADDQ.W #1,A0
 MOVE.B (A0),(A1)
 MOVE.B 42(A0),160(A1)
 MOVE.B 84(A0),320(A1)
 MOVE.B 126(A0),480(A1)
 MOVE.B 168(A0),640(A1)
 MOVE.B 210(A0),800(A1)
 MOVE.B 252(A0),960(A1)
 MOVE.B 294(A0),1120(A1)
 MOVE.B 336(A0),1280(A1)
 MOVE.B 378(A0),1440(A1)
 MOVE.B 420(A0),1600(A1)
 MOVE.B 462(A0),1760(A1)
 MOVE.B 504(A0),1920(A1)
 MOVE.B 546(A0),2080(A1)
 MOVE.B 588(A0),2240(A1)
 MOVE.B 630(A0),2400(A1)
 ADDQ.W #1,A0
 ADDQ.W #7,D2
 DBRA D0,SCROL1

 SUB.W #70*160,A6
 RTS

CLSTARS:
 MOVEQ #0,D1
 MOVEQ #0,D7
 MOVEQ #0,D6
 LEA STCL1,A0
 CMP.L #SCR,A6
 BNE CLST1
 ADD.W #250*4,A0
CLST1:
 MOVE.L A0,STARS+2

ANPEK1:
 LEA STARTAB,A3
CLST2:
 ADDQ.W #1,D6
 MOVE.L (A0)+,A1

CLST3:
N@=0
 REPEAT
  MOVE.W (A3)+,D0
  BEQ.S CLST4
  MOVE.W D1,(A1,D0.W)
  ADDQ.W #1,D7
N@=N@+1
 UNTILEQ N@,10
 JMP CLST3
CLST4:
 CMP.W #$FFFF,(A3)
 BNE CLST2

 MOVEQ #32,D1
 SUB.W D6,D1
 ADD.W D1,D1
 MOVEQ #70,D0  ;WAIT TO EQUAL RASTER TIME
 SUB.W D7,D0
 ADD.W D1,D0
PLOP:
 DC.W 7,$4E71
 DBRA D0,PLOP
 RTS


STARS:   ;PEK!
 LEA STCL1,A5

 ADDQ.W #4,A6
ANPEK2:
 LEA STARTAB,A3
STPEK1:
 LEA STARX,A2
STPEK2:
 LEA STARY,A4

ST2:
 MOVE.L A6,A1
 MOVE.W (A2)+,D0  ;X COORD
 MOVE.W D0,D1
 AND.B #%11110000,D0
 LSR.W #1,D0
 ADD.W D0,A1
 ADD.W (A4)+,A1   ;Y COORD
 MOVE.L A1,(A5)+
 AND.W #%0000000000001111,D1
 LSL.W #1,D1
 LEA STAROFF,A0
 MOVE.W (A0,D1.W),D2  ;D2=STAR

ST3:
 MOVE.W (A3)+,D0
 BEQ ST1

 OR.W D2,(A1,D0.W)
 JMP ST3

ST1:
 CMP.W #$FFFF,(A3)
 BNE ST2
 SUBQ.W #4,A6

 TST.W (A4)
 BNE ST5
 MOVE.L #STARY-2,STPEK2+2
 MOVE.L #STARX-2,STPEK1+2

ANPEK3:
 MOVE.L ANTAB,A0
 CMP.L #0,A0
 BNE ST6
 MOVE.L #ANTAB,ANPEK3+2
 LEA STARTAB,A0
ST6:
 ADDQ.L #4,ANPEK3+2
 MOVE.L A0,ANPEK1+2
 MOVE.L A0,ANPEK2+2

ST5:
 ADDQ.L #2,STPEK2+2
 ADDQ.L #2,STPEK1+2
 RTS
STAROFF:
 DC.W 32768,16384,8192,4096,2048,1024,512,256,128,64,32,16,8,4,2,1
ANTAB:
 DC.L STARTAB6,STARTAB1,STARTAB2,STARTAB,STARTAB6,STARTAB2,STARTAB3
 DC.L STARTAB,STARTAB6,STARTAB1,STARTAB2,STARTAB5,STARTAB,STARTAB6
 DC.L STARTAB1,STARTAB2,STARTAB4,STARTAB,STARTAB6,STARTAB1,STARTAB2
 DC.L STARTAB7
 DC.L 0

FRONTER:  ;PEK!
 LEA CLTAB1,A3
 ADD.W #6*4,A3

 LEA FRONT,A0
FPEK1:
 LEA FRONTSIN,A2
 MOVEQ #80,D6
 MOVEQ #4,D7
F1:
 MOVE.L A6,A1
 ADD.W (A2),A1
 ADD.W #8,A2
 ADD.W D6,A1
 MOVE.L A1,(A3)+
L@=0
N@=0
 REPEAT
  MOVEM.W (A0)+,D0-D5
  MOVE.W D0,L@(A1)
  MOVE.W D1,2+L@(A1)
  MOVE.W D2,4+L@(A1)
  MOVE.W D3,8+L@(A1)
  MOVE.W D4,10+L@(A1)
  MOVE.W D5,12+L@(A1)
L@=L@+160
N@=N@+1
 UNTILEQ N@,32
 ADD.W #16,D6
 DBRA D7,F1

 CMP.W #$FFFF,(A2)
 BNE F2
 MOVE.L #FRONTSIN-2,FPEK1+2
F2:
 ADDQ.L #2,FPEK1+2
 RTS

ZOOMFLEX:
 MOVE.L A6,A1   ;CLEAR OLD
 ADDQ.W #6,A1
 MOVEQ #0,D0
 MOVE.W #160,D2
 MOVEQ #15,D1
ZZ4:
 MOVE.W D0,(A1)
 MOVE.W D0,8(A1)
 MOVE.W D0,16(A1)
 MOVE.W D0,24(A1)
 MOVE.W D0,32(A1)
 MOVE.W D0,40(A1)
 MOVE.W D0,48(A1)
 MOVE.W D0,56(A1)
 MOVE.W D0,64(A1)
 MOVE.W D0,72(A1)
 ADD.W D2,A1
 DBRA D1,ZZ4

 ADD.W #10*160,A1
 MOVEQ #16,D1
ZZ5:
 MOVE.W D0,(A1)
 MOVE.W D0,8(A1)
 MOVE.W D0,16(A1)
 MOVE.W D0,24(A1)
 MOVE.W D0,32(A1)
 MOVE.W D0,40(A1)
 MOVE.W D0,48(A1)
 MOVE.W D0,56(A1)
 MOVE.W D0,64(A1)
 MOVE.W D0,72(A1)
 ADD.W D2,A1
 DBRA D1,ZZ5

 LEA BUFF,A1
 LEA ZOOM2,A2
ZZPEK1:
 LEA ZADD,A3

L@=20
N@=0
 REPEAT
  MOVE.L A2,A0
  ADD.W (A3)+,A0
  MOVEM.L L@-20(A0),D0-D4
  MOVEM.L D0-D4,L@(A1)
N@=N@+1
L@=L@+20
 UNTILEQ N@,27

 CMP.W #$FFFF,(A3)
 BNE ZZ1
 MOVE.L #ZADD-2,ZZPEK1+2
ZZ1:
 ADDQ.L #2,ZZPEK1+2

 MOVE.L A6,A3
 ADDQ.W #6,A3
ZZPEK2:
 LEA ZYADD,A2
 LEA BUFF,A0
 MOVEQ #0,D1

 MOVEQ #9,D0
ZZ2:
 MOVE.L A3,A1
 ADD.W (A2)+,A1
 ADD.W D1,A1
K@=0
L@=0
N@=0
 REPEAT
 MOVE.B L@(A0),K@(A1)
N@=N@+1
L@=L@+20
K@=K@+160
 UNTILEQ N@,27

 ADDQ.W #1,D1
 ADDQ.W #1,A0
 MOVE.L A3,A1
 ADD.W (A2)+,A1
 ADD.W D1,A1
K@=0
L@=0
N@=0
 REPEAT
 MOVE.B L@(A0),K@(A1)
N@=N@+1
L@=L@+20
K@=K@+160
 UNTILEQ N@,27
 ADDQ.W #1,A0
 ADDQ.W #7,D1
 DBRA D0,ZZ2

 CMP.W #$FFFF,(A2)
 BNE ZZ3
 MOVE.L #ZYADD-2,ZZPEK2+2
ZZ3:
 ADD.L #0,ZZPEK2+2
 RTS


ZOOM:
 LEA ZCL1,A4
 CMP.L #SCR,A6
 BNE Z3
 ADD.W #16,A4
Z3:
 MOVE.L A4,A1
 MOVEQ #0,D0
 MOVEQ #3,D1  ;NR OF SPRITES-1
Z4:
 MOVE.L (A1)+,A0

L@=0
N@=0
 REPEAT
  MOVE.W D0,L@(A0)
  MOVE.W D0,8+L@(A0)
  MOVE.W D0,16+L@(A0)
  MOVE.W D0,160+L@(A0)
  MOVE.W D0,168+L@(A0)
  MOVE.W D0,176+L@(A0)
L@=L@+320
N@=N@+1
 UNTILEQ N@,16
 DBRA D1,Z4

 MOVE.W #$070,$FF8240+16

 LEA ZOOMER,A5
 MOVEQ #3,D6
Z6:
 MOVE.L (A5),A3
 CMP.L #ZOOMZSLUT,A3
 BNE Z2
 LEA ZOOMZ,A3
Z2:
 LEA ZOOMGRAF,A0
 ADD.W (A3)+,A0   ;ZOOM LEVEL
 MOVE.L A3,(A5)+

 MOVE.L A6,A1
 ADDQ.W #6,A1

 MOVE.L (A5),A3
 CMP.L #ZOOMYSLUT,A3
 BNE Z7
 LEA ZOOMY,A3
Z7:
 ADD.W (A3)+,A1   ;Y OFFSET
 MOVE.L A3,(A5)+
 MOVE.L (A5),A3
 CMP.L #ZOOMXSLUT,A3
 BNE Z8
 LEA ZOOMX,A3
Z8:
 MOVE.W (A3)+,D0  ;X COORD
 MOVE.L A3,(A5)+
 MOVE.W D0,D1
 AND.B #%11110000,D0
 LSR.W #1,D0
 ADD.W D0,A1
 MOVE.L A1,(A4)+
 AND.W #%0000000000001111,D1
 LSL.W #1,D1
 LEA ZOOMOFF,A2
 ADD.W (A2,D1.W),A0
L@=0
N@=0
 REPEAT
  MOVEM.W (A0)+,D0-D5
  OR.W D0,L@(A1)
  OR.W D1,8+L@(A1)
  OR.W D2,16+L@(A1)
  OR.W D3,160+L@(A1)
  OR.W D4,168+L@(A1)
  OR.W D5,176+L@(A1)
L@=L@+320
N@=N@+1
 UNTILEQ N@,16
 DBRA D6,Z6

 RTS

ZOOMOFF:
 DC.W 0,192,192*2,192*3,192*4,192*5,192*6,192*7,192*8,192*9,192*10
 DC.W 192*11,192*12,192*13,192*14,192*15

SCROLL:
 MOVE.L A5,A0   ;CLEAR OLD LETTERS
 MOVEQ #0,D1
 MOVEQ #5,D2
SC2:
 MOVE.L (A0)+,A1
L@=0
N@=0
 REPEAT
  MOVE.L D1,L@(A1)
  MOVE.W D1,4+L@(A1)
  MOVE.L D1,8+L@(A1)
  MOVE.W D1,12+L@(A1)
  MOVE.L D1,16+L@(A1)
  MOVE.W D1,20+L@(A1)
L@=L@+160
N@=N@+1
 UNTILEQ N@,32
 DBRA D2,SC2

 MOVEQ #4,D2       ;CLEAR FRONT LOGO
SSC2:
 MOVE.L (A0)+,A1
L@=0
N@=0
 REPEAT
  MOVE.L D1,L@(A1)
  MOVE.W D1,4+L@(A1)
  MOVE.L D1,8+L@(A1)
  MOVE.W D1,12+L@(A1)
L@=L@+160
N@=N@+1
 UNTILEQ N@,32
 DBRA D2,SSC2

 LEA LETTAB,A4
 MOVE.L A6,A2
SCPEK1:
 ADD.W #0,A2

 MOVEQ #5,D1
SC1:
 MOVE.L A2,A1
 CLR.W D2

 LEA SCSIN,A3
 ADD.W (A4),A3
 MOVE.B (A3),D2
 BNE SC3
 CLR.W (A4)
 MOVE.B SCSIN,D2
SC3:
 ADDQ.W #1,(A4)+
 MOVE.W D2,D3
 AND.B #%11110000,D2
 LSR.W #1,D2
 ADD.W D2,A1
 MOVE.L A1,(A5)+
 AND.W #%0000000000001111,D3
 LSL.W #2,D3
 LEA SCOFF,A0
 MOVE.L (A0,D3.W),A0
 ADD.W (A4)+,A0
L@=0
N@=0
 REPEAT
  MOVE.L (A0)+,L@(A1)
  MOVE.W (A0)+,4+L@(A1)
  MOVE.L (A0)+,8+L@(A1)
  MOVE.W (A0)+,12+L@(A1)
  MOVE.L (A0)+,16+L@(A1)
  MOVE.W (A0)+,20+L@(A1)
L@=L@+160
N@=N@+1
 UNTILEQ N@,32
 ADD.W #40*160,A2
 DBRA D1,SC1

 CMP.W #-(40*160),SCPEK1+2
 BNE SC5
 MOVE.W #0,SCPEK1+2
 LEA LETTAB+4,A0
 MOVEM.L (A0),D0-D4
 SUBQ.W #4,A0
 MOVEM.L D0-D4,(A0)
 CLR.W D0
ASCPEK:
 MOVE.B ASCCI,D0
 BNE SC6
 MOVE.L #ASCCI,ASCPEK+2
 MOVEQ #64,D0
SC6:
 ADDQ.L #1,ASCPEK+2
 SUB.B #64,D0
 MOVE.W D0,D1
 LSL.W #8,D1  ;*512
 LSL.W #1,D1  ; -"-
 LSL.W #6,D0  ;*64
 ADD.W D1,D0
 MOVE.W D0,LETTAB+(5*4)+2
 MOVE.W LETTAB+(4*4),D0
 ADDQ.W #2,D0
 MOVE.W D0,LETTAB+(5*4)
SC5:
 SUB.W #320,SCPEK1+2
 RTS
O=15552
SCOFF:
 DC.L FONT,FONT+O,FONT+O*2,FONT+O*3,FONT+O*4,FONT+O*5,FONT+O*6,FONT+O*7
 DC.L FONT+O*8,FONT+O*9,FONT+O*10,FONT+O*11,FONT+O*12,FONT+O*13,FONT+O*14,FONT+O*15

FIXFONT:
 BSR FIXZOOM
 LEA FONT,A0
 LEA TEMP,A1
 MOVEQ #0,D1

 MOVEQ #26,D2
FIX2:
 MOVEQ #31,D0
FIX1:
 MOVE.L (A0)+,(A1)+
 MOVE.L (A0)+,(A1)+
 MOVE.L (A0)+,(A1)+
 MOVE.L D1,(A1)+
 MOVE.W D1,(A1)+
 DBRA D0,FIX1
 DBRA D2,FIX2

 LEA TEMP,A0
 LEA FONT,A1
 MOVE.W #8352,D0
 BSR COPY

 LEA FONT,A2       ;START ROTATING
 LEA FONT+15552,A3
 MOVEQ #14,D2
FIX5:
 MOVE.L A2,A0
 MOVE.L A3,A1
 MOVE.W #3888,D0
 BSR COPY
 MOVE.L A3,A0
 MOVE.W #27*32,D1
FIX4:
 MOVEQ #2,D0
FIX3:
 MOVE #0,CCR
 ROXR.W (A0)
 ROXR.W 6(A0)
 ROXR.W 12(A0)
 ADDQ.L #2,A0
 DBRA D0,FIX3
 ADD.W #12,A0
 DBRA D1,FIX4
 ADD.L #15552,A2
 ADD.L #15552,A3
 DBRA D2,FIX5

 LEA ASCCI,A0   ;FIX ASCIIS
ALOP:
 MOVE.B (A0)+,D0
 BEQ AFIN
 CMP.B #32,D0
 BNE ALOP
 MOVE.B #64,-1(A0)
 BRA ALOP
AFIN:
 RTS

FIXZOOM:      ;SETUP FF ZOOM GRAFIX
 LEA TEMP,A0
 LEA ZOOMGRAF,A1
 MOVEQ #0,D0
 MOVEQ #5,D2
FIXZ2:
 MOVEQ #31,D1
FIXZ1:
 MOVE.L (A0)+,(A1)+
 MOVE.W D0,(A1)+
 DBRA D1,FIXZ1
 ADD.W #192*15,A1
 DBRA D2,FIXZ2

 LEA ZOOMGRAF,A2
 LEA ZOOMGRAF+192,A3
 MOVEQ #5,D3
FIXZ5:
 MOVEQ #14,D2
FIXZ4:
 MOVE.L A2,A0
 MOVE.L A3,A1
 MOVEQ #192/4,D0
 BSR COPY
 MOVE.L A3,A0
 MOVEQ #31,D1
FIXZ3:
 ROXR.W (A0)
 ROXR.W 2(A0)
 ROXR.W 4(A0)
 ADDQ.W #6,A0
 DBRA D1,FIXZ3
 ADD.W #192,A3
 ADD.W #192,A2
 DBRA D2,FIXZ4
 ADD.W #192,A3
 ADD.W #192,A2
 DBRA D3,FIXZ5
 RTS

MUSIC:
 IBYTES SPEEDBAL.MUS
 ALIGN.W

SPAR: DS.L 8,0
SPAR1: DC.L 0
SPAR2: DC.L 0
SPAR3: DC.L 0
SPAR4: DC.L 0
SPAR5: DC.W 0
PAL:
 DC.W $000,$310,$420,$530,$640,$750,$760,$770
 DC.W $700,$310,$420,$530,$640,$750,$760,$770
OPAL:
 DS.W 16,0
OVBL:
 DC.L 0

MOUSE:
 DC.B $12,$1A,$14,8

SCREENS:
 DC.L SCR1,SCR

CLOCK:
 DC.W 0

LETTAB:
 DS.L 6,0

ASCCI:
 DC.B ' SINCE NOONE BOTHERS TO READ THIS SCROLLER THERE IS NO USE IN'
 DC.B ' WRITING SOMETHING SENSIBLE    A PERFECT PLACE FOR GREETINGS'
 DC.B '  UNFORTUNATELY I AM IN THE SAME SITUATION AS NICK OF TCB WHEN HE'
 DC.B ' WROTE THE THREE DEE SCROLLER IN UNION DEMO  NO SPECIAL CHARACTERS'
 DC.B '  NOT EVEN NUMBERS  JUST THE LETTERS A TO Z  ALL BECAUSE OF MEMORY'
 DC.B ' SHORTAGE  WHY WASNT ONE MEG STANDARD ON THE ST  QUESTIONS SURE LOOK'
 DC.B ' SILLY WITHOUT A QUESTION MARK  OK LETS GO ON ANYWAY WITH THE'
 DC.B ' GREETINGS WITHOUT THE COMMENTS'
 DC.B '   WE IN FLEXIBLE FRONT SEND OUR BINARY HELLOS TO THE THE FOLLOWING'
 DC.B ' THE CAREBEARS  THE EXCEPTIONS  THE REPLICANTS  PHALANX  LEGEND'
 DC.B '  OMEGA  SYNC  THE LIGHT TEAM  AUTOMATION  BLADE RUNNERS  TNT CREW  LEVEL SIXTEEN'
 DC.B '    ARE YOU ANGRY ON US BECAUSE WE FORGOT TO GREET YOU  THEN JUST'
 DC.B ' WRITE US A LETTER  CONTACT ADRESS'
 DC.B ' FLEXIBLE FRONT  C O VILLE KRUMLINDE  TORSVIKSSVANGEN ONE SEVEN'
 DC.B '  ONE EIGHT ONE THREE FOUR LIDINGO  SWEDEN  '
 DC.B '  IF QUESTIONS LOOK STUPID WITHOUT QUESTION MARKS THEN THATS NOTHING'
 DC.B ' COMPARED TO WHAT ADRESSES LOOK WITHOUT NUMBERS'
 DC.B '  CONGRATULATIONS YOU HAVE READ THE WHOLE SCROLLER  YOU HAVE NOT'
 DC.B ' IMPROVED YOUR IQ FACTOR BUT I DID SAY THAT I WOULDNT WRITE SOMETHING'
 DC.B ' SENSIBLE    END OF TEXT    '
 DC.B '          THIS IS YOUR ST SPEAKING  OH THAT STUPID STALIN'
 DC.B '  HE FORGOT TO MARK THE END OF HIS SCROLLTEXT WITH A ZERO BYTE'
 DC.B '  IF I WASNT SUCH A SMART COMPUTER I WOULD DRAW SOME REALLY'
 DC.B ' STRANGE CHARACTERS BY NOW  OK I WILL HAVE TO LOOP THE TEXT MYSELF        ZERO                '
 DC.B 0
 ALIGN.W

ASCCI2:
 DC.B ' FLEXIBLE FRONT PRESENTS THE GOLDSCROLLER SCREEN. '
 DC.B 'CODE AND SINEWAVES BY STALIN. GOLD FONT AND FLEXIBLE LOGO DRAWN BY EARTHLIGHT. '
 DC.B 'THE DAVE WHITTAKER MUSIC THAT YOU HAVE HEARD 10 BILLION TIMES BEFORE IS OF COURSE FROM SPEEDBALL. '
 DC.B 'WHEN I (STALIN) WRITES THIS, THERE ARE STILL A COUPLE OF DAYS BEFORE THE COPY PARTY BEGINS. '
 DS.B 20,' '
 DC.B 0
 ALIGN.W

CLTAB1:
 DS.L 6+5,SCR
CLTAB2:
 DS.L 6+5,SCR
ZCL1:
 DS.L 8,SCR
STCL1:
 DS.L 500,SCR

 DS.B 5000

SCSIN:
 DS.B 100,1
 IBYTES GFX.SIN
 DS.B 100,1
 DS.B 10,0
 ALIGN.W

ZOOMX:
 IBYTES ZOOMX.SIN
ZOOMXSLUT:

ZOOMY:
 IBYTES ZOOMY.SIN
ZOOMYSLUT:

ZOOMZ:
 IBYTES ZOOMZ.SIN
ZOOMZSLUT:

ZADD:
 DS.W 27,0
 IBYTES ZZOOM.SIN
 DS.W 27,0
 DC.W $FFFF

ZYADD:
 DS.W 30,0
 IBYTES ZZOOMY.SIN
 DS.W 30,0
 DC.W $FFFF

ZOOMER:
 DC.L ZOOMZ,ZOOMY,ZOOMX
 DC.L ZOOMZ+2,ZOOMY+10,ZOOMX+10
 DC.L ZOOMZ+4,ZOOMY+20,ZOOMX+20
 DC.L ZOOMZ+6,ZOOMY+30,ZOOMX+30

L=160
FRONTSIN:
 DS.W 40,0
 IBYTES FRONT.SIN
 DS.W 40,0
 DC.W $FFFF

STARY:
 IBYTES STARY.SIN
 DS.W 50,32000
 DC.W 0

STARX:
 IBYTES STARX.SIN
 DC.W 0

STARTAB:    ;'ST'
 DC.W L*1,L*5,L*9,L*13,L*17,L*21,L*33,L*37,0
 DC.W L*1,L*5,L*9,L*13,L*17,L*21,L*33,L*37,0
 DC.W L*1,L*5,L*17,L*21,L*25,L*29,L*33,L*37,0
 DC.W L*1,L*5,L*17,L*21,L*25,L*29,L*33,L*37,0
 DC.W 0
 DC.W 0
 DC.W L*1,L*5,0
 DC.W L*1,L*5,0
 DC.W L*1,L*5,L*9,L*13,L*17,L*21,L*25,L*29,L*33,L*37,0
 DC.W L*1,L*5,L*9,L*13,L*17,L*21,L*25,L*29,L*33,L*37,0
 DC.W L*1,L*5,0
 DC.W L*1,L*5,0
 DC.W $FFFF

STARTAB1:    ;ATARI LOGO
 DC.W L*47,0
 DC.W L*41,L*45,0
 DC.W L*37,L*41,L*45,0
 DC.W L*33,L*37,L*41,0
 DC.W L*1,L*5,L*9,L*13,L*17,L*21,L*25,L*29,L*33,L*37,0
 DC.W L*1,L*5,L*9,L*13,L*17,L*21,L*25,L*29,L*33,0
 DC.W 0
 DC.W L*1,L*5,L*9,L*13,L*17,L*21,L*25,L*29,L*33,L*37,L*41,L*45,0
 DC.W L*1,L*5,L*9,L*13,L*17,L*21,L*25,L*29,L*33,L*37,L*41,L*45,0
 DC.W 0
 DC.W L*1,L*5,L*9,L*13,L*17,L*21,L*25,L*29,L*33,0
 DC.W L*1,L*5,L*9,L*13,L*17,L*21,L*25,L*29,L*33,L*37,0
 DC.W L*33,L*37,L*41,0
 DC.W L*37,L*41,L*45,0
 DC.W L*41,L*45,0
 DC.W L*47,0
 DC.W $FFFF

STARTAB2:   ;'SWEDEN'
 DC.W L*1,L*5,L*9,L*17,0
 DC.W L*1,L*7,L*17,0
 DC.W L*1,L*7,L*13,L*17,0
 DC.W 0
 DC.W L*5,L*9,L*13,L*17,L*21,0
 DC.W L*21,0
 DC.W L*13,L*17,L*21,0
 DC.W L*21,0
 DC.W L*5,L*9,L*13,L*17,L*21,0
 DC.W 0
 DC.W L*9,L*13,L*17,L*21,L*25,0
 DC.W L*9,L*17,L*25,0
 DC.W L*9,L*25,0
 DC.W 0
 DC.W L*13,L*17,L*21,L*25,L*29,0
 DC.W L*13,L*29,0
 DC.W L*17,L*21,L*25,0
 DC.W 0
 DC.W L*17,L*21,L*25,L*29,L*33,0
 DC.W L*17,L*25,L*33,0
 DC.W L*17,L*33,0
 DC.W 0
 DC.W L*21,L*25,L*29,L*33,L*37,0
 DC.W L*21,0
 DC.W L*25,0
 DC.W L*21,L*25,L*29,L*33,L*37,0
 DC.W $FFFF

STARTAB3:   ;EINSTURZENDE GUBBE
 DC.W L*21,0
 DC.W L*5,L*17,L*33,L*37,0
 DC.W L*1,L*9,L*17,L*29,0
 DC.W L*1,L*5,L*13,L*17,L*21,L*25,L*29,0
 DC.W L*1,L*9,L*17,L*29,0
 DC.W L*5,L*17,L*33,L*37,0
 DC.W L*21,0
 DC.W $FFFF

STARTAB4:  ;ACID GUBBE
 DC.W L*9,L*13,L*17,L*21,L*25,L*29,0
 DC.W L*5,L*21,L*33,0
 DC.W L*1,L*13,L*25,L*37,0
 DC.W L*1,L*25,L*37,0
 DC.W L*1,L*25,L*37,0
 DC.W L*1,L*13,L*25,L*37,0
 DC.W L*5,L*21,L*33,0
 DC.W L*9,L*13,L*17,L*21,L*25,L*29,0
 DC.W $FFFF

STARTAB5:  ;NOT
 DC.W L*29,L*33,0
 DC.W L*25,L*29,L*33,L*37,0
 DC.W L*25,L*29,L*33,L*37,0
 DC.W L*1,L*5,L*9,L*13,L*17,L*21,L*25,L*29,L*33,0
 DC.W L*1,0
 DC.W L*5,0
 DC.W $FFFF

STARTAB6:  ;'FF'
 DC.W L*1,L*5,L*9,L*13,L*17,L*21,L*25,L*29,L*33,L*37,0
 DC.W L*1,L*5,L*9,L*13,L*17,L*21,L*25,L*29,L*33,L*37,0
 DC.W L*1,L*5,L*17,L*21,0
 DC.W L*1,L*5,L*17,L*21,0
 DC.W L*1,L*5,0
 DC.W L*1,L*5,0
 DC.W 0
 DC.W L*1,L*5,0
 DC.W L*1,L*5,0
 DC.W L*1,L*5,L*17,L*21,0
 DC.W L*1,L*5,L*17,L*21,0
 DC.W L*1,L*5,L*9,L*13,L*17,L*21,L*25,L*29,L*33,L*37,0
 DC.W L*1,L*5,L*9,L*13,L*17,L*21,L*25,L*29,L*33,L*37,0
 DC.W $FFFF

STARTAB7:  ;'242'
 DC.W L*5,L*21,0
 DC.W L*1,L*17,L*21,0
 DC.W L*1,L*13,L*21,0
 DC.W L*1,L*9,L*21,0
 DC.W L*5,L*21,0
 DC.W L*13,0
 DC.W L*9,L*13,0
 DC.W L*5,L*13,0
 DC.W L*1,L*5,L*9,L*13,L*17,L*21,0
 DC.W L*13,0
 DC.W L*5,L*21,0
 DC.W L*1,L*17,L*21,0
 DC.W L*1,L*13,L*21,0
 DC.W L*1,L*9,L*21,0
 DC.W L*5,L*21,0
 DC.W $FFFF

USGRAF:
 IBYTES STACCK.DAT

SCWOB:
 IBYTES SCROLL5.SIN

ZOOM2:
 IBYTES ZOOMFF.PIC

FRONT:
 IBYTES FRONT.DAT

SMALLFONT:
 IBYTES SMALLFNT.DAT

FONT:
 IBYTES GOLDFONT.DAT
TEMP:
 IBYTES FFZOOM.DAT
ZOOMGRAF EQU TEMP+(O*16)
 END

 