ADRESS	EQU	$10000
TRACK	EQU	83
	MOVE.W #1,-(A7)
	CLR -(A7)
	MOVE.W #0,-(A7)
	MOVE.W #1,-(A7)
	CLR.W -(A7)
	CLR.L -(A7)
	PEA BUFFER
	MOVE.W #8,-(A7)
	TRAP #14
	LEA 20(A7),A7
	LEA BUFFER+2,A0
	LEA INFO,A1
	MOVEQ #33,D7
COPY
	MOVE.B (A0)+,(A1)+
	DBRA D7,COPY
	LEA LOADER,A0
	MOVE.W #$FE,D7
	MOVE.W #$1234,D6
	MOVEQ #0,D0
.1
	ADD.W (A0)+,D0
	DBRA D7,.1
	SUB.W D0,D6
	MOVE.W D6,(A0)
	MOVE.W #1,-(A7)
	CLR -(A7)
	MOVE.W #TRACK,-(A7)
	MOVE.W #11,-(A7)
	CLR.W -(A7)
	CLR.L -(A7)
	PEA BUFFER
	MOVE.W #9,-(A7)
	TRAP #14
	LEA 20(A7),A7
	MOVE.W #1,-(A7)
	CLR -(A7)
	MOVE.W #0,-(A7)
	MOVE.W #1,-(A7)
	CLR.W -(A7)
	CLR.L -(A7)
	PEA LOADER
	MOVE.W #9,-(A7)
	TRAP #14
	LEA 20(A7),A7
	CLR.W -(A7)
	TRAP #1
LOADER	
	BRA.S BOOT
INFO
	DS.B 34
BOOT
	BSR SUPEX
	MOVE.W #1,-(A7)
	CLR -(A7)
	MOVE.W #TRACK,-(A7)
	MOVE.W #11,-(A7)
	CLR.W -(A7)
	CLR.L -(A7)
	PEA ADRESS
	MOVE.W #8,-(A7)
	TRAP #14
	LEA 20(A7),A7
	JMP ADRESS
SUPEX
	MOVE.L #$0E0E0505,$FFFF8800.W
	ST $43E.W
	MOVEQ #1,D1
	MOVE.W #$8604,A0
	BSR SEEK
	BSR SET
	BSR RT
	BSR CHK
	BSR RST
	SF $43E.W
	MOVE.L #$0E0E0707,$FFFF8800.W
	RTS
SET
	LEA BUFFER(PC),A1
	MOVE.W #$90,2(A0)
	MOVE.W #$190,2(A0)
	MOVE.W #$90,2(A0)
	MOVE.W #$1F,(A0)
	EXG A1,D7
	MOVE.B D7,9(A0)
	LSR.L #8,D7		
	MOVE.B D7,7(A0)		
	LSR.L #8,D7		
	MOVE.B D7,5(A0)
	RTS
CHK
	LEA BUFFER(PC),A1
CL	CMP.B #$7F,(A1)
	BEQ.S .END
	CMP.B #$A1,(A1)+
	BEQ.S .NO
	BRA.S CL
.END
	ADDQ.W #1,A1
	MOVE.B (A1)+,D7
	LSL.L #8,D7
	MOVE.B (A1)+,D7
	LSL.L #8,D7
	MOVE.B (A1)+,D7
	LSL.L #8,D7
	MOVE.B (A1)+,D7
;	CMP.L #$C24F03C8,D7	; RAP
;	CMP.L #$91980190,D7	; LGT
;	CMP.L #$91921813,D7	; LIGHTMAN
	CMP.L #$C2000E08,D7	; SUCK MY DICK
	BNE.S .NO
	RTS
.NO
	NOP
	MOVE.W	#$2700,SR
	NOP
	BEQ.S	RT
	NOP
	BRA.S .NO
	DC.W	$4AFC
	DC.W	$6001
RT
	MOVE.W #$80,2(A0)
	DIVU D1,D1
	MOVE.W #$E0,(A0)
	DIVU D1,D1
	BSR FDCW
	RTS
RST
	MOVE.W #$80,2(A0)
	DIVU D1,D1
	MOVE.W  #3,(A0)
	DIVU D1,D1
	BSR FDCW
	RTS
SEEK
	MOVE.W #$86,2(A0)
	DIVU D1,D1
	MOVE.W #TRACK,(A0)
	DIVU D1,D1
	MOVE.W #$80,2(A0)
	DIVU D1,D1
	MOVE.W #$13,(A0)
	DIVU D1,D1
	BSR FDCW
	RTS
FDCW
	MOVE.L #$80000,D7
.1
	BTST #5,$FFFFFA01.W
	BEQ.S .END
	SUBQ.L #1,D7
	BNE.S .1
.END
	RTS
	DS.B 512
BUFFER
	
	
	