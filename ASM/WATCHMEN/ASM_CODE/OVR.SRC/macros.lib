* bibliotheque de macros tres utilisees
*
* (c)decembre 1989 Mister Bee from The Overlanders

supexec	macro
	pea	\1
	move	#$26,-(sp)	supexec
	trap	#14		xbios
	lea	6(sp),sp
	endm

pterm	macro
	clr	-(sp)		pterm
	trap	#1		gemdos
	endm

vsync	macro
	move	#$25,-(sp)	vsync
	trap	#14		xbios
	lea	2(sp),sp
	endm

prtmess	macro
	move.l	\1,-(sp)
	move	#9,-(sp)		printline
	trap	#1		gemdos
	lea	6(sp),sp
	endm
	
waitkey	macro
	move	#7,-(sp)		attend une touche
	trap	#1		gemdos
	lea	2(sp),sp
	endm

inkey	macro
	move	#$b,-(sp)		conin stat
	trap	#1		gemdos
	lea	2(sp),sp
	tst	d0		touche appuyee ?
	beq.s	.inkey2		non ...
	move	#7,-(sp)		oui alors on la lit
	trap	#1		gemdos
	lea	2(sp),sp
.inkey2	
	endm
	
getphy	macro
	move	#2,-(sp)		physbase
	trap	#14		xbios
	lea	2(sp),sp
	move.l	d0,\1		save phybase
	endm

getlog	macro
	move	#3,-(sp)		logbase
	trap	#14		xbios
	lea	2(sp),sp
	move.l	d0,\1		save logbase
	endm

getrez	macro
	move	#4,-(sp)		getrez
	trap	#14		xbios
	lea	2(sp),sp
	move	d0,\1		save rez
	endm

setscr	macro
	move	\1,-(sp)		rez
	move.l	\2,-(sp)		phybase
	move.l	\3,-(sp)		logbase
	move	#5,-(sp)		setsceen
	trap	#14
	lea	12(sp),sp
	endm

setpal	macro
	movem.l	\1,d0-d7		lit la palette
	movem.l	d0-d7,$ffff8240.w	setpal
	endm

getpal	macro
	movem.l	$ffff8240.w,d0-d7	lit la palette
	movem.l	d0-d7,\1
	endm

clrpal	macro
	moveq	#7,d0
	movea	#$8240,a0
.clrpal2	clr.l	(a0)+
	dbf	d0,.clrpal2
	endm

soundoff	macro
	move.l	#$8080000,$ffff8800.w
	move.l	#$9090000,$ffff8800.w
	move.l	#$a0a0000,$ffff8800.w
	endm

mouseoff	macro
	move.b	#$12,$fffffc02.w
	endm

mouseon	macro
	move.b	#8,$fffffc02.w
	endm
