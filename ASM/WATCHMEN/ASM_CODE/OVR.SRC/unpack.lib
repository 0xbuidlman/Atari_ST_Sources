* Decompression d'image DEGAS.PC1
* (C)Octobre 1989  Mister Bee
* Parms:
*  Entree:a0.l=adresse du buffer
*         a1.l=adresse de l'ecran

unpack	move	#199,d0		200 lignes
unpack1	moveq	#3,d1		4 plans
unpack2	moveq	#40,d2		40 colonnes
unpack3	moveq	#0,d3
	move.b	(a0)+,d3		lit l'octet de cmd
	bmi.s	unpack6		si cmd neg ...

unpack4	move.b	(a0)+,(a1)+	on recopie les d3+1 valeurs suivantes ds le plan en cours
	subq	#1,d2		compteur de colonne-1
	move	a1,d5
	btst	#0,d5		mot du plan fait ?
	bne.s	unpack5		non ...
	lea	6(a1),a1		colonne suivante
unpack5	dbf	d3,unpack4
	bra.s	unpack9		suite ...

unpack6	neg.b	d3		rend le compteur d'octet positif
	move.b	(a0)+,d4		lit la valaur suivante
unpack7	move.b	d4,(a1)+		et la recopie d3+1 fois
	subq	#1,d2		compteur de colonne-1
	move	a1,d5
	btst	#0,d5		mot du plan fait ?
	bne.s	unpack8		non ...
	lea	6(a1),a1		colonne suivante
unpack8	dbf	d3,unpack7

unpack9	tst	d2		fin de la ligne ???
	bne.s	unpack3		cmd suivante ...
	lea	-158(a1),a1	revient au debut de la ligne plan suivant
	dbf	d1,unpack2	plan suivant ...
	lea	152(a1),a1	ligne suivante
	dbf	d0,unpack1	pour toutes les lignes
	rts