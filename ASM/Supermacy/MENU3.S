
NUMZIK=1		; NUMERO DE LA MUSIQUE (ICI, 2EME)
ADDZIK=8		; 8 POUR MADMAX, 2 POUR COUNTZERO

	MOVE.L	4(A7),BASE

	CLR.L	-(A7)
	MOVE.W	#32,-(A7)
	TRAP	#1
	ADDQ.L	#6,A7
	MOVE.L	D0,STACK
	LEA	MY,A7

	MOVE.B	#$12,$FFFFFC02.W
	LEA	STORAGE,A0
	MOVE.L	$70.W,(A0)+
	MOVE.L	$120.W,(A0)+
	MOVE.B	$FFFFFA1B.W,(A0)+
	MOVE.B	$FFFFFA21.W,(A0)+
	MOVE.B	$FFFFFA07.W,(A0)+
	MOVE.B	$FFFFFA09.W,(A0)+
	MOVE.B	$FFFFFA13.W,(A0)+
	ADDQ.L	#1,A0
	MOVE.W	$FFFF8240.W,(A0)+
	MOVE.W	$FFFF8242.W,(A0)+

	MOVE.W	#2,-(A7)
	TRAP	#14
	ADDQ.L	#2,A7
	MOVE.L	D0,PHYSIC

	MOVE.W	#3,-(A7)
	TRAP	#14
	ADDQ.L	#2,A7
	MOVE.L	D0,LOGIC

	MOVE.W	#4,-(A7)
	TRAP	#14
	ADDQ.L	#2,A7
	MOVE.W	D0,RES

	MOVEQ	#NUMZIK,D0
	JSR	ZIK

	DC.W	$A000
	DC.W	$A00A
	
	CLR.W	-(A7)
	PEA	$70000
	PEA	$70000
	MOVE.W	#5,-(A7)
	TRAP	#14
	LEA	12(A7),A7

	LEA	$70000,A0
	LEA	IMAGE+34,A1
	MOVE.W	#7999,D0
FILL	MOVE.L	(A1)+,(A0)+
	DBF	D0,FILL
	MOVE.W	#1799,D0
CLE	CLR.L	(A0)+
	DBF	D0,CLE
	CLR.W	$FFFF8240.W
	MOVE.W	#$777,$FFFF8242.W
	
	MOVE.W	#$2700,SR
	MOVE.L	#VBL,$70.W
	MOVE.L	#TIMER_B,$120.W
	CLR.B	$FFFFFA07.W
	CLR.B	$FFFFFA09.W
	ORI.B	#1,$FFFFFA07.W
	ORI.B	#1,$FFFFFA13.W
	CLR.B	$FFFFFA1B.W
	MOVE.W	#$2300,SR

WAIT	MOVE.B	$FFFFFC02.W,D7

	CMPI.B	#$3B,D7
	BLT.S	A1
	CMPI.B	#$45,D7
	BLT.S	FNCTION		; F1-F2-F3-F4-F5-F6-F7-F8-F9-F10

A1	CMPI.B	#$01,D7
	BLT.S	A2
	CMPI.B	#$0C,D7
	BLT.S	UN_A_0		; ESC-1-2-3-4-5-6-7-8-9-0

A2	CMPI.B	#$10,D7
	BLT.S	A3
	CMPI.B	#$1A,D7
	BLT.S	A_A_P		; A-Z-E-R-T-Y-U-I-O-P

A3	CMPI.B	#$1E,D7
	BLT.S	A4
	CMPI.B	#$28,D7
	BLT.S	Q_A_M		; Q-S-D-F-G-H-J-K-L-M

A4	CMPI.B	#$2C,D7
	BLT.S	A5
	CMPI.B	#$35,D7
	BLT.S	W_A_FIN		; W-X-C-V-B-N-,-;-:-=

A5	CMPI.B	#$39,D7
	BNE.S	WAIT		; SPACE

	LEA	NSPACE(PC),A4
	BRA.S	FOUND

W_A_FIN	SUBI.B	#$2C-41,D7
	BRA.S	LOAD
Q_A_M	SUBI.B	#$1E-31,D7
	BRA.S	LOAD
A_A_P	SUBI.B	#$10-21,D7
	BRA.S	LOAD
UN_A_0	SUBI.B	#$01-10,D7
	BRA.S	LOAD
FNCTION	SUBI.B	#$3B,D7

LOAD	EXT.W	D7
	LEA	DESNOMS(PC),A4
	LSL	#2,D7		; POSITIONNE SUR LA BONNE ADRESSE
	MOVE.L	(A4,D7.W),A4	; (TSB-BP66-59420 MOUVAUX-FRANCE)
FOUND	TST.B	(A4)		; PAS DE NOM ICI ?
	BNE.S	NOWAIT		; ON OUBLIE L'APPUI !
	CLR	D7
	LEA	$0.W,A4
	BRA	WAIT

NOWAIT	PEA	(A4)
	MOVE.W	#$2700,SR
	LEA	STORAGE,A0
	MOVE.L	(A0)+,$70.W
	MOVE.L	(A0)+,$120.W
	MOVE.B	(A0)+,$FFFFFA1B.W
	MOVE.B	(A0)+,$FFFFFA21.W
	MOVE.B	(A0)+,$FFFFFA07.W
	MOVE.B	(A0)+,$FFFFFA09.W
	MOVE.B	(A0)+,$FFFFFA13.W
	ADDQ.L	#1,A0
	MOVE.W	(A0)+,$FFFF8240.W
	MOVE.W	(A0)+,$FFFF8242.W
	MOVE.B	#8,$FFFFFC02.W

	MOVE.L	#$8080000,$FFFF8800.W
	MOVE.L	#$9090000,$FFFF8800.W
	MOVE.L	#$A0A0000,$FFFF8800.W

	DC.W	$A000
	DC.W	$A009

	MOVE.W	#$2300,SR

	MOVE.W	RES,-(A7)
	MOVE.L	PHYSIC,-(A7)
	MOVE.L	LOGIC,-(A7)
	MOVE.W	#5,-(A7)
	TRAP	#14
	LEA	12(A7),A7
		
	MOVE.L	(A7)+,A6
	MOVE.L	STACK,-(A7)
	MOVE.W	#32,-(A7)
	TRAP	#1
	ADDQ.L	#6,A7		; UTILISATEUR
	PEA	(A6)

	PEA	$100.W
	MOVE.L	BASE,-(A7)
	PEA	$4A0000
	TRAP	#1
	LEA	12(A7),A7	; REDUIT LA MEMOIRE PROGRAMME A 1,5KO

	MOVE.L	BASE,A0
	MOVE.L	A0,A1
	ADD.L	#1500,A0
	ADD.L	#100000,A1	; EFFACE 100KO DE MEMOIRE
	MOVEQ	#0,D0
BEUH	MOVE.L	D0,(A0)+
	CMPA.L	A0,A1
	BNE.S	BEUH

	MOVE.L	(A7)+,A0
	PEA	BIDON(PC)
	PEA	BIDON(PC)
	PEA	(A0)
	PEA	$4B0000
	TRAP	#1		; EXEC PROGRAMME
	LEA	12(A7),A7

	CLR.L	-(A7)
	MOVE	#32,-(A7)
	TRAP	#1

	CLR	-(SP)
	TRAP	#1

BIDON	DC.W	0
DESNOMS	DC.L	N0,N1,N2,N3,N4,N5,N6,N7,N8,N9
	DC.L	N10,N11,N12,N13,N14,N15,N16,N17,N18,N19
	DC.L	N20,N21,N22,N23,N24,N25,N26,N27,N28,N29
	DC.L	N30,N31,N32,N33,N34,N35,N36,N37,N38,N39
	DC.L	N40,N41,N42,N43,N44,N45,N46,N47,N48,N49,N50

N0	DC.B	"\1.TSB",0	; F1
N1	DC.B	"\2.TSB",0	; F2
N2	DC.B	"\3.TSB",0	; F3
N3	DC.B	0		; F4
N4	DC.B	0		; F5
N5	DC.B	0		; F6
N6	DC.B	0		; F7
N7	DC.B	0		; F8
N8	DC.B	0		; F9
N9	DC.B	0		; F10

N10	DC.B	0		; ESC

N11	DC.B	0		; 1
N12	DC.B	0		; 2
N13	DC.B	0		; 3
N14	DC.B	0		; 4
N15	DC.B	0		; 5
N16	DC.B	0		; 6
N17	DC.B	0		; 7
N18	DC.B	0		; 8
N19	DC.B	0		; 9
N20	DC.B	0		; 0

N21	DC.B	0		; A
N22	DC.B	0		; Z
N23	DC.B	0		; E
N24	DC.B	0		; R
N25	DC.B	0		; T
N26	DC.B	0		; Y
N27	DC.B	0		; U
N28	DC.B	0		; I
N29	DC.B	0		; O
N30	DC.B	0		; P

N31	DC.B	0		; Q
N32	DC.B	0		; S
N33	DC.B	0		; D
N34	DC.B	0		; F
N35	DC.B	0		; G
N36	DC.B	0		; H
N37	DC.B	0		; J
N38	DC.B	0		; K
N39	DC.B	0		; L
N40	DC.B	0		; M

N41	DC.B	0		; W
N42	DC.B	0		; X
N43	DC.B	0		; C
N44	DC.B	0		; V
N45	DC.B	0		; B
N46	DC.B	0		; N
N47	DC.B	0		; ,
N48	DC.B	0		; ;
N49	DC.B	0		; :
N50	DC.B	0		; =

NSPACE	DC.B	0	; SPACE !!

VBL	CLR.B	$FFFFFA1B.W
	MOVE.B	#199,$FFFFFA21.W
	MOVE.L	#TIMER_B,$120.W
	MOVE.B	#8,$FFFFFA1B.W
	MOVEM.L	D0-A6,-(A7)
	JSR	ZIK+ADDZIK
	MOVEM.L	IMAGE+2,D0-D6/A0
	MOVEM.L	D0-D6/A0,$FFFF8240.W
	MOVE.L	$44E.W,A6
	LEA	32000+3*160(A6),A6
	LEA	153(A6),A5
	MOVEQ	#32-1,D1		; HAUTEUR FONTE - 1
REPT	MOVEQ	#19,D2
REPT2	MOVEP.L	1(A6),D0
	MOVEP.L	D0,(A6)
	MOVEP.L	8(A6),D0
	MOVEP.L	D0,1(A6)
	ADDQ.L	#8,A6
	DBF	D2,REPT2
	DBF	D1,REPT
	TST.W	OFFS
	BNE.S	NS1
	MOVE	#1,OFFS
	BRA.S	NS4
NS1	CMPI.W	#1,OFFS
	BNE.S	NS2
	MOVE	#8,OFFS
	BRA.S	NS4
NS2	CMPI.W	#8,OFFS
	BNE.S	NS3
	MOVE	#9,OFFS
	BRA.S	NS4
NS3	CMPI.W	#9,OFFS
	BNE.S	NS4
	CLR	OFFS
	MOVE	POINT,D1
	ADDQ	#1,D1
	LEA	TEXT,A6
	TST.B	(A6,D1.W)
	BNE.S	NOEND
	CLR	D1
NOEND	MOVE	D1,POINT
	MOVE.B	(A6,D1.W),D1
	SUBI.B	#32,D1
	EXT.W	D1
	LSL	#2,D1
	LEA	CHAR,A6
	MOVE.L	(A6,D1.W),ADR
NS4	MOVE	OFFS,D1
	MOVE.L	ADR,A6
	LEA	(A6,D1.W),A6
LM	SET	0
	REPT	32			; HAUTEUR FONTE
	MOVEP.L	LM(A6),D0
	MOVEP.L	D0,LM(A5)
LM	SET	LM+160
	ENDR
	MOVEM.L	(A7)+,D0-A6
	RTE

TIMER_B	CLR.B	$FFFFFA1B.W
	MOVEM.L	D0-A6,-(A7)
	MOVEA.W	#$FA21,A0
	MOVE.B	#199,(A0)
	MOVE.B	#8,$FFFFFA1B.W
	MOVE.B	(A0),D0
WAIT2	CMP.B	(A0),D0			
	BEQ.S	WAIT2
	CLR.B	$FFFF820A.W
	MOVEQ	#3,D0
LOOP1	NOP
	DBF	D0,LOOP1
	MOVE.B	#2,$FFFF820A.W
	MOVEM.L	FONT+2,D0-D6/A0
	MOVEM.L	D0-D6/A0,$FFFF8240.W
	MOVEM.L	(A7)+,D0-A6
	CLR.B	$FFFFFA1B.W
	MOVE.B	#1,$FFFFFA21.W
	MOVE.B	#8,$FFFFFA1B.W
	MOVE.L	#HBL2,$120.W
	LEA	PALETTE(PC),A0
	BCLR	#0,$FFFFFA0F.W
	RTE
		
HBL2	MOVE.W	(A0)+,$FFFF8240.W
	BCLR	#0,$FFFFFA0F.W
	RTE

	SECTION	DATA

OFFS	DC.W	9
PALETTE	DC.W	0,0,0,0,0,0,0,0,0,0,0,0,0,$100,$100,$100,$100,$100,$100,$100,$100,$100,$100,$100,0,0,0,0,0,0,0,0,0,0,0,0,0
	DS.L	15
;PALETTE	DC.W	0,$10,$20,$30,$40,$50,$60,$70,$71,$72,$73,$74,$75,$76,$77,$176,$275,$374,$473,$572,$671,$770,$761,$752,$743,$734,$725,$716,$707
;	DC.W	$607,$507,$407,$307,$207,$107,7,6,5,3,2,1,0
	DS.L	15

	; DES RASTERS ? EN VEUX-TU EN VOILA ^^^^^^^^^^^^
	; POUR LES CHANGER TU LES REMPLACES PAR CEUX QUI SONT EN ";"

TEXT	INCLUDE	MENU3.TXT	; nom de ton fichier texte
IMAGE	INCBIN	MENU3.PI1
FONT	INCBIN	FONT.PI1
I	EQU	FONT+34
CHAR	DC.L	I,I+16,I+32,I+48,I+64,I+80,I+96,I+112,I+128,I+144
	DC.L	I+160*32,I+160*32+16,I+160*32+32,I+160*32+48,I+160*32+64,I+160*32+80,I+160*32+96,I+160*32+112,I+160*32+128,I+160*32+144
	DC.L	I+160*64,I+160*64+16,I+160*64+32,I+160*64+48,I+160*64+64,I+160*64+80,I+160*64+96,I+160*64+112,I+160*64+128,I+160*64+144
	DC.L	I+160*96,I+160*96+16,I+160*96+32,I+160*96+48,I+160*96+64,I+160*96+80,I+160*96+96,I+160*96+112,I+160*96+128,I+160*96+144
	DC.L	I+160*128,I+160*128+16,I+160*128+32,I+160*128+48,I+160*128+64,I+160*128+80,I+160*128+96,I+160*128+112,I+160*128+128,I+160*128+144
	DC.L	I+160*160,I+160*160+16,I+160*160+32,I+160*160+48,I+160*160+64,I+160*160+80,I+160*160+96,I+160*160+112,I+160*160+128,I+160*160+144

ZIK	INCBIN	MAD_1.MUS

	SECTION	BSS

STORAGE	DS.B	18
	DS.L	63
MY	DS.L	1
RES	DS.W	1
LOGIC	DS.L	1
PHYSIC	DS.L	1
STACK	DS.L	1
BASE	DS.L	1
POINT	DS.W	1
ADR	DS.L	1

	END
