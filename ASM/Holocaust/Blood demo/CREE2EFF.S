;Programme cr‚ant le code g‚n‚r‚ de l'effa‡age pour les montagnes
;fractales. Code by Zappy/Holocaust.

MAIN	CLR.L	-(SP)
	MOVE.W	#$20,-(SP)
	TRAP	#1
	ADDQ.W	#6,SP

	CLR	-(SP)
	PEA	-1.W
	PEA	-1.W
	MOVE	#5,-(SP)
	TRAP	#14
	LEA	12(SP),SP

	MOVE.L	$44E.W,A0
	LEA	IMG,A1
	MOVE	#7999-40*8,D7
PO	MOVE.L	(A1)+,(A0)+
	DBF	D7,PO

	MOVE.L	$44E.W,A0
	MOVE.L	A0,A4
	LEA	EFF,A1
	MOVEQ	#0,D7
LOOP	MOVEQ	#-1,D6
	MOVEQ	#40+1,D5
	MOVE.L	A4,A0
	LEA	160(A0),A2
.LOOK	ADDQ	#1,D6
	TST.L	(A0)
	BNE.S	.LOOK2
	ADDQ	#4,A0
	CMP.L	A0,A2
	BEQ.S	.NUL
	BRA.S	.LOOK

.LOOK2	SUBQ	#1,D5
	TST.L	-(A2)
	BEQ.S	.LOOK2
	
	MOVE	D6,D4
	ADD	D4,D4
	ADD	D4,D4	D4=OFF7 DEP
	
	SUB	D6,D5	D5=Dx
	
	CMPI	#14,D5
	BGE.S	.DO
	BRA.S	.RESTE
.DO	DIVU	#14,D5
	MOVE	D5,D6
	SUBQ	#1,D6
.ENTIER	MOVE.L	#$48E87EFF,(A1)+
	MOVE	D7,(A1)
	ADD	D4,(A1)+
	ADD	#14*4,D4
	DBF	D6,.ENTIER
	SWAP	D5
.RESTE	TST	D5
	BEQ.S	.NUL
	SUBQ	#1,D5
	ADD	D5,D5
	LEA	TABLE,A2
	MOVE	#$48E8,(A1)+
	MOVE	(A2,D5.W),(A1)+
	MOVE	D7,(A1)
	ADD	D4,(A1)+
	;ADD	D5,D5
	;ADD	D5,D4
.NUL
	ADD	#160,D7
	LEA	160(A4),A4
	CMPI	#160*192,D7
	BNE	LOOP

;D0-D7/A1-A6=8+6=14 REGS
J2
	MOVE	#$4E75,(A1)
****
	
	MOVE.L	#VBL,$70.W
J	BRA.S	J

TABLE	DC	$0001
	DC	$0003
	DC	$0007
	DC	$000F
	DC	$001F
	DC	$003F
	DC	$007F
	DC	$00FF
	DC	$02FF
	DC	$06FF
	DC	$0EFF
	DC	$1EFF
	DC	$3EFF
	DC	$7EFF

VBL	MOVE	#5000,D7
P	DBF	D7,P
	ST	$FFFF8240.W
	MOVE.L	$44E.W,A0
	MOVEQ	#0,D0
	MOVEQ	#0,D1
	MOVEQ	#0,D2
	MOVEQ	#0,D3
	MOVEQ	#0,D4
	MOVEQ	#0,D5
	MOVEQ	#0,D6
	MOVEQ	#0,D7
	MOVE.L	D0,A1
	MOVE.L	D0,A2
	MOVE.L	D0,A3
	MOVE.L	D0,A4
	MOVE.L	D0,A5
	MOVE.L	D0,A6
	JSR	EFF
	SF	$FFFF8240.W
	RTE
	DATA
IMG	INCBIN	SCREEN.SAV

	BSS
EFF	DS.B	20000
