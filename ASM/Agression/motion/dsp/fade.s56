; Realtime truecolor fader..  21.12.95 WizTom of AGGRESSION
; begin: 18:09

PBC	equ	$ffe0
HSR	equ	$ffe9
HTX	equ	$ffeb


	org	p:$0
	jmp	start

	dc	0,0,0,0,0,0,0,0,0,0
	dc	0,0,0,0,0,0,0,0,0,0
	dc	0,0,0,0,0,0,0,0,0,0
	dc	0,0,0,0,0,0,0,0,0,0
	dc	0,0,0,0,0,0,0,0,0,0
	dc	0,0,0,0,0,0,0,0,0,0
	dc	0,0,0,0
start	
	move	#>$ffff,m0
	movep	#$0,x:$fffe		;fix read
	move	m0,m1		
	move	m0,m2		
	move	m0,m3		
	move	m0,m4		
	move	m0,m5		
	move	m0,m6		
	move	m0,m7		
	jsr	fixvakiot
	move	#>$babe,x0
x_ready	jclr	#1,X:HSR,x_ready
	move	x0,X:HTX		
;------------------------------
main
	move	#>$feed,x0
xtest	jclr	#1,X:HSR,xtest
	move	x0,X:HTX		

monta	movep	#1,X:PBC		
	jclr	#0,x:HSR,monta
	move	x:HTX,n0

get_eka	movep	#1,X:PBC
	jclr	#0,X:HSR,get_eka
	move	x:HTX,y1

get_tok	movep	#1,X:PBC
	jclr	#0,X:HSR,get_tok
	move	x:HTX,x1

;------
	move	#>HTX,r0
	move	#>spareRB,r3
	move	#>spareG,r4
	move	#>stackRB,r2
	move	#>stackG,r5
	move	#vakio,r7
	move	#vakio,r1
	movep	#1,X:PBC		
	do	n0,luekamaa

v1	equ	$0007e0		;G maski -> B
v1b	equ	$00f81f		;RB maski -> A
;--------
r_eka	jclr	#0,x:HSR,r_eka
	move			x:(r0),x0	y:(r7),a
	and	x0,a		x:(r7),b
	and	x0,b		a,x0
	mpy	y1,x0,a		b,x0
	mpy	y1,x0,b		a,x:(r2)
	move			x:(r1),b	b,y:(r5)

r_toka	jclr	#0,x:HSR,r_toka
	move			x:(r0),x0	y:(r7),a
	and	x0,a		
	and	x0,b		x:(r2),a	a,y0
	mac	x1,y0,a		b,x0		y:(r5),b
	mac	x1,x0,b		a,x:(r3)+
	move			b,y:(r4)+
luekamaa
;-----
	move	#>spareRB,r3
	move	#>spareG,r4
	move	#>vakio+1,r7
	move	#>vakio+1,r1
	do	n0,ulos

v2	equ	$0007e0
v2b	equ	$00f81f
;-----
	move			x:(r3)+,x0	y:(r7),a
	and	x0,a		x:(r1),b	y:(r4)+,y0	
	and	y0,b		a,x0
	or	x0,b
w_one	jclr	#1,X:HSR,w_one
	move	b1,X:(r0)		
ulos	
;----

	jmp	main
;------------------------------------
fixvakiot
	move	#vakio,r0
	move	#>v1,x1
	move	#>v1b,x0
	move	x,l:(r0)+
	move	#>v2,x1
	move	#>v2b,x0
	move	x,l:(r0)+
	rts
;-------------------
;-------------------
;read_one
;	movep	#1,X:PBC		
;	jclr	#0,x:HSR,read_one
;	move	x:HTX,a1
;write_one
;	jclr	#1,X:HSR,write_one
;	move	a1,X:HTX		
;---------------------
;jako						;divs x0,a (signed $123.456)
;	rep	#12
;	asr	a
;	andi	#$fe,ccr			;clear carry
;	rep	#24
;	div	x0,a
;	asl	a
;	jclr	#23,x0,pos_diver
;	neg	a
;pos_diver
;	rts		;signed result in a0 (finally WORKS!!!)
;---------------------------------
	org	x:$0
vakio	ds	5
stackRB	ds	1
spareRB	ds	10000	

	org	y:$0
vakiob	ds	5
stackG	ds	1
spareG	ds	10000
	end
