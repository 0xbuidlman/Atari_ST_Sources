RESERVE 150000
INLINE dma%,24500
INLINE dms%,5
INLINE dms2%,5
IF C:dma%(W:0,W:0)
  PRINT "Buy a STe !!"
  PRINT "Achetez un STe !!!"
  PAUSE 100
  OUT 4,8
  EDIT
ENDIF
befor
main
PROCEDURE re_init
  POKE dms%,40
  POKE dms%+1,20
  POKE dms%+2,20
  POKE dms%+3,10
  POKE dms%+4,10
  POKE dms2%,40
  POKE dms2%+1,20
  POKE dms2%+2,20
  POKE dms2%+3,12
  POKE dms2%+4,12
RETURN
PROCEDURE main
  dmp%=0
  maxra%=138
  ra1%=ra%+2
  ra2%=ra%+4
  ra3%=ra%+6
  CLR pra%
  a$=SPACE$(20)
  BMOVE mu%,V:a$,20
  tx(0,163,a$)
  k|=0
  ek%=20
  w1%=130
  w2%=-1
  '  FOR a%=0 TO 15
  '  DEFFILL a%
  '  PBOX a%*16,180,a%*16+16,199
  ' NEXT a%
  DO
    a5%=PEEK(ADD(dma%,3024))
    a6%=PEEK(ADD(dma%,3022))
    a1%=PEEK(ADD(dma%,3278))
    a2%=PEEK(ADD(dma%,3302))
    a3%=PEEK(ADD(dma%,3326))
    a4%=PEEK(ADD(dma%,3350))
    IF w2%<>a5%
      IF w1%<>a6%
        IF w1%>a6%
          IF ek%=30
            ek%=20
          ELSE
            ek%=30
          ENDIF
        ENDIF
        w1%=a6%
      ENDIF
      w2%=a5%
      RC_COPY XBIOS(2),ek%,76,1,2 TO XBIOS(2),MUL(w1%,2),ADD(MUL(w2%,2),150)
    ENDIF
    IF a1%<5
      ~XBIOS(30,-1)
    ELSE
      ~XBIOS(29,0)
    ENDIF
    IF k|=27
      k|=0
      t%=-1
    ENDIF
    IF k|=9
      k|=0
      t%=0
      ~XBIOS(6,L:ADD(serious%,2))
      VSYNC
    ENDIF
    IF t%
      SETCOLOR 1,a1%
      SETCOLOR 2,a2%
      SETCOLOR 3,a3%
      SETCOLOR 8,a4%
    ENDIF
    INC pra%
    INC pra%
    IF pra%=maxra%
      CLR pra%
    ENDIF
    IF tm%>0
      DEC tm%
      SETCOLOR 9,DPEEK(ra%+pra%)
      IF tm%>6
        SETCOLOR 14,&H60
      ELSE
        SETCOLOR 14,0,tm%,0
      ENDIF
    ENDIF
    IF tm%=0
      tm%=-1
      SETCOLOR 9,0
      SETCOLOR 14,0
    ENDIF
    v1|=INT(ADD(DIV(PEEK(e1%),16),(PEEK(f1%) AND &X10000)))
    v2|=INT(ADD(DIV(PEEK(e2%),16),(PEEK(f2%) AND &X10000)))
    v3|=INT(ADD(DIV(PEEK(e3%),16),(PEEK(f3%) AND &X10000)))
    v4|=INT(ADD(DIV(PEEK(e4%),16),(PEEK(f4%) AND &X10000)))
    IF v1|>0
      IF v1|<16
        RC_COPY pc%,2,3,20,16 TO XBIOS(2),MUL(v1|,20),0
      ELSE
        RC_COPY pc%,26,0,20,16 TO XBIOS(2),MUL(SUB(v1|,15),20),20
      ENDIF
    ENDIF
    IF v2|>0
      IF v2|<16
        RC_COPY pc%,50,3,20,16 TO XBIOS(2),MUL(v2|,20),0
      ELSE
        RC_COPY pc%,74,0,20,16 TO XBIOS(2),MUL(SUB(v2|,15),20),20
      ENDIF
    ENDIF
    IF v3|>0
      IF v3|<16
        RC_COPY pc%,98,3,20,16 TO XBIOS(2),MUL(v3|,20),0
      ELSE
        RC_COPY pc%,122,0,20,16 TO XBIOS(2),MUL(SUB(v3|,15),20),20
      ENDIF
    ENDIF
    IF v4|>0
      IF v4|<16
        RC_COPY pc%,146,3,20,16 TO XBIOS(2),MUL(v4|,20),0
      ELSE
        RC_COPY pc%,170,0,20,16 TO XBIOS(2),MUL(SUB(v4|,15),20),20
      ENDIF
    ENDIF
    BMOVE x1%,x0%,2760
    BMOVE x2%,x3%,2760
    IF INP(-2)
      k|=INP(2)
      REPEAT
      UNTIL INKEY$=""
    ENDIF
    IF k|=226
      k|=0
      aff_dms(dmp%)
    ENDIF
    IF k|=205
      IF PEEK(dms%+dmp%)<PEEK(dms2%+dmp%)
        POKE dms%+dmp%,PEEK(dms%+dmp%)+1
        ste
      ENDIF
      aff_dms(dmp%)
    ENDIF
    IF k|=203
      IF PEEK(dms%+dmp%)>0
        POKE dms%+dmp%,PEEK(dms%+dmp%)-1
        ste
      ENDIF
      aff_dms(dmp%)
    ENDIF
    IF k|=200
      INC dmp%
      IF dmp%=5
        dmp%=0
      ENDIF
      aff_dms(dmp%)
    ENDIF
    IF k|=199 OR k|=210
      re_init
      ste
      aff_dms(dmp%)
    ENDIF
    IF k|=208
      DEC dmp%
      IF dmp%=-1
        dmp%=4
      ENDIF
      aff_dms(dmp%)
    ENDIF
    IF k|=127
      k|=0
      SPOKE -32246,MUL((PEEK(-32246)=252),-2)
      REPEAT
      UNTIL INKEY$=""
    ENDIF
  LOOP UNTIL k|=32
  k|=0
  VOID C:dma%(W:3,W:0)   ! No mods
  VOID C:dma%(W:0,W:2)
  ~MFREE(mu%)
  ~MFREE(mu2%)
  ~XBIOS(30,-1)
RETURN
PROCEDURE befor
o4:
  CLS
  FOR a%=0 TO 15
    SETCOLOR a%,0
  NEXT a%
  VSYNC
  k%=0
  RESTORE dat
  tx2(0,96,"-  SERIOUS TRANCE  -")
  FOR a%=0 TO 7
    SETCOLOR 14,a%,a%,a%
    VSYNC
    VSYNC
    VSYNC
    SETCOLOR 14,a%+8,a%+8,a%+8
    VSYNC
    VSYNC
    VSYNC
  NEXT a%
  PAUSE 10
  FOR a%=7 DOWNTO 0
    SETCOLOR 14,a%,a%,a%
    VSYNC
    SETCOLOR 14,a%+8,a%+8,a%+8
    VSYNC
    VSYNC
  NEXT a%
  CLS
  FOR a%=0 TO 15
    SETCOLOR a%,0
  NEXT a%
  VSYNC
  FOR a%=1 TO 9
    READ a$
    tx(0,a%*17,"F"+STR$(a%)+" "+MID$(a$,1,LEN(a$)-4))
  NEXT a%
  tx(0,0,"- TECH  MUSIK DISK -")
  ~XBIOS(6,L:trance%+2)
  VSYNC
  SETCOLOR 14,0
  REPEAT
  UNTIL INKEY$=""
  hid%=0
  np%=0
  tm%=TIMER-200
  nt%=23
  RESTORE da3
  lon%=18388
  adr%=qudr%
  convert(adr%,a%,b%,c%)
  SPOKE &HFF8903,a%
  SPOKE &HFF8905,b%
  SPOKE &HFF8907,c%
  convert(adr%+lon%,a%,b%,c%)
  SPOKE &HFF890F,a%
  SPOKE &HFF8911,b%
  SPOKE &HFF8913,c%
  freq%=0
  SDPOKE &HFF8920,&HA0 OR freq%
  SDPOKE &HFF8900,3
  tz%=0
  DO
    tt%=TIMER-tm%
    IF tt%>80 AND tt%<200
      IF tz%<7
        INC tz%
      ENDIF
      SETCOLOR 14,tz%,tz%,tz%
      VSYNC
      VSYNC
    ENDIF
    IF tt%>340 AND tt%<400
      IF tz%>0
        DEC tz%
      ENDIF
      SETCOLOR 14,tz%,tz%,tz%
      VSYNC
      VSYNC
    ENDIF
    IF tt%>=400
      tm%=TIMER
      READ c$
      INC np%
      IF np%=nt%
        np%=0
        RESTORE da3
      ENDIF
      tx2(0,184,c$)
    ENDIF
    IF INP(-2)
      k%=INP(2)
    ENDIF
    IF k%=127
      REPEAT
      UNTIL INKEY$=""
      k%=0
      SPOKE -32246,MUL((PEEK(-32246)=252),-2)
      PAUSE 15
    ENDIF
    IF k%=42
      k%=0
      INC hid%
    ENDIF
  LOOP UNTIL k%>186 AND k%<196 OR k%=27 OR k%=13 OR hid%=10
  pk$="*"
  '
  IF k%=27
    CLS
    OUT 4,8
    fin
    EDIT
  ENDIF
  '
  IF k%=13
  o2:
    CLS
    PRINT CHR$(27)+"pEnter name of module to load:"+CHR$(27)+"q"
    DIR "A:\*.MOD"
    INPUT n$
    IF MID$(n$,1,1)="*"
      n$=MID$(n$,2,LEN(n$)-1)
      pk$="*"
    ELSE
      pk$=""
    ENDIF
    IF NOT EXIST(n$)
      k%=0
      REPEAT
      UNTIL INKEY$=""
      GOTO o4
    ENDIF
  ELSE
    RESTORE dat
    FOR a%=1 TO k%-186
      READ n$
    NEXT a%
  ENDIF
  '
  ~XBIOS(6,L:trance%+2)
  VSYNC
  BMOVE trance%+34,XBIOS(2),32000
  ' Loading and depacking...
  n$="A:\"+n$
o3:
  IF hid%<>10
    IF NOT EXIST(n$)
      SETCOLOR 0,&H500
      CLS
      PRINT AT(1,12);"- Insert "+CHR$(27)+"pSERIOUS TRANCE"+CHR$(27)+"q disk in A: -"
      PRINT AT(8,14);"- And bash anykey -"
      REPEAT
      UNTIL INKEY$=""
      k%=INP(2)
      IF k%=27
        CLS
        SETCOLOR 0,0
        OUT 4,8
        fin
        EDIT
      ENDIF
      ~XBIOS(6,L:trance%+2)
      VSYNC
      BMOVE trance%+34,XBIOS(2),32000
      GOTO o3
    ENDIF
  ENDIF
  '
  IF hid%<>10
    IF pk$="*"
      OPEN "I",#1,n$
      len2%=LOF(#1)
      mu2%=MALLOC(len2%)
      CLOSE #1
      BLOAD n$,mu2%
      len%=LPEEK(mu2%+8)+10
      mu%=MALLOC(len%)
      ~C:pak%(L:mu2%,L:mu%)
    ELSE
      OPEN "I",#1,n$
      len%=LOF(#1)+10
      mu%=MALLOC(len%)
      CLOSE #1
      BLOAD n$,mu%
    ENDIF
  ELSE
    len%=LPEEK(venty%+8)+10
    mu%=MALLOC(len%)
    ~C:pak%(L:venty%,L:mu%)
  ENDIF
  '
  stop.digit
  VOID C:dma%(W:0,W:1)        ! sauve l'etat du DMA
  ste
  VOID C:dma%(W:3,W:1,L:mu%)   ! module ON
  e1%=dma%+&HC5A
  f1%=dma%+&HC58
  e2%=dma%+&HC76
  f2%=dma%+&HC74
  e3%=dma%+&HC92
  f3%=dma%+&HC90
  e4%=dma%+&HCAE
  f4%=dma%+&HCAC
  pc%=pic%+34
  x0%=XBIOS(2)
  x1%=XBIOS(2)+160
  x2%=XBIOS(2)+3040
  x3%=XBIOS(2)+3200
  CLS
  ~XBIOS(6,L:serious%+2)
  BMOVE serious%+34,XBIOS(2),32000
  REPEAT
  UNTIL INKEY$=""
RETURN
PROCEDURE ste
  VOID C:dma%(W:1,W:0,W:PEEK(dms%))   ! Master
  VOID C:dma%(W:1,W:1,W:PEEK(dms%+1))   ! droite
  VOID C:dma%(W:1,W:2,W:PEEK(dms%+2))   ! gauche
  VOID C:dma%(W:1,W:3,W:PEEK(dms%+3))    ! Graves
  VOID C:dma%(W:1,W:4,W:PEEK(dms%+4))    ! Aigus
RETURN
PROCEDURE aff_dms(dmp%)
  k|=0
  tm%=200
  RC_COPY pic%+34,0,84,319,16 TO wk%,0,0
  RC_COPY pic%+34,70,28,5*PEEK(dms2%+dmp%),3 TO wk%,34,6
  RC_COPY pic%+34,70,32,5*PEEK(dms%+dmp%),16 TO wk%,34,0
  RC_COPY pic%+34,4+32*dmp%,65,32,16 TO wk%,0,0
  BMOVE wk%,XBIOS(2)+29280,2560
RETURN
