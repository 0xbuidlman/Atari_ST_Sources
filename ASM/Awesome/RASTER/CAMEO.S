;  Awesome raster intro coded by Ruthless/Magnum 1991.
;  This intro is designed not to look mega amazing but to fit
;  the disks nice and small - so fuck you!

	SECTION TEXT
	OPT	O-,D+,S-

NUMFILES	EQU	5

START	MOVE.L	4(A7),A5
	LEA	STACK,A7
	MOVE.L	$C(A5),D0
	ADDI.L	#$100,D0
	MOVE.L	D0,-(SP)
	MOVE.L	A5,-(SP)
	MOVE.L	#$4A0000,-(SP)
	TRAP	#1
	LEA	12(SP),SP

	JSR	INITIT

MLOOP	MOVE.W	#37,-(SP)
	TRAP	#14
	ADDQ.L	#2,SP

KEYS	MOVE.B	$FFFC02,D0
	TST.B	D0
	BEQ.S	MLOOP

	CMP.B	OKEY,D0
	BEQ.S	MLOOP
	MOVE.B	D0,OKEY

	CMP.B	#11,D0
	BNE	NHZ

	EORI.B	#2,HZ
	MOVE.W	#$777,$FF8240	

NHZ	SUB.B	#1,D0
	CMPI.B	#1,D0
	BGE.S	MAX
	BRA	MLOOP
MAX	CMPI.B	#NUMFILES,D0
	BLE.S	OKNUM
	BRA	MLOOP
OKNUM	MOVE.B	D0,FILE+1

QUIT	JSR	hbl_off

	MOVEM.L	(SAVEPAL),D0-D7
	MOVEM.L	D0-D7,$FF8240

	MOVE.L	#mse_on,-(SP)
	MOVE.L	#$190000,-(SP)
	TRAP	#14
	ADDQ.L	#8,SP

	DC.W	$A009

	MOVE.W	#-1,$4EE.W

	MOVE.W	old_rez,-(SP)
	MOVE.L	old_scr,-(SP)
	MOVE.L	(SP),-(SP)
	MOVE.W	#5,-(SP)
	TRAP	#14
	LEA	12(SP),SP

	MOVE.B	#7,$484

	MOVE.B	HZ,$FF820A

	MOVE.L	old_sp,-(SP)
	MOVE.W	#$20,-(SP)
	TRAP	#1
	ADDQ.L	#6,SP

	MOVE.W	FILE,D0
	MOVEQ	#0,D1
	MOVE.W	D0,D1
	SUBQ.W	#1,D1
	LSL	#2,D1
	LEA	NAMES,A0
	ADDA.L	D1,A0
	MOVE.L	(A0),A6
	PEA	ENV
	PEA	ENV
	MOVE.L	A6,-(SP)
	MOVE.L	#$4B0000,-(SP)
	TRAP	#1
	LEA	16(SP),SP

EXIT	CLR.W	-(SP)
	TRAP	#1

INITIT	MOVE.W	#4,-(SP)
	TRAP	#14
	ADDQ.L	#2,SP
	MOVE.W	D0,old_rez
	CMPI.W	#2,D0
	BEQ	EXIT

	CLR.L	-(SP)
	MOVE.W	#$20,-(SP)
	TRAP	#1
	ADDQ.L	#6,SP
	MOVE.L	D0,old_sp

	MOVE.B	#2,$FF820A

	MOVEQ	#1,D0	
	JSR	MUS

	MOVEM.L	$FF8240,D0-D7
	MOVEM.L	D0-D7,SAVEPAL

	MOVE.L	#mse_off,-(SP)
	MOVE.L	#$190000,-(SP)
	TRAP	#14
	ADDQ.L	#8,SP

	DC.W	$A00A

	MOVE.W	#2,-(SP)
	TRAP	#14
	ADDQ.L	#2,SP
	MOVE.L	D0,old_scr

	MOVE.L	#big_scr+(160*+50)+256,D0
	ANDI.L	#-256,D0
	MOVE.L	D0,scr_adr

	MOVEM.L	BLACK,D0-D7
	MOVEM.L	D0-D7,$FF8240

	CLR.W	-(SP)
	MOVE.L	scr_adr,-(SP)
	MOVE.L	(SP),-(SP)
	MOVE.W	#5,-(SP)
	TRAP	#14
	LEA	12(SP),SP

	LEA	PIC+34,A0
	MOVE.L	$44E,A1
	MOVE.W	#160*48/4/4-1,D7
DLP	MOVE.L	(A0)+,(A1)+
	MOVE.L	(A0)+,(A1)+
	MOVE.L	(A0)+,(A1)+
	MOVE.L	(A0)+,(A1)+
	DBRA	D7,DLP

	PEA	TEXT
	MOVE.W	#9,-(SP)
	TRAP	#1
	ADDQ.L	#6,SP

	MOVE.W	#1,$4EE.W

	LEA	old_mfp,A0
	MOVE.B	$FFFFFA07.W,(A0)+
	MOVE.B	$FFFFFA09.W,(A0)+
	MOVE.B	$FFFFFA13.W,(A0)+
	MOVE.B	$FFFFFA15.W,(A0)+
	MOVE.B	$FFFFFA1F.W,(A0)+
	MOVE.B	$FFFFFA19.W,(A0)+
	MOVE.B	$FFFFFA17.W,(A0)+

	CLR.B	$484.W

	MOVE.W	#-1,$468.W
.vsync	TST.W	$468.W
	BNE.S	.vsync

	MOVE.W	#$2700,SR
	MOVE.B	#50,$FFFA21
	MOVE.L	#BORDTB2,$120.W
	LEA	keyvec,A0
	MOVE.L	$118.W,6(A0)
	MOVE.L	$118.W,key_vec
	MOVE.L	A0,$118.W
	MOVE.L	#new_vbl,A0
	MOVE.L	$70.W,2(A0)
	MOVE.L	$70.W,old_vbl
	MOVE.L	#vblank,$70.W
	MOVE.B	#%01000000,$FFFFFA15.W
	MOVE.B	#%00000001,$FFFFFA07.W

	BSET.B	#3,$FFFFFA17.W
	MOVE.W	#$2300,SR
	MOVEQ	#-1,D7
over	NOP
	DBF	D7,over	
	RTS

BORDTB2	MOVE #$2700,SR
	clr.b $fffffa1b.w
	not $ffff8240.w
	nop
	nop
	nop
	not $ffff8240.w
	MOVEM.L D0-D2/A0-A1,-(SP)
	MOVE #$8209,A0
waits	MOVE.B (A0),D0
	beq.s waits
	AND #$3F,D0
	MOVEQ #$3F,D1
	SUB D0,D1
	LSL D1,D0
	CLR.W	$FF8240

	MOVE.L	PALPOS,A3
	DCB.W 70-42-5-5,$4E71

	LEA	$FF8240,A0
	MOVE.W	#228,D2
CLSD	MOVE.W	(A3)+,(A0)

	REPT	7
	ADDQ.W	#1,$FF8240
	ENDR
	REPT	7
	SUBQ.W	#1,$FF8240
	ENDR
	REPT	5
	ADDQ.W	#1,$FF8240
	ENDR
	REPT	5
	SUBQ.W	#1,$FF8240
	ENDR

;	REPT	4
;	NOP
;	ENDR

	DBF	D2,CLSD
 
		movem.l (sp)+,d0-d2/a0-a1
		BCLR	#0,$FFFA0F
		rte

vblank	MOVEM.L	D0-D7/A0-A6,-(SP)

	MOVEM.L	(PIC+2),D0-D7
	MOVEM.L	D0-D7,$FF8240
	CLR.W	$FF8240

	JSR	MUS+8
	JSR	SCROLLER

	MOVE.B	#8,$FFFFFA1B.W
	ORI.B	#1,$FFFFFA07.W
	ORI.B	#1,$FFFFFA13.W
	MOVE.L	D0,-(SP)
	MOVE	SR,-(SP)
	MOVE.W	#$2700,SR
	MOVE.W	(SP)+,SR
	MOVE.L	#BORDTB2,$120.W
*	MOVE.W	#0,$FF8240
.wait3	MOVE.L	(SP)+,D0
	CLR.B	$FFFFFA1B.W
	MOVE.B	#1,$FFFFFA21.W
	MOVE.B	#8,$FFFFFA1B.W

	MOVE.L	PALPOS,A0
	ADDA.L	#2,A0
	CMP.L	#PAL3+100,A0
	BLT	NOS
	MOVE.L	#PAL2,A0
NOS	MOVE.L	A0,PALPOS

	MOVEM.L	(SP)+,D0-D7/A0-A6
new_vbl	JMP	$0.L

keyvec	MOVE.W	#$2500,SR
	JMP	$0.L

ipl_nul	BCLR.B	#5,$FFFFFA0F.W
	RTE

hbl_off	MOVE.W	#$2700,SR
	MOVE.L	key_vec,$118.W
	MOVE.L	old_vbl,$70.W
	MOVE.L	#ipl_nul,$120.W
	LEA	old_mfp,A0
	MOVE.B	(A0)+,$FFFFFA07.W
	MOVE.B	(A0)+,$FFFFFA09.W
	MOVE.B	(A0)+,$FFFFFA13.W
	MOVE.B	(A0)+,$FFFFFA15.W
	MOVE.B	(A0)+,$FFFFFA1F.W
	MOVE.B	(A0)+,$FFFFFA19.W
	MOVE.B	(A0)+,$FFFFFA17.W
	MOVE.W	#$2300,SR
	MOVE.L	#$08080000,$FF8800		; CLEAR CHANNELS
	MOVE.L	#$09090000,$FF8800
	MOVE.L	#$0A0A0000,$FF8800
	RTS

mse_off	DC.B	$12,$00
mse_on	DC.B	$08,$00
	EVEN

PALPOS	DC.L	PAL2

PICPAL2	DC.W	$000,$111,$222,$333,$444,$555,$666,$777
	DC.W	$000,$100,$210,$320,$431,$542,$653,$764

PAL2	DS.W	100
	DC.W	$100,$100,$200,$100,$200,$200,$300,$200,$300,$300,$400,$300,$400,$400,$500
	DC.W	$400,$500,$500,$600,$500,$600,$600,$700,$600,$700
	DC.W	$700,$700,$710,$700,$710,$710,$720,$710,$720,$720,$730,$720,$730,$730,$740
	DC.W	$730,$740,$740,$750,$740,$750,$750,$760,$750,$760,$760,$770,$760,$770,$770
	DC.W	$670,$770,$670,$670,$570,$670,$570,$570,$470,$570,$470,$470,$370,$470,$370
	DC.W	$370,$270,$370,$270,$270,$170,$270,$170,$170,$070,$170,$070,$070,$071,$070
	DC.W	$071,$071,$072,$071,$072,$072,$073,$072,$073,$073,$074,$073,$074,$074,$075
	DC.W	$074,$075,$075,$076,$075,$076,$076,$077,$076,$077,$077,$067,$077,$067,$067
	DC.W	$057,$067,$057,$057,$047,$057,$047,$047,$037,$047,$037,$037,$027,$037,$027
	DC.W	$027,$017,$027,$017,$017,$007,$017,$007,$007,$107,$007,$107,$107,$207,$107
	DC.W	$207,$207,$307,$207,$307,$307,$407,$307,$407,$407,$507,$407,$507,$507,$607
	DC.W	$507,$607,$607,$707,$607,$707,$707,$706,$707,$706,$706,$705,$706,$705,$705
	DC.W	$704,$705,$704,$704,$703,$704,$703,$703,$702,$703,$702,$702,$701,$702,$701
	DC.W	$701,$700,$701,$700
	DC.W	$700,$600,$700,$600,$600,$500,$600,$500,$500,$400,$500,$400,$400,$300,$400
	DC.W	$300,$300,$200,$300,$200,$200,$100,$200,$100,$100

PAL3	DS.W	100
	DC.W	$100,$100,$200,$100,$200,$200,$300,$200,$300,$300,$400,$300,$400,$400,$500
	DC.W	$400,$500,$500,$600,$500,$600,$600,$700,$600,$700
	DC.W	$700,$700,$710,$700,$710,$710,$720,$710,$720,$720,$730,$720,$730,$730,$740
	DC.W	$730,$740,$740,$750,$740,$750,$750,$760,$750,$760,$760,$770,$760,$770,$770
	DC.W	$670,$770,$670,$670,$570,$670,$570,$570,$470,$570,$470,$470,$370,$470,$370
	DC.W	$370,$270,$370,$270,$270,$170,$270,$170,$170,$070,$170,$070,$070,$071,$070
	DC.W	$071,$071,$072,$071,$072,$072,$073,$072,$073,$073,$074,$073,$074,$074,$075
	DC.W	$074,$075,$075,$076,$075,$076,$076,$077,$076,$077,$077,$067,$077,$067,$067
	DC.W	$057,$067,$057,$057,$047,$057,$047,$047,$037,$047,$037,$037,$027,$037,$027
	DC.W	$027,$017,$027,$017,$017,$007,$017,$007,$007,$107,$007,$107,$107,$207,$107
	DC.W	$207,$207,$307,$207,$307,$307,$407,$307,$407,$407,$507,$407,$507,$507,$607
	DC.W	$507,$607,$607,$707,$607,$707,$707,$706,$707,$706,$706,$705,$706,$705,$705
	DC.W	$704,$705,$704,$704,$703,$704,$703,$703,$702,$703,$702,$702,$701,$702,$701
	DC.W	$701,$700,$701,$700
	DC.W	$700,$600,$700,$600,$600,$500,$600,$500,$500,$400,$500,$400,$400,$300,$400
	DC.W	$300,$300,$200,$300,$200,$200,$100,$200,$100,$100

ENDPAL	DC.W	$fff

TEXT    DC.B      13,10 
        DC.B      10
        DC.B      10
        DC.B      10
        DC.B      10
        DC.B      10
        DC.B      10
        DC.B      '           AWESOME BRINGS YOU',13,10 
        DC.B      '           ~~~~~~~~~~~~~~~~~~',13,10 
        DC.B      '    NEW STUFF BY CAMEO OF REPLICANTS',13,10
        DC.B      10
        DC.B      '              1. 3-D SOCCER             '
        DC.B      13,10 
        DC.B      '              2. DERRICK THE DROID+     '
        DC.B      13,10 
        DC.B      '              3. PUZZMANIA+             '
        DC.B      13,10 
        DC.B      10
        DC.B      '    CONTACT US, WRITE TO:-              ',13,10 
        DC.B      '            AWESOME                     ',13,10 
        DC.B      '            P.O BOX 9296                ',13,10
        DC.B      '            109 RVK                     ',13,10 
        DC.B      '            ICELAND                     ',13,10 
        DC.B      0
	EVEN


PIC	incbin	picture.pi1
	even
MUS	incbin	awes.mus
	even

SCROLLER	CMP.W	#16,CCNT
	BLT.S	NOTNEW
	JSR	NEWCHAR
NOTNEW	LEA	CHARBUF,A3
	MOVE.W	#2,D6
SLOOP	MOVE.L	A3,A0
	MOVE.L	$44E.W,A1
	LEA	184*160(A1),A1
	ADDQ.W	#1,CCNT
	MOVE.W	#16-1,D7
SCROLLOOP	LSL.W	(A0)+
	ADDQ.L	#2,A0
ADD	SET	152
	REPT	20
	ROXL	ADD(A1)
ADD	SET	ADD-8
	ENDR
	LEA	160(A1),A1
	DBRA	D7,SCROLLOOP
	DBRA	D6,SLOOP
	RTS

NEWCHAR	MOVEQ	#0,D0
	MOVE.L	MPOS,A0
	MOVE.B	(A0)+,D0
	CMP.B	#$FF,(A0)
	BNE.S	NOTEND
	MOVE.L	#MESSAGE,A0
NOTEND	MOVE.L	A0,MPOS
	CLR.W	CCNT

	LEA	FONTBIN,A0
	LEA	CHARBUF,A1
	SUB.W	#32,D0
	MULU	#32,D0
	ADD.W	D0,A0
	REPT	16
	MOVE.W	(A0)+,(A1)+
	ADDQ.L	#2,A1
	ENDR
	RTS

MPOS	DC.L	MESSAGE
MESSAGE DC.B      '    HIYA DUDES, JUST A QUICK FEW WORDS F'
        DC.B      'ROM RUTHLESS HERE.     THE REASON I HAVE'
        DC.B      ' GOT THIS UP-LOADED IS BECAUSE NOT MUCH '
        DC.B      'REPLICANTS STUFF GETS ON THE BOARDS NOW '
        DC.B      'AND AT THE MOMENT THERE IS A DISTINCT LA'
        DC.B      'CK OF SOFTWARE!    CONTACT US, WRITE TO:'
        DC.B      '- AWESOME, P.O BOX 9296, 109 RVK, ICELAN'
        DC.B      'D.    WE ARE ACTUALLY FROM BRITAIN BUT W'
        DC.B      'E HAVE A GOOD FRIEND IN ICELAND WHO WILL'
        DC.B      ' PASS (ALL) CORRESPONDENCE ON TO US!  HE'
        DC.B      'Y LULU, THANX FOR THE SUPPORT ON YOUR GR'
        DC.B      'EAT B.B.S HOW ABOUT GETTING IN TOUCH?   '
        DC.B      ' ALSO THANX TO AXE OF SUPERIOR FOR GREET'
        DC.B      'ING US PLEASE GET IN TOUCH!!   IN FACT E'
        DC.B      'VERYONE GET IN TOUCH!!!    QUICK GREETZ '
        DC.B      'GOTO:- THE REST OF AWESOME (WHO ARE - TH'
        DC.B      'E EDITMAN, ZELDA, PHOENIX AND DEMON-X), '
        DC.B      'FROSTY, THE MEGA-MIGHTY CAMEO!, LULU (GR'
        DC.B      'EAT B.B.S!), SYNDICATE, DEREK M.D (HERE '
        DC.B      'YOU ARE!), SCOTT FROM MILLION SALOON BAR'
        DC.B      ' (GET IN TOUCH!), GENIE, YUM-YUM, MOB, D'
        DC.B      'OCNO, ZINE AND ALL OTHER COOL DUDES I FO'
        DC.B      'RGOT!   BYEEEEEEE!             ',$ff

FONTPOS	DC.B	$41,$4C,$4C,$20,$43,$4F,$44,$49,$4E,$47,$20,$42,$59,$20,$4D,$41,$47
	DC.B	$4E,$55,$4D,$20,$31,$39,$39,$31,$21
	DCB.B	20,$20
	DC.B	$1,$2,$3,$4,$FF

OKEY	DC.B	0
HZ	DC.B	250

ENV	DC.L	0
FILE	DC.W	0
	EVEN
FILE1	DC.B	'CAMEO1.AWE',0
	EVEN
FILE2	DC.B	'CAMEO2.AWE',0
	EVEN
FILE3	DC.B	'CAMEO3.AWE',0
	EVEN
FILE4	DC.B	'FILE4',0
	EVEN
FILE5	DC.B	'FILE5',0
	EVEN
FILE6	DC.B	'FILE6',0
	EVEN
FILE7	DC.B	'FILE7',0
	EVEN
FILE8	DC.B	'FILE8',0
	EVEN
FILE9	DC.B	'FILE9',0
	EVEN
NAMES	DC.L	FILE1
	DC.L	FILE2
	DC.L	FILE3
	DC.L	FILE4
	DC.L	FILE5
	DC.L	FILE6
	DC.L	FILE7
	DC.L	FILE8
	DC.L	FILE9

BLACK	DS.W	16

FPOS	DC.L	FONTBIN
CCNT	DC.W	25
CHAR	DC.B	0
CHARBUF	DS.L	24
FONTBIN	INCBIN	NORMFONT.16

	SECTION BSS

SAVEPAL	DS.L	8
old_mfp	DS.B	8
old_rez	DS.W	1
key_vec	DS.L	1
old_sp	DS.L	1
old_vbl	DS.L	1
old_scr	DS.L	1
scr_adr	DS.L	1
	DS.L	200
STACK	DS.L	1
big_scr	DS.L	1
