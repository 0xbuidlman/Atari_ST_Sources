' HYPMOD: Manipuliert den NSECS-Parameter im Bootsektor
' Written & Ω 1988 by Claus Brod
'
' Alte HYPERFORMAT-Versionen manipulierten die Anzahl der Sektoren
' (4 zusÑtzliche Sektoren) im Bootsektor, um einen hartnÑckigen
' GEMDOS-Fehler zu egalisieren, der dazu fÅhrt, daû auf allen Medien
' grundsÑtzlich 2 KB zuwenig freier Platz gemeldet wird.
' Diese Manipulation wird jetzt, da langsam fehlerbereinigte TOS-Versionen
' (KAOS) in Sicht kommen, etwas suspekt: Man sollte sie
' rÅckgÑngig machen. Dazu dient dieses Programm. Es erkennt HYPERFORMAT-
' Disketten der alten Bauart (ab HYPERFORMAT 3.08 wird nichts mehr
' getrickst) und korrigiert den betreffenden Wert im Bootsektor.
'
' Wer darauf besteht, kann sich auch eine nicht manipulierte Diskette
' nachtrÑglich umpatchen, um 2 KB mehr zu gewinnen.
'
buf$=STRING$(512,0)             ! Sektorpuffer
DO
  ALERT 1," ------HYPMOD------ | (C) 1988 Claus Brod | Diskette in | Laufwerk... ",drive%+1," A | B |Raus",erg%
  EXIT IF erg%=3
  drive%=erg%-1
  buf=VARPTR(buf$)
  @rdboot(drive%,buf)   ! Bootsektor lesen
  IF fehler%
    ALERT 1,"    Lesen: XBIOS-Fehler    |    "+STR$(fehler%),1,"Ui",erg%
  ELSE
    CLR s%
    FOR t=0 TO 255
      ADD s%,DPEEK(t*2+buf)
    NEXT t
    s%=s% AND 65535
    IF s%=&H1234
      ALERT 1,"    Der Bootsektor der    |    eingelegten Diskette ist |    ausfÅhrbar.",1,"Ach?",erg%
    ENDIF
    buf=VARPTR(buf$)
    spd%=PEEK(buf+19)+PEEK(buf+20)*256
    spt%=PEEK(buf+24)+256*PEEK(buf+25)
    sd%=PEEK(buf+27)*256+PEEK(buf+26)
    spc%=PEEK(buf+13)
    CLR erg%
    IF spd% MOD (spt%*sd%)
      ALERT 2,"    Das ist eine|    HYPERFORMAT-Diskette,    |    die korrigiert|    werden sollte!",1,"Klar|Nie!",erg%
      IF erg%=1
        SUB spd%,2*spc%
      ENDIF
    ELSE
      ALERT 2,"    Das ist eine normale|    Diskette. Wollen Sie sich|    "+STR$(spc%)+" KB zusÑtzlich|    zusammenmanipulieren?",2,"Sicher|Nee",erg%
      IF erg%=1
        ADD spd%,2*spc%
      ENDIF
    ENDIF
    IF erg%=1
      POKE buf+19,spd% AND 255
      POKE buf+20,spd%/256
      VOID XBIOS(18,L:buf,L:-1,-1,-(s%=&H1234))       ! AusfÅhrbar halten
      @wrboot(drive%,buf)
      IF fehler%
        ALERT 1,"    Schreiben: XBIOS-Fehler    |    "+STR$(fehler%),1,"Ui",erg%
      ENDIF
    ENDIF
  ENDIF
LOOP
'
PROCEDURE rdboot(laufwerk%,buf)
  fehler%=XBIOS(8,L:buf,L:0,laufwerk%,1,0,0,1)
RETURN
PROCEDURE wrboot(laufwerk%,buf)
  fehler%=XBIOS(9,L:buf,L:0,laufwerk%,1,0,0,1)
RETURN
