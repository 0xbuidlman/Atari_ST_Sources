' #########################################################################
' Boot-Viren Killer V1.0
' FÅr Scheibenkleister (Massenspeicher am ST/Claus Brod,Anton Stepper)
' von Marcus Klocke-Sewing
'     Gronauer Str.19
'   D-4800 Bielefeld 1
' ##########################################################################
' ACHTUNG: Sollten Sie Viren besitzen, die nicht erkannt werden, dann senden
'          Sie mir diese bitte auf Diskette zu. Ich schicke Ihnen dann
'          eine Programmversion zurÅck, die auch diese Viren erkennt !!!
'
' Folgende Bootspurviren werden erkannt:
' VDU-IMUNE,GHOST,SPEICHER STôRER,MAD(FUN)-VIRUS,BILDSCHIRM VERDUNKLER,
' SIGNUM VIRUS,ARNOLD/RAMBO VIRUS,LAXY VIRUS I,VIRE_87 !
'
' Folgende Immunsektoren werden erkannt:
' ANTI VIREN KIT,SAGROTAN,VIRENTOD & (logisch) BOOT-VIREN KILLER V1.0
'
ALERT 1,"BOOT-VIREN KILLER V1.0|FÅr SCHEIBENKLEISTER|     Massenspeicher am ST|von Marcus Klocke-Sewing",1,"Aha",dummy%
DO
  CLEAR
  ALERT 3,"Diskette zum Åber-|prÅfen bitte in|Floppy A einlegen",1,"Ist drin!|Abbruch",a%
  IF a%=2 THEN
    END
  ENDIF
  DEFMOUSE 2
  sek$=SPACE$(512)
  adr%=VARPTR(sek$)
  a%=XBIOS(8,L:adr%,L:1,0,1,0,0,1)
  IF a%=0 THEN
    IF INSTR(sek$,"VIRE 87") AND INSTR(sek$,"BA") THEN
      ALERT 4,"VIRE 87 gefunden !|Kopiert sich auf A/B|Nicht resetfest|LÑuft nicht unter TOS 1.04 ",1,"AHA",v%
    ENDIF
    IF INSTR(sek$,"LAXY 1") THEN
      ALERT 4,"LAXY 1/Immunsektor TÑuschung |Kopiert sich auf A/B|Nicht resetfest|LÑuft nicht unter TOS 1.04",1,"Aha",v%
    ENDIF
    IF INSTR(sek$,"ARNOLD/RAMBO VIRUS") THEN
      ALERT 4,"ARNOLD/RAMBO-Virus gefunden ! |Kopiert sich auf A/B|Nicht resetfest|LÑuft nicht unter TOS 1.04",1,"Aha",v%
    ENDIF
    IF INSTR(sek$,"Virus") AND INSTR(sek$,"ldA") THEN
      ALERT 4,"Bildschirmverdunkler gefunden! |Kopiert sich auf A/B|Nicht resetfest|Wird nach 10 Kopien aktiv! ",1,"Aha",v%
    ENDIF
    IF INSTR(sek$,"BPL") THEN
      ALERT 4,"BPL/Signum-Virus gefunden ! |Kopiert sich auf A/B|Nicht resetfest|Versch. Versionen im Umlauf ! ",1,"Aha",v%
    ENDIF
    IF INSTR(sek$,"vNuNV") AND INSTR(sek$,"BGBm") THEN
      ALERT 4,"MAD/FUN-Virus gefunden ! |Kopiert sich auf A/B|Nicht resetfest|Erzeugt Bildschirmgags ",1,"Aha",v%
    ENDIF
    IF INSTR(sek$,"Vxa") AND INSTR(sek$,"Bi") THEN
      ALERT 4,"Speicherstîrer gefunden! |Kopiert sich auf A/B|Nicht resetfest|VerÑndert Speicher",1,"Aha",v%
    ENDIF
    IF INSTR(sek$,"adNu") AND INSTR(sek$,"NNT") THEN
      ALERT 4,"GHOST-Virus gefunden ! |Kopiert sich nur auf A|IST resetfest|VerÑndert Mausbewegung ",1,"Aha",v%
    ENDIF
    IF INSTR(sek$,"gRC") AND INSTR(sek$,"Dent") THEN
      ALERT 4,"VDU-Virus gefunden ! |Kopiert sich auf A/B|Nicht resetfest|Lîscht Bootsektor-Lader ",1,"Aha",v%
    ENDIF
    IF INSTR(sek$,"Virus-Report") OR INSTR(sek$,"Anti Viren Kit:") OR INSTR(sek$,"SAGROTAN") THEN
      ALERT 4,"Immunsektor gefunden !|Diskette ist geschÅtzt |gegen Virenbefall !",1,"Gut",dummy%
      geh%=1
    ENDIF
  ELSE
    ALERT 2,"Diskette konnte|nicht gelesen|werden ???",1,"Mist",dummy%
  ENDIF
  IF v% THEN
    ALERT 2,"Die Diskette ist verseucht!|Soll das Virus |gelîscht werden ?",1,"Sicher!|Nein",a%
    IF a%=1 THEN
      DEFMOUSE 2
      MID$(sek$,1)="`8"
      MID$(sek$,30)=SPACE$(479)
      MID$(sek$,113)="Diskette wurde durch den Boot-Viren-Killer V1.0 geheilt!"
      a%=XBIOS(9,L:adr%,L:1,0,1,0,0,1)
      IF a%<>0 THEN
        ALERT 1,"Diskette schreibgeschÅtzt?|Virus wurde NICHT gelîscht ! ",1,"Mist",dummy%
      ELSE
        ALERT 1,"Virus wurde erfolgreich|gelîscht!",1,"Juhu",dummy%
      ENDIF
    ENDIF
  ELSE
    IF INSTR(sek$,"Boot-Viren Killer") THEN
      ALERT 4,"Die Diskette ist VIRENFREI!|Auch wurde sie gegen|Bootviren geschÅtzt !!!",1,"SUPER",dummy
    ELSE
      IF geh%=0 THEN
        ALERT 4,"Die Diskette ist vermutlich|nicht virusbefallen.|Sie ist aber auch nicht|vor Viren geschÅtzt worden !",1,"Aha|Immun",a%
        IF a%=2 THEN
          MID$(sek$,1)="`8"
          MID$(sek$,30)=SPACE$(479)
          MID$(sek$,113)="Diskette wurde durch den Boot-Viren Killer V1.0 geschÅtzt !"
          DEFMOUSE 2
          a%=XBIOS(9,L:adr%,L:1,0,1,0,0,1)
          IF a% THEN
            ALERT 1,"Diskette schreibgeschÅtzt?|Diskette wurde nicht|gegen Viren geimpft !!",1,"Mist",dummy%
          ELSE
            ALERT 1,"Diskette ist nun geschÅtzt !|ACHTUNG: Hilft nicht gegen|alle Viren !!!",1,"OK",dummy%
          ENDIF
        ENDIF
      ENDIF
    ENDIF
  ENDIF
LOOP
